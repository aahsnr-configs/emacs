#+TITLE: Emacs Configuration
#+AUTHOR: Ahsanur Rahman
#+DESCRIPTION: Personal Minimal Emacs Configuration
#+VERSION: 0.45
#+STARTUP: overview
#+PROPERTY: header-args:emacs-lisp :tangle ./init.el :mkdirp yes

* Core Emacs Configuration
** Lexical Binding
#+begin_src emacs-lisp
;;; init.el --- Main Configuration File -*- no-byte-compile: t; lexical-binding: t; -*-
;;; Commentary:
;; v0.3
;; The contents of my whole emacs configuration
#+end_src

** Garbage Collection
#+begin_src emacs-lisp
;; CRITICAL: Restore GC after startup with optimized values
(defvar better-gc-cons-threshold 134217728  ;; 128MB
  "Optimal GC threshold for modern LSP-heavy workflows.
Lower this if you experience freezing, raise if stuttering.")

(defvar better-gc-cons-percentage 0.1
  "GC percentage of heap to trigger collection.")

;; Restore GC settings after startup
(add-hook 'emacs-startup-hook
          (lambda ()
            (setq gc-cons-threshold better-gc-cons-threshold
                  gc-cons-percentage better-gc-cons-percentage)

            ;; FIX: Only restore file handlers if the original variable actually exists
            (when (boundp 'file-name-handler-alist-original)
              (setq file-name-handler-alist file-name-handler-alist-original)
              (makunbound 'file-name-handler-alist-original))

            ;; Report startup time
            (message "Emacs loaded in %.2fs with %d GCs"
                     (float-time (time-subtract after-init-time before-init-time))
                     gcs-done))
          100) ;; Run late to ensure accurate timing

;; Increase GC threshold in minibuffer to prevent slowdowns during completion
(add-hook 'emacs-startup-hook
          (lambda ()
            (defun gc-minibuffer-setup-hook ()
              (setq gc-cons-threshold (* better-gc-cons-threshold 2)))

            (defun gc-minibuffer-exit-hook ()
              (garbage-collect)
              (setq gc-cons-threshold better-gc-cons-threshold))

            (add-hook 'minibuffer-setup-hook #'gc-minibuffer-setup-hook)
            (add-hook 'minibuffer-exit-hook #'gc-minibuffer-exit-hook)))
#+end_src

** Load Path
#+begin_src emacs-lisp
;; Prioritize local lisp directory in load-path
(add-to-list 'load-path (expand-file-name "lisp" user-emacs-directory))
#+end_src

** Package Management
#+begin_src emacs-lisp
(require 'package)

(setq package-archives '(("gnu" . "https://elpa.gnu.org/packages/")
                         ("nongnu" . "https://elpa.nongnu.org/nongnu/")
                         ("melpa" . "https://melpa.org/packages/")))
(package-initialize)
(unless package-archive-contents
  (package-refresh-contents))

(require 'use-package)
(require 'bind-key)

(setq use-package-verbose nil
      use-package-expand-minimally t
      use-package-compute-statistics nil
      use-package-always-ensure t
      use-package-enable-imenu-support t)

#+end_src

** Custom File
#+begin_src emacs-lisp
(setq custom-file (expand-file-name "customs.el" user-emacs-directory))
(add-hook 'after-init-hook (lambda () (load custom-file 'noerror)))
#+end_src

** Emacs Start Up Profiler
Profile startup time with detailed breakdown
#+begin_src emacs-lisp
(use-package esup
  :defer t
  :commands esup
  :config
  (setq esup-depth 0))
#+end_src

** Custom Functions
#+begin_src emacs-lisp
(defun childframe-workable-p ()
  "Whether childframe is workable."
  (and (>= emacs-major-version 26)
       (not noninteractive)
       (or (display-graphic-p)
           (featurep 'tty-child-frames))
       (eq (frame-parameter (selected-frame) 'minibuffer) 't)))

(defun childframe-completion-workable-p ()
  "Whether childframe completion is workable."
  (childframe-workable-p))

(defun icons-displayable-p ()
  "Return non-nil if icons are displayable."
  (or (featurep 'nerd-icons)
      (require 'nerd-icons nil t)))

(defun too-long-file-p ()
  "Check whether the file is too long."
  (or (> (buffer-size) 500000)
      (and (fboundp 'buffer-line-statistics)
           (> (car (buffer-line-statistics)) 10000))))

(defun reload-init-file ()
  "Tangle and reload the Emacs configuration from config.org."
  (interactive)
  (let* ((config-org (expand-file-name "config.org" user-emacs-directory))
         (init-el (expand-file-name "init.el" user-emacs-directory)))
    ;; Check if config.org exists
    (unless (file-exists-p config-org)
      (user-error "Configuration file %s not found!" config-org))
    
    (message "Tangling configuration from %s..." config-org)
    
    ;; Tangle the org file
    (org-babel-tangle-file config-org init-el "emacs-lisp")
    
    (message "Loading %s..." init-el)
    
    ;; Load the tangled init file
    ;; Note: This doesn't fully restart Emacs, so some changes
    ;; (especially to package declarations) may require a restart
    (load-file init-el)
    
    ;; Process any queued Elpaca packages
    (when (fboundp 'elpaca-process-queues)
      (elpaca-process-queues))
    
    (message "Configuration reloaded! Some changes may require an Emacs restart.")))

(defun my/auto-tangle-config-org ()
  "Automatically tangle config.org when saved."
  (when (string-equal (buffer-file-name)
                      (expand-file-name "config.org" user-emacs-directory))
    ;; Dynamic scoping to avoid confirmation prompts
    (let ((org-confirm-babel-evaluate nil))
      (org-babel-tangle))))

(add-hook 'org-mode-hook
          (lambda ()
            (add-hook 'after-save-hook #'my/auto-tangle-config-org nil t)))

(defun selected-region-or-symbol-at-point ()
  "Return the selected region, otherwise return the symbol at point."
  (if (region-active-p)
      (buffer-substring-no-properties (region-beginning) (region-end))
    (thing-at-point 'symbol t)))
#+end_src

** Constants
#+begin_src emacs-lisp
(defconst sys/linuxp
  (eq system-type 'gnu/linux)
  "Are we running on a GNU/Linux system?")

(defconst sys/rootp
  (string-equal "root" (getenv "USER"))
  "Are you using ROOT user?")

(defconst emacs/>=29p
  (>= emacs-major-version 29)
  "Emacs is 29 or above.")

(defconst emacs/>=29.2p
  (and (>= emacs-major-version 29)
       (>= emacs-minor-version 2))
  "Emacs is 29.2 or above.")

(defconst emacs/>=30p
  (>= emacs-major-version 30)
  "Emacs is 30 or above.")

(defconst emacs/>=31p
  (>= emacs-major-version 31)
  "Emacs is 31 or above.")
#+end_src

** Performance Tuning
#+begin_src emacs-lisp
(when (fboundp 'pgtk-use-im-context)
  (add-hook 'after-make-frame-functions
            (lambda (frame)
              (with-selected-frame frame
                (pgtk-use-im-context nil)))))


(setq-default bidi-display-reordering nil
              bidi-paragraph-direction 'left-to-right
              bidi-inhibit-bpa t)

;; Apply to common modes
(defun my/bidi-optimizations ()
  "Apply bidi optimizations for current buffer."
  (setq-local bidi-paragraph-direction 'left-to-right
              bidi-inhibit-bpa t))

(add-hook 'org-mode-hook #'my/bidi-optimizations)
(add-hook 'text-mode-hook #'my/bidi-optimizations)
(add-hook 'prog-mode-hook #'my/bidi-optimizations)

(setq-default long-line-threshold 1000
              large-hscroll-threshold 1000
              syntax-wholeline-max 1000)

(setq undo-limit 800000
      undo-strong-limit 1200000
      undo-outer-limit 120000000)

(setq scroll-conservatively 101
      scroll-margin 0
      scroll-preserve-screen-position t
      auto-window-vscroll nil
      fast-but-imprecise-scrolling t  ;; Trade accuracy for speed
      redisplay-skip-fontification-on-input t)  ;; Skip during rapid input

(setq highlight-nonselected-windows nil)
(setq blink-cursor-mode nil)

(setq frame-resize-pixelwise t)
(setq window-resize-pixelwise nil)

(setq byte-compile-warnings '(not obsolete))
(setq native-comp-async-report-warnings-errors 'silent)

(setq ffap-machine-p-known 'reject)

(setq inhibit-compacting-font-caches t)  ;; Don't compact font cache
#+end_src

** Small Configs
#+begin_src emacs-lisp
;; Move backup files to dedicated directory
(setq backup-directory-alist `(("." . ,(expand-file-name ".backup" user-emacs-directory))))

;; FIX: Explicitly disable bzr, svn, hg to prevent TRAMP errors
(setq vc-handled-backends '(Git))

;; Confirmation settings
(setq confirm-kill-processes nil)  ;; Auto-kill processes on exit

;; Faster echo of keystrokes
(setq echo-keystrokes 0.1)

;; Don't create lockfiles
(setq-default create-lockfiles nil)

;; Compilation settings
(setq-default compilation-always-kill t
              compilation-ask-about-save nil
              compilation-scroll-output t)

;; Suppress redefinition warnings
(setq ad-redefinition-action 'accept)

;; Require final newline
(setq require-final-newline t)

;; Enable commands
(put 'erase-buffer 'disabled nil)

(delete-selection-mode 1)

;; File associations
(add-to-list 'auto-mode-alist '("\\.in\\'" . text-mode))
(add-to-list 'auto-mode-alist '("\\.out\\'" . text-mode))
(add-to-list 'auto-mode-alist '("\\.args\\'" . text-mode))
(add-to-list 'auto-mode-alist '("\\.bb\\'" . shell-script-mode))
(add-to-list 'auto-mode-alist '("\\.bbclass\\'" . shell-script-mode))
(add-to-list 'auto-mode-alist '("\\.Rmd\\'" . markdown-mode))

;; Alt key mapping
(setq x-alt-keysym 'meta)
#+end_src

** Garbage Collector Magic Hack
#+begin_src emacs-lisp
(use-package gcmh
  :hook (after-init . gcmh-mode)
  :init
  (setq gcmh-idle-delay 'auto))
#+end_src

** UTF-8 Coding System
#+begin_src emacs-lisp
(set-selection-coding-system 'utf-8)
(prefer-coding-system 'utf-8)
(set-language-environment "UTF-8")
(set-default-coding-systems 'utf-8)
(set-terminal-coding-system 'utf-8)
(set-keyboard-coding-system 'utf-8)
(setq locale-coding-system 'utf-8)
;; Treat clipboard input as UTF-8 string first; compound text next, etc.
(when (display-graphic-p)
  (setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING)))
#+end_src

** Environment
#+begin_src emacs-lisp
(use-package exec-path-from-shell
  :custom
  (exec-path-from-shell-variables
   '("PATH" "MANPATH"
     "OPENAI_API_KEY" "ANTHROPIC_API_KEY"
     "XAI_API_KEY" "DEEPSEEK_API_KEY"
     "OPENROUTER_API_KEY" "GEMINI_API_KEY"))
  :config
  (when (daemonp)
    (exec-path-from-shell-initialize)))
#+end_src

** Session Management
#+begin_src emacs-lisp
;; Save place
(use-package saveplace
  :ensure nil
  :hook (after-init . save-place-mode))

;; History with symlink resolution
(use-package recentf
  :ensure nil
  :hook (after-init . recentf-mode)
  :init
  (setq recentf-max-saved-items 300
        recentf-exclude
        '("\\.?cache" ".cask" "url" "COMMIT_EDITMSG\\'" "bookmarks"
          "\\.\\(?:gz\\|gif\\|svg\\|png\\|jpe?g\\|bmp\\|xpm\\)$"
          "\\.?ido\\.last$" "\\.revive$" "/G?TAGS$" "/.elfeed/"
          "^/tmp/" "^/var/folders/.+$" "^/ssh:" "/persp-confs/"
          (lambda (file) (file-in-directory-p file package-user-dir))))
  :config
  (push (expand-file-name recentf-save-file) recentf-exclude)
  
  ;; CRITICAL: Resolve symlinks FIRST, then abbreviate
  ;; This ensures ~/org/todo.org and ~/Git/.../org/todo.org become the same
  (setq recentf-filename-handlers
        '(file-truename        ; Resolve symlinks to real path
          abbreviate-file-name)) ; Then abbreviate for display
  
  ;; Prevent org-agenda files from cluttering recent files
  ;; Dashboard opens these files, triggering recentf
  (defun ar/exclude-org-agenda-files (file)
    "Exclude files in org-agenda-files from recentf."
    (when (bound-and-true-p org-agenda-files)
      (let ((file-true (file-truename file)))
        (cl-some (lambda (agenda-file)
                   (string= file-true (file-truename agenda-file)))
                 (org-agenda-files t)))))
  
  (add-to-list 'recentf-exclude #'ar/exclude-org-agenda-files))

(use-package savehist
  :ensure nil
  :hook (after-init . savehist-mode)
  :init
  (setq enable-recursive-minibuffers t
        history-length 1000
        savehist-additional-variables '(mark-ring
                                        global-mark-ring
                                        search-ring
                                        regexp-search-ring
                                        extended-command-history)
        savehist-autosave-interval 300))

#+end_src

** Misc
#+begin_src emacs-lisp
(use-package simple
  :ensure nil
  :hook ((text-mode . visual-line-mode)
         ((prog-mode markdown-mode conf-mode) . enable-trailing-whitespace))
  :init
  (setq column-number-mode t
        line-number-mode t
        kill-whole-line t
        line-move-visual nil
        track-eol t
        set-mark-command-repeat-pop t)

  (setq-default show-trailing-whitespace nil)
  (defun enable-trailing-whitespace ()
    (setq show-trailing-whitespace t)
    (add-hook 'before-save-hook #'delete-trailing-whitespace nil t))

  ;; Prettify the process list
  (with-no-warnings
    (defun my-list-processes--prettify ()
      (when-let* ((entries tabulated-list-entries))
        (setq tabulated-list-entries nil)
        (dolist (p (process-list))
          (when-let* ((val (cadr (assoc p entries)))
                      (name (aref val 0))
                      (pid (aref val 1))
                      (status (aref val 2))
                      (status (list status 'face
                                    (if (memq status '(stop exit closed failed)) 'error 'success)))
                      (buf-label (aref val 3))
                      (tty (list (aref val 4) 'face 'font-lock-doc-face))
                      (thread (list (aref val 5) 'face 'font-lock-doc-face))
                      (cmd (list (aref val 6) 'face 'completions-annotations)))
            (push (list p (vector name pid status buf-label tty thread cmd))
		          tabulated-list-entries)))))
    (advice-add #'list-processes--refresh :after #'my-list-processes--prettify)))

;; Misc Settings
(if (boundp 'use-short-answers)
    (setq use-short-answers t)
  (fset 'yes-or-no-p 'y-or-n-p))

(setq-default major-mode 'text-mode
              fill-column 80
              tab-width 4
              indent-tabs-mode nil)

;; Silence the Bell
(setq visible-bell nil)
(setq ring-bell-function #'ignore)

;; Suppress "End/Beginning of buffer" Messages
(setq command-error-function
      (lambda (data context caller)
        (when (not (memq (car data) '(beginning-of-buffer end-of-buffer)))
          (command-error-default-function data context caller))))

;; Removes <dir> style
(setq uniquify-buffer-name-style 'forward) 

;; Other Misc
(setq inhibit-compacting-font-caches t
      delete-by-moving-to-trash t
      make-backup-files nil
      auto-save-default nil
      adaptive-fill-regexp "[ t]+|[ t]*([0-9]+.|*+)[ t]*"
      adaptive-fill-first-line-regexp "^* *$"
      sentence-end "\\([。！？]\\|……\\|[.?!][]\"')}]*\\($\\|[ \t]\\)\\)[ \t\n]*"
      sentence-end-double-space nil
      word-wrap-by-category t)
#+end_src

** Setup User
#+begin_src emacs-lisp
(setq user-full-name "Ahsanur Rahman"
      user-mail-address "ahsanur041@proton.me")
#+end_src

* Evil
** Undo Fu
#+begin_src emacs-lisp
;; The undo-fu package is a lightweight wrapper around Emacs' built-in undo system, providing more convenient undo/redo functionality.
(use-package undo-fu
  :commands (undo-fu-only-undo
             undo-fu-only-redo
             undo-fu-only-redo-all
             undo-fu-disable-checkpoint)
  :config
  (global-unset-key (kbd "C-z"))
  (global-set-key (kbd "C-z") 'undo-fu-only-undo)
  (global-set-key (kbd "C-S-z") 'undo-fu-only-redo))

;; The undo-fu-session package complements undo-fu by enabling the saving
;; and restoration of undo history across Emacs sessions, even after restarting.
(use-package undo-fu-session
  :commands undo-fu-session-global-mode
  :hook (after-init . undo-fu-session-global-mode))
#+end_src

** Core Evil
#+begin_src emacs-lisp
(setq evil-undo-system 'undo-fu)

;; Vim emulation
(use-package evil
  :commands (evil-mode evil-define-key)
  :hook (after-init . evil-mode)

  :init
  (setq evil-want-integration t)
  (setq evil-want-keybinding nil)

  :custom
  (evil-ex-visual-char-range t)
  (evil-ex-search-vim-style-regexp t)
  (evil-split-window-below t)
  (evil-vsplit-window-right t)
  (evil-echo-state nil)
  (evil-move-cursor-back nil)
  (evil-v$-excludes-newline t)
  (evil-want-C-h-delete t)
  (evil-want-C-u-delete t)
  (evil-want-fine-undo t)
  (evil-move-beyond-eol t)
  (evil-search-wrap nil)
  (evil-want-Y-yank-to-eol t)
  :config
  (evil-select-search-module 'evil-search-module 'isearch))
#+end_src

** Evil Collection
#+begin_src emacs-lisp
(use-package evil-collection
  :after evil
  :init
  ;; It has to be defined before evil-colllection
  (setq evil-collection-setup-minibuffer t)
  :config
  (evil-collection-init))
#+end_src

** Evil Extensions
#+begin_src emacs-lisp
(use-package evil-surround
  :after evil
  :commands global-evil-surround-mode
  :custom
  (evil-surround-pairs-alist
   '((?\( . ("(" . ")"))
     (?\[ . ("[" . "]"))
     (?\{ . ("{" . "}"))

     (?\) . ("(" . ")"))
     (?\] . ("[" . "]"))
     (?\} . ("{" . "}"))

     (?< . ("<" . ">"))
     (?> . ("<" . ">"))))
  :hook (after-init . global-evil-surround-mode))

;; Jump to the last change (Visual Studio/Vim style 'g;')
(use-package goto-chg
  :after evil
  :commands (goto-last-change goto-last-change-reverse)
  :config
  (evil-define-key 'normal 'global (kbd "g;") 'goto-last-change)
  (evil-define-key 'normal 'global (kbd "g,") 'goto-last-change-reverse))

;; Text Objects for arguments (e.g., 'cia' to change inner argument)
(use-package evil-args
  :after evil
  :config
  ;; Bind text objects to 'a' (argument)
  (define-key evil-inner-text-objects-map "a" 'evil-inner-arg)
  (define-key evil-outer-text-objects-map "a" 'evil-outer-arg))

;; Increment/Decrement numbers with C-a/C-x
(use-package evil-numbers
  :after evil
  :config
  (define-key evil-normal-state-map (kbd "C-a") 'evil-numbers/inc-at-pt)
  (define-key evil-normal-state-map (kbd "C-x") 'evil-numbers/dec-at-pt))

;; Quick 2-char jump motions (like Vimium 'f')
(use-package evil-snipe
  :after evil
  :hook (evil-mode . evil-snipe-mode)
  :hook (evil-mode . evil-snipe-override-mode)
  :config
  (setq evil-snipe-smart-case t
        evil-snipe-scope 'buffer
        evil-snipe-repeat-scope 'buffer)
  ;; Make s/S work in visual mode too
  (evil-define-key 'visual evil-snipe-local-mode-map "z" 'evil-snipe-s)
  (evil-define-key 'visual evil-snipe-local-mode-map "Z" 'evil-snipe-S))

;; Swap two regions of text with 'gx'
(use-package evil-exchange
  :after evil
  :config
  (evil-exchange-install))

;; Jump between matching tags/keywords with '%' (better than default)
(use-package evil-matchit
  :after evil
  :config
  (global-evil-matchit-mode 1))

;; Start a * search using the current visual selection
(use-package evil-visualstar
  :after evil
  :config
  (global-evil-visualstar-mode))

;; Visual feedback when editing (pulsing regions on yank/paste)
(use-package evil-goggles
  :after evil
  :config
  (evil-goggles-mode)
  (setq evil-goggles-duration 0.1))

;; Align text with 'gl' (e.g., 'glip=' aligns paragraph on = signs)
(use-package evil-lion
  :after evil
  :config
  (evil-lion-mode))
#+end_src

** Evil Window Movement Enhancements
#+begin_src emacs-lisp
(defcustom ar/evil-want-move-window-to-wrap-around nil
  "If non-nil, window movement commands wrap around."
  :type 'boolean
  :group 'evil)

(defun ar/evil-window-move-left ()
  "Move to window on the left, wrapping if needed."
  (interactive)
  (if (and ar/evil-want-move-window-to-wrap-around
           (not (ignore-errors (windmove-left))))
      (evil-window-bottom-right)
    (windmove-left)))

(defun ar/evil-window-move-right ()
  "Move to window on the right, wrapping if needed."
  (interactive)
  (if (and ar/evil-want-move-window-to-wrap-around
           (not (ignore-errors (windmove-right))))
      (evil-window-top-left)
    (windmove-right)))

(defun ar/evil-window-move-up ()
  "Move to window above, wrapping if needed."
  (interactive)
  (if (and ar/evil-want-move-window-to-wrap-around
           (not (ignore-errors (windmove-up))))
      (evil-window-bottom-right)
    (windmove-up)))

(defun ar/evil-window-move-down ()
  "Move to window below, wrapping if needed."
  (interactive)
  (if (and ar/evil-want-move-window-to-wrap-around
           (not (ignore-errors (windmove-down))))
      (evil-window-top-left)
    (windmove-down)))
#+end_src

** Native Commenting Operator
Replaces evil-nerd-commenter/evil-commentary with native Emacs behavior. This restores the "smart" toggling you prefer.
#+begin_src emacs-lisp
(with-eval-after-load 'evil
  (evil-define-operator ar/evil-comment-operator (beg end type)
    "Toggle comment for the region defined by BEG and END."
    :move-point nil
    (interactive "<R>")
    (comment-or-uncomment-region beg end))

  ;; Bind 'gc' to this operator
  (define-key evil-normal-state-map (kbd "gc") #'ar/evil-comment-operator)
  ;; Bind 'gc' in visual mode to ensure it works on selections
  (define-key evil-visual-state-map (kbd "gc") #'ar/evil-comment-operator))
#+end_src

** Smart Comment Continuation
#+begin_src emacs-lisp
(defcustom ar/evil-want-o/O-to-continue-comments t
  "If non-nil, o/O keys will continue comment lines."
  :type 'boolean
  :group 'evil)

(defun ar/evil-open-below-smart ()
  "Open line below. If in a comment, continue it using Emacs native logic."
  (interactive)
  (if (and ar/evil-want-o/O-to-continue-comments
           (save-excursion (comment-beginning)))
      (progn
        (end-of-line)
        ;; Delegates to Emacs' native 'M-j' behavior, which handles
        ;; every language's comment syntax (;;, //, #, *) perfectly.
        (comment-indent-new-line)
        (evil-insert-state))
    (evil-open-below 1)))

(defun ar/evil-open-above-smart ()
  "Open line above. If in a comment, continue it using Emacs native logic."
  (interactive)
  (if (and ar/evil-want-o/O-to-continue-comments
           (save-excursion (comment-beginning)))
      (progn
        (beginning-of-line)
        ;; Split the current comment line from above
        (comment-indent-new-line)
        ;; Move to the upper line (the new one)
        (forward-line -1)
        (indent-according-to-mode)
        (evil-insert-state))
    (evil-open-above 1)))

(with-eval-after-load 'evil
  (define-key evil-normal-state-map "o" #'ar/evil-open-below-smart)
  (define-key evil-normal-state-map "O" #'ar/evil-open-above-smart))
#+end_src

* Child Frame
#+begin_src emacs-lisp
(use-package posframe
  :init
  (defface posframe-border
    `((t (:inherit region)))
    "Face used by the `posframe' border."
    :group 'posframe)
  (defvar posframe-border-width 1
    "Default posframe border width.")
  :config
  (with-no-warnings
    (defun my-posframe--prettify-frame (&rest _)
      (set-face-background 'fringe nil posframe--frame))
    (advice-add #'posframe--create-posframe :after #'my-posframe--prettify-frame)

    (defun posframe-poshandler-frame-center-near-bottom (info)
      (cons (/ (- (plist-get info :parent-frame-width)
                  (plist-get info :posframe-width))
               2)
            (/ (+ (plist-get info :parent-frame-height)
                  (* 2 (plist-get info :font-height)))
               2)))))
#+end_src

* Remap C-g to Escape
#+begin_src emacs-lisp
(keymap-set key-translation-map "C-g" "<escape>")
(keymap-global-set "C-M-g" #'keyboard-quit)
#+end_src

* Hydra Setup
#+begin_src emacs-lisp
(use-package hydra
  :defer t
  :hook (emacs-lisp-mode . hydra-add-imenu)
  :init
  (setq hydra-hint-display-type 'lv))

(use-package major-mode-hydra
  :defer t
  :bind ("<f6>" . toggles-hydra/body)
  :hook (emacs-lisp-mode . (lambda ()
                             (add-to-list
                              'imenu-generic-expression
                              '("Hydras"
                                "^.*(\\(pretty-hydra-define\\) \\([a-zA-Z-]+\\)"
                                2))))
  :init
  (cl-defun pretty-hydra-title (title &optional icon-type icon-name
                                      &key face height v-adjust)
    "Add an icon in the hydra title."
    (let ((face (or face 'mode-line-emphasis))
          (height (or height 1.2))
          (v-adjust (or v-adjust 0.0)))
      (concat
       (when (and (display-graphic-p)
                  (or (featurep 'nerd-icons) (require 'nerd-icons nil t))
                  icon-type icon-name)
         (let ((f (intern (format "nerd-icons-%s" icon-type))))
           (when (fboundp f)
             (concat
              (apply f (list icon-name :face face :height height :v-adjust v-adjust))
              " "))))
       (propertize title 'face face))))

  ;; Global toggles
  (with-no-warnings
    (pretty-hydra-define toggles-hydra 
      (:title (pretty-hydra-title "Toggles" 'faicon "nf-fa-toggle_on")
       :color amaranth 
       :quit-key ("q" "C-g"))
      
      ("Basic"
       (("n" (cond ((fboundp 'display-line-numbers-mode)
                    (display-line-numbers-mode (if display-line-numbers-mode -1 1)))
                   ((fboundp 'global-linum-mode)
                    (global-linum-mode (if global-linum-mode -1 1))))
         "line number"
         :toggle (or (bound-and-true-p display-line-numbers-mode)
                     (bound-and-true-p global-linum-mode)))
        ("a" global-aggressive-indent-mode "aggressive indent" 
         :toggle t)
        ("d" global-hungry-delete-mode "hungry delete" 
         :toggle t)
        ("e" electric-pair-mode "electric pair" 
         :toggle t)
        ("c" flyspell-mode "spell check" 
         :toggle t)
        ("s" prettify-symbols-mode "pretty symbol" 
         :toggle t)
        ("l" global-page-break-lines-mode "page break lines" 
         :toggle t)
        ("b" display-battery-mode "battery" 
         :toggle t)
        ("i" display-time-mode "time" 
         :toggle t)
        ("m" (if (fboundp 'doom-modeline-mode)
                 (doom-modeline-mode (if doom-modeline-mode -1 1))
               (message "doom-modeline not available"))
         "modern mode-line" 
         :toggle (and (fboundp 'doom-modeline-mode) doom-modeline-mode)))
       
       "Highlight"
       (("h l" global-hl-line-mode "line" 
         :toggle t)
        ("h p" show-paren-mode "paren" 
         :toggle t)
        ("h s" (if (fboundp 'symbol-overlay-mode)
                   (symbol-overlay-mode (if symbol-overlay-mode -1 1))
                 (message "symbol-overlay not available"))
         "symbol" 
         :toggle (and (fboundp 'symbol-overlay-mode) symbol-overlay-mode))
        ("h r" (if (fboundp 'rainbow-mode)
                   (rainbow-mode (if rainbow-mode -1 1))
                 (message "rainbow-mode not available"))
         "rainbow" 
         :toggle (and (fboundp 'rainbow-mode) rainbow-mode))
        ("h w" (setq-default show-trailing-whitespace (not show-trailing-whitespace))
         "whitespace" 
         :toggle show-trailing-whitespace)
        ("h d" (if (fboundp 'rainbow-delimiters-mode)
                   (rainbow-delimiters-mode (if rainbow-delimiters-mode -1 1))
                 (message "rainbow-delimiters not available"))
         "delimiter" 
         :toggle (and (fboundp 'rainbow-delimiters-mode) rainbow-delimiters-mode))
        ("h i" (if (fboundp 'highlight-indent-guides-mode)
                   (highlight-indent-guides-mode (if highlight-indent-guides-mode -1 1))
                 (message "highlight-indent-guides not available"))
         "indent" 
         :toggle (and (fboundp 'highlight-indent-guides-mode) 
                      highlight-indent-guides-mode))
        ("h t" (if (fboundp 'global-hl-todo-mode)
                   (global-hl-todo-mode (if global-hl-todo-mode -1 1))
                 (message "hl-todo not available"))
         "todo" 
         :toggle (and (fboundp 'global-hl-todo-mode) global-hl-todo-mode)))
       
       "Program"
       (("f" flymake-mode "flymake" 
         :toggle t)
        ("O" hs-minor-mode "hideshow" 
         :toggle t)
        ("u" subword-mode "subword" 
         :toggle t)
        ("W" which-function-mode "which function" 
         :toggle t)
        ("E" toggle-debug-on-error "debug on error" 
         :toggle (default-value 'debug-on-error))
        ("Q" toggle-debug-on-quit "debug on quit" 
         :toggle (default-value 'debug-on-quit))
        ("v" (if (fboundp 'global-diff-hl-mode)
                 (global-diff-hl-mode (if global-diff-hl-mode -1 1))
               (message "diff-hl not available"))
         "gutter" 
         :toggle (and (fboundp 'global-diff-hl-mode) global-diff-hl-mode))
        ("V" (if (fboundp 'diff-hl-flydiff-mode)
                 (diff-hl-flydiff-mode (if diff-hl-flydiff-mode -1 1))
               (message "diff-hl-flydiff not available"))
         "live gutter" 
         :toggle (and (fboundp 'diff-hl-flydiff-mode) diff-hl-flydiff-mode))
        ("M" (if (fboundp 'diff-hl-margin-mode)
                 (diff-hl-margin-mode (if diff-hl-margin-mode -1 1))
               (message "diff-hl-margin not available"))
         "margin gutter" 
         :toggle (and (fboundp 'diff-hl-margin-mode) diff-hl-margin-mode))
        ("D" (if (fboundp 'diff-hl-dired-mode)
                 (diff-hl-dired-mode (if diff-hl-dired-mode -1 1))
               (message "diff-hl-dired not available"))
         "dired gutter" 
         :toggle (and (fboundp 'diff-hl-dired-mode) diff-hl-dired-mode)))
       
       "Theme"
       (("t d" (load-theme 'modus-operandi t) "light (modus-operandi)" 
         :exit t)
        ("t k" (load-theme 'modus-vivendi t) "dark (modus-vivendi)" 
         :exit t)
        ("t w" (load-theme 'whiteboard t) "whiteboard" 
         :exit t)
        ("t t" (load-theme 'tango t) "tango" 
         :exit t)
        ("t g" (load-theme 'tango-dark t) "tango-dark" 
         :exit t)
        ("t l" (load-theme 'leuven t) "leuven" 
         :exit t)
        ("t a" (load-theme 'adwaita t) "adwaita" 
         :exit t)
        ("t D" (load-theme 'deeper-blue t) "deeper-blue" 
         :exit t)
        ("t w" (load-theme 'wombat t) "wombat" 
         :exit t)
        ("t o" (call-interactively 'load-theme) "other..." 
         :exit t)
        ("t c" (call-interactively 'customize-themes) "customize themes" 
         :exit t)
        ("t u" (mapc #'disable-theme custom-enabled-themes) "unload all" 
         :exit t))
       
       "Package"
       (("p r" package-refresh-contents "refresh archives" 
         :exit t)
        ("p l" package-list-packages "list packages" 
         :exit t)
        ("p i" package-install "install package" 
         :exit t)
        ("p d" package-delete "delete package" 
         :exit t)
        ("p a" package-autoremove "autoremove" 
         :exit t))))))
#+end_src

* Editor Behaviour
** Documentation
#+begin_src comment
=============================================================================
  ELDOC ARCHITECTURE & BREADCRUMB INTEGRATION GUIDE
=============================================================================

1. GLOBAL BEHAVIOR & PERFORMANCE
   - eldoc-idle-delay (0.1s): 
     * What: The time Emacs waits after a cursor movement before showing docs.
     * Set: via :custom in the eldoc use-package block.
     * Debug: If docs feel "laggy," increase this. If they feel "slow," decrease.
   - eldoc-echo-area-use-multiline-p (2): 
     * What: Allows ElDoc to expand the echo area up to 2 lines.
     * Set: via :custom.
     * Debug: Check variable value if docs are being cut off prematurely.
   - eldoc-documentation-strategy (eldoc-documentation-compose):
     * What: Tells ElDoc to combine results from ALL active backends (e.g., LSP 
       + Breadcrumbs) rather than just picking the first one.
     * Debug: Check 'eldoc-documentation-functions' to see which backends are active.

2. UI & MANUAL INTERACTION
   - ar/show-eldoc-buffer (C-h .):
     * What: A safe wrapper around 'eldoc-doc-buffer'. It forces a full buffer
       to open with complete documentation.
     * Debug: If C-h . shows nothing, the current backend lacks a "long-form" 
       doc string. Check the *Messages* buffer for error logs.

3. ORG-MODE BREADCRUMBS (ar/get-org-breadcrumb-string)
   - What: A structural context engine. It calculates the heading hierarchy
     (Parent › Current) using 'org-get-outline-path'.
   - Specific Logic:
     a) EOB Check: Detects if you are at the bottom of the file.
     b) Src-Block Check: Returns nil inside code blocks so that LSP/ElDoc can 
        show function signatures instead of headings.
     c) Face Application: Uses 'shadow' for parents and 'bold-keyword' for 
        the current heading to provide visual depth.
   - Debug: Run (ar/get-org-breadcrumb-string) in M-x ielm while in an Org 
     file to see the raw string generated.

4. GENERIC CONTEXT FALLBACK (ar/generic-eldoc-backend)
   - What: When not in Org-mode and not using LSP, this uses 'which-function'
     to show which class/function your cursor is currently inside.
   - Dependency: Requires 'which-function-mode' to be 1.
   - Debug: If context doesn't appear in programming modes, verify 
     'which-function-mode' is active.

5. HOVER SUPPORT (eldoc-mouse)
   - What: Displays ElDoc documentation in a floating childframe when the 
     mouse hovers over a symbol.
   - Set: Enabled via 'eldoc-mouse-mode' hook.
   - Integration: Customizes 'posframe-border' to match your UI theme and 
     disables standard tooltips to prevent "double popups."
   - Debug: Check if 'childframe-workable-p' returns T. Ensure 'posframe' 
     is installed.

6. DEBUGGING BACKEND PRIORITY
   - Backend priority is managed via 'add-hook' depth values:
     * Org Breadcrumbs: -100 (Highest - always runs first).
     * Generic Context: 0 (Default - runs if others aren't managing).
   - Debug: Run 'C-h v eldoc-documentation-functions' in any buffer to see 
      the execution order.
=============================================================================
#+end_src

** Eldoc
#+begin_src emacs-lisp
;; Dependency: Enable which-function-mode globally
(use-package which-func
  :ensure nil
  :config
  ;; Enable globally so we can calculate context
  (which-function-mode 1)
  ;; Hide it from the modeline (we only want it in Echo Area)
  (setq which-func-format ""))

(use-package eldoc
  :ensure nil
  :defer t
  :commands (eldoc-mode global-eldoc-mode)
  :hook ((after-init . global-eldoc-mode)
         (emacs-lisp-mode . eldoc-mode)
         (lisp-interaction-mode . eldoc-mode))
  :custom
  (eldoc-idle-delay 0.1)
  (eldoc-echo-area-use-multiline-p 2)
  (eldoc-documentation-strategy 'eldoc-documentation-compose)
  (eldoc-echo-area-display-truncation-message nil)
  
  :config
  ;; === SAFE DOC BUFFER TOGGLE ===
  (defun ar/show-eldoc-buffer ()
    "Attempt to display the full ElDoc buffer."
    (interactive)
    (condition-case nil
        (eldoc-doc-buffer t)
      (error (message "No full documentation available at point"))))

  (global-set-key (kbd "C-h .") #'ar/show-eldoc-buffer)

  ;; === ORG MODE BREADCRUMBS ===
  (defun ar/get-org-breadcrumb-string ()
    "Generate breadcrumb string: Parent › Current.
    Priority Order: Bottom Level > Src Block (Nil) > Hierarchy > Top Level."
    (when (derived-mode-p 'org-mode)
      (let* ((face-sep '(:inherit shadow :height 0.8))
             (face-path '(:inherit shadow))
             (face-current '(:inherit font-lock-keyword-face :weight bold))
             (sep-str (propertize " › " 'face face-sep)))

        (cond
         ;; 1. Check Bottom Level FIRST (Highest Priority)
         ;; Checks if we are at EOB or on the last line of the buffer
         ((or (eobp)
              (save-excursion (forward-line 1) (eobp)))
          (propertize "Bottom Level" 'face face-path))

         ;; 2. Check Src Block (Return nil to let LSP/ElDoc handle it)
         ((org-in-src-block-p)
          nil)

         ;; 3. Check Hierarchy
         ((let ((path (ignore-errors (org-get-outline-path t))))
            (when path
              (mapconcat
               (lambda (heading)
                 (if (string= heading (car (last path)))
                     (propertize heading 'face face-current)
                   (propertize heading 'face face-path)))
               path
               sep-str))))

         ;; 4. Default to Top Level
         (t 
          (propertize "Top Level" 'face face-path))))))

  (defun ar/org-eldoc-backend (callback &rest _ignored)
    "ElDoc backend for Org Breadcrumbs."
    (let ((doc (ar/get-org-breadcrumb-string)))
      (when doc
        (funcall callback doc))))

  ;; === GENERIC CONTEXT (Fallback) ===
  (defun ar/generic-eldoc-backend (callback &rest _ignored)
    "ElDoc backend for Generic Context (which-function)."
    (when (and (not (derived-mode-p 'org-mode))
               (not (bound-and-true-p lsp-mode))
               (not (bound-and-true-p eglot--managed-mode))
               (fboundp 'which-function))
      (let ((current-ctx (which-function)))
        (when current-ctx
          (funcall callback
                   (concat
                    (propertize "⚙ " 'face 'font-lock-function-name-face)
                    (propertize current-ctx 'face 'font-lock-function-name-face)))))))

  ;; === HOOK REGISTRATION ===
  (add-hook 'org-mode-hook
            (lambda ()
              (setq-local eldoc-documentation-strategy 'eldoc-documentation-compose)
              (add-hook 'eldoc-documentation-functions #'ar/org-eldoc-backend -100 t)))

  (add-hook 'eldoc-mode-hook
            (lambda ()
              (add-hook 'eldoc-documentation-functions #'ar/generic-eldoc-backend 0 t))))

;; Enable eldoc-mouse (hover support)
(when (childframe-workable-p)
  (use-package eldoc-mouse
    :hook (eldoc-mode . eldoc-mouse-mode)
    :init
    (setq eldoc-mouse-posframe-border-color (face-background 'posframe-border nil t))
    :config
    (tooltip-mode -1)
    (add-to-list 'eldoc-mouse-posframe-override-parameters
                 `(background-color . ,(face-background 'tooltip nil t)))))
#+end_src

** Cross Referencing Commands
#+begin_src emacs-lisp
(use-package xref
  :ensure nil
  :defer t
  :autoload xref-show-definitions-completing-read
  :bind (("M-g ." . xref-find-definitions)
         ("M-g ," . xref-go-back))
  :init
  ;; Use faster search tool
  (when (executable-find "rg")
    (setq xref-search-program 'ripgrep))

  ;; Select from xref candidates in minibuffer
  (setq xref-show-definitions-function #'xref-show-definitions-completing-read
        xref-show-xrefs-function #'xref-show-definitions-completing-read))
#+end_src

** Automatically reload files was modified by external program
#+begin_src emacs-lisp
(use-package autorevert
  :ensure nil
  :hook (after-init . global-auto-revert-mode))
#+end_src

** Avy/Ace
#+begin_src emacs-lisp
;; Jump to things in Emacs tree-style
(use-package avy
  :hook (after-init . avy-setup-default)
  :config (setq avy-all-windows nil
                avy-all-windows-alt t
                avy-background t
                avy-style 'pre))

;; Kill text between the point and the character CHAR
(use-package avy-zap
  :defer t)

;; Quickly follow links
(use-package ace-link
  :hook (after-init . ace-link-setup-default))
#+end_src

** Anzu
#+begin_src emacs-lisp
(use-package anzu
  :bind (([remap query-replace] . anzu-query-replace)
         ([remap query-replace-regexp] . anzu-query-replace-regexp)
         :map isearch-mode-map
         ([remap isearch-query-replace] . anzu-isearch-query-replace)
         ([remap isearch-query-replace-regexp] . anzu-isearch-query-replace-regexp))
  :hook (after-init . global-anzu-mode))

(use-package evil-anzu :after evil)
#+end_src

** Ediff
#+begin_src emacs-lisp
(use-package ediff
  :defer t
  :ensure nil
  :hook(;; show org ediffs unfolded
        (ediff-prepare-buffer . outline-show-all)
        ;; restore window layout when done
        (ediff-quit . winner-undo))
  :config
  (setq ediff-window-setup-function 'ediff-setup-windows-plain
        ediff-split-window-function 'split-window-horizontally
        ediff-merge-split-window-function 'split-window-horizontally))
#+end_src

** Automatic parenthesis pairing
#+begin_src emacs-lisp
(use-package elec-pair
  :ensure nil
  :hook (after-init . electric-pair-mode)
  :init (setq electric-pair-inhibit-predicate 'electric-pair-conservative-inhibit))
#+end_src

** Edit multiple regions simultaneously
#+begin_src emacs-lisp
(use-package iedit :defer t)
#+end_src

** Subword: Handling capitalized subwords in a nomenclature
#+begin_src emacs-lisp
(use-package subword
  :ensure nil
  :hook ((prog-mode . subword-mode)
         (minibuffer-setup . subword-mode)))
#+end_src

** Sudo edit
#+begin_src emacs-lisp
(use-package sudo-edit :defer t)
#+end_src

** Move Text
#+begin_src emacs-lisp
(use-package move-text
  :defer t
  :config
  (defun ar/indent-after-move-text (&rest ignored)
    "Indent the current line or region after moving, preserving mark state."
    (let ((deactivate deactivate-mark))
      (if (region-active-p)
          (indent-region (region-beginning) (region-end))
        (indent-region (line-beginning-position) (line-end-position)))
      (setq deactivate-mark deactivate)))

  (advice-add 'move-text-up :after #'ar/indent-after-move-text)
  (advice-add 'move-text-down :after #'ar/indent-after-move-text))
#+end_src

** Terminal
#+begin_src emacs-lisp
(use-package eshell
  :ensure nil
  :defer t
  :commands (eshell ar/eshell-toggle ar/eshell-new)
  :hook ((eshell-mode . (lambda ()
                          ;; Disable line numbers
                          (when (bound-and-true-p display-line-numbers-mode)
                            (display-line-numbers-mode -1))
                          ;; Corfu manual completion only
                          (setq-local corfu-auto nil))))
  :custom
  ;; Kill processes without confirmation
  (eshell-kill-processes-on-exit t)
  (eshell-destroy-buffer-when-process-dies nil)
  
  ;; Remove banner
  (eshell-banner-message "")
  
  ;; Scrolling
  (eshell-scroll-to-bottom-on-input 'this)
  (eshell-scroll-show-maximum-output t)
  (eshell-scroll-to-bottom-on-output nil)
  
  ;; History
  (eshell-history-size 10000)
  (eshell-hist-ignoredups t)
  (eshell-save-history-on-exit t)
  
  ;; Buffer management
  (eshell-buffer-maximum-lines 20000)
  
  ;; Visual commands (programs requiring terminal emulation)
  (eshell-visual-commands
   '("top" "htop" "btop" "less" "more" "vim" "vi" "nano" "emacs"
     "ssh" "tail" "watch" "ncdu" "ranger" "htop" "tmux" "screen"
     "python" "ipython" "node" "ruby" "irb"))
  (eshell-visual-subcommands
   '(("git" "log" "diff" "show" "help")
     ("docker" "run" "exec" "logs" "attach")
     ("podman" "run" "exec" "logs" "attach")))
  (eshell-visual-options
   '(("git" "--help" "--paginate")))
  
  ;; Prefer Lisp implementations
  (eshell-prefer-lisp-functions t)
  (eshell-prefer-lisp-variables t)
  
  :config
  ;; Suppress PAGER
  (setenv "PAGER" "cat")
  
  ;; === TOGGLE FUNCTION ===
  (defun ar/eshell-toggle ()
    "Toggle eshell popup window. Works from any Evil state."
    (interactive)
    (let* ((eshell-buffer-name "*eshell*")
           (eshell-buffer (get-buffer eshell-buffer-name))
           (eshell-window (and eshell-buffer (get-buffer-window eshell-buffer))))
      (if eshell-window
          ;; If visible, hide it
          (progn
            (delete-window eshell-window)
            (message "Eshell hidden"))
        ;; Otherwise, show it
        (let ((display-buffer-alist
               '(("\\*eshell\\*"
                  (display-buffer-at-bottom)
                  (window-height . 0.33)
                  (dedicated . t)))))
          (if eshell-buffer
              (pop-to-buffer eshell-buffer)
            (eshell))
          (message "Eshell opened")))))
  
  ;; === NEW ESHELL INSTANCE ===
  (defun ar/eshell-new ()
    "Create a new eshell buffer."
    (interactive)
    (let ((buf (generate-new-buffer "*eshell*")))
      (with-current-buffer buf
        (eshell-mode))
      (pop-to-buffer buf)))
  
  ;; === CLEAR BUFFER ===
  (defun ar/eshell-clear ()
    "Clear the eshell buffer."
    (interactive)
    (let ((inhibit-read-only t))
      (erase-buffer)
      (eshell-send-input)))
  
  ;; === ALIASES ===
  (defalias 'eshell/clear #'ar/eshell-clear)
  (defalias 'eshell/ff #'find-file)
  (defalias 'eshell/fo #'find-file-other-window)
  (defalias 'eshell/d #'dired)
  (defalias 'eshell/e #'find-file)
  (defalias 'eshell/eh #'find-file-other-window)
  (defalias 'eshell/v #'view-file)
  (defalias 'eshell/l #'eshell/ls)
  (defalias 'eshell/ll #'eshell/ls)
  
  ;; === GIT BRANCH HELPER ===
  (defun ar/eshell-git-branch ()
    "Get current git branch if in repository."
    (when (and (not (file-remote-p default-directory))
               (executable-find "git")
               (locate-dominating-file default-directory ".git"))
      (let ((branch (string-trim
                     (shell-command-to-string 
                      "git rev-parse --abbrev-ref HEAD 2>/dev/null"))))
        (if (and branch (not (string-empty-p branch)))
            (concat " " (propertize (concat "(" branch ")")
                                   'face '(:foreground "#7aa2f7")))
          ""))))
  
  ;; === CUSTOM PROMPT ===
  (setq eshell-prompt-function
        (lambda ()
          (concat
           (propertize (abbreviate-file-name (eshell/pwd))
                      'face '(:foreground "#bb9af7"))
           (ar/eshell-git-branch)
           (propertize (if (zerop (user-uid)) " # " " $ ")
                      'face '(:foreground "#9ece6a" :weight bold)))))
  
  (setq eshell-prompt-regexp "^[^#$\n]* [#$] "))

;; === GLOBAL TOGGLE KEYBINDING (works in ALL states) ===
(global-set-key (kbd "C-c e") #'ar/eshell-toggle)

;; === COMPLETION WITH CAPE ===
(with-eval-after-load 'eshell
  (with-eval-after-load 'cape
    (defun ar/eshell-setup-completion ()
      "Setup completion for eshell."
      (setq-local completion-at-point-functions
                  (list (cape-capf-super
                         #'pcomplete-completions-at-point
                         #'cape-file))))
    (add-hook 'eshell-mode-hook #'ar/eshell-setup-completion)))

;; === HISTORY WITH CONSULT ===
(with-eval-after-load 'eshell
  (with-eval-after-load 'consult
    (defun ar/eshell-consult-history ()
      "Browse eshell history with Consult."
      (interactive)
      (require 'em-hist)
      (let* ((start-pos (save-excursion (eshell-bol) (point)))
             (end-pos (point))
             (input (buffer-substring-no-properties start-pos end-pos))
             (history (delete-dups
                      (mapcar (lambda (x) (substring-no-properties x))
                              (ring-elements eshell-history-ring)))))
        (let ((selected (consult--read history
                                      :prompt "History: "
                                      :initial input
                                      :sort nil
                                      :category 'eshell-history)))
          (when selected
            (delete-region start-pos end-pos)
            (insert selected)))))))

;; === EVIL INTEGRATION ===
(with-eval-after-load 'evil
  (with-eval-after-load 'eshell
    ;; Insert state bindings
    (evil-define-key 'insert eshell-mode-map
      (kbd "C-r") #'ar/eshell-consult-history
      (kbd "M-r") #'ar/eshell-consult-history
      (kbd "C-d") #'eshell-delchar-or-maybe-eof
      (kbd "C-a") #'eshell-bol
      (kbd "C-e") #'end-of-line
      (kbd "C-u") #'eshell-kill-input)
    
    ;; Normal state bindings
    (evil-define-key 'normal eshell-mode-map
      (kbd "C-r") #'ar/eshell-consult-history
      (kbd "M-r") #'ar/eshell-consult-history
      (kbd "C-d") #'eshell-life-is-too-much
      (kbd "C-l") #'ar/eshell-clear)))
#+end_src

* UI and Theming
** Fonts
#+begin_src emacs-lisp
(defun ar/set-fonts ()
  "Set the default, fixed-pitch, and variable-pitch fonts for the current frame."
  (set-face-attribute 'default nil :font "JetBrains Mono" :height 125 :weight 'medium)
  (set-face-attribute 'fixed-pitch nil :font "JetBrains Mono" :height 125 :weight 'medium)
  (set-face-attribute 'variable-pitch nil :font "JetBrains Mono" :height 125 :weight 'medium)
  ;; Apply italic slant to comments and keywords
  (set-face-attribute 'font-lock-comment-face nil :slant 'italic)
  (set-face-attribute 'font-lock-keyword-face nil :slant 'italic))

(if (daemonp)
    (add-hook 'emacs-startup-hook
              (lambda ()
                (add-hook 'after-make-frame-functions
                          (lambda (frame)
                            (with-selected-frame frame (ar/set-fonts))))))
  ;; For non-daemon mode, set fonts immediately
  (ar/set-fonts))

;; Line spacing
(setq-default line-spacing 0.02)

;; Font settings
(setq font-lock-maximum-decoration t
      inhibit-compacting-font-caches t)
#+end_src

** Title
#+begin_src emacs-lisp
(setq frame-title-format '("Emacs - %b")
      icon-title-format frame-title-format)
#+end_src

** Doom Themes
#+begin_src emacs-lisp
(use-package doom-themes
  :custom
  (doom-themes-enable-bold t)
  (doom-themes-enable-italic t)
  (doom-themes-treemacs-theme "doom-atom")
  :config
  (load-theme 'doom-tokyo-night t)
  (doom-themes-neotree-config)
  (doom-themes-treemacs-config)
  (doom-themes-org-config))
#+end_src

** Solaire Mode
#+begin_src emacs-lisp
(use-package solaire-mode
  ;; :ensure (:host github 
  ;;          :repo "hlissner/emacs-solaire-mode"
  ;;          :ref "c9334666bd208f3322e6118d30eba1b2438e2bb9")
  :hook (after-init . solaire-global-mode))
#+end_src

** Modeline
#+begin_src emacs-lisp
(size-indication-mode -1)
(setq-default mode-line-percent-position nil)

(use-package doom-modeline
  :defer 0.1
  :hook (after-init . doom-modeline-mode)
  :hook (doom-modeline-mode . column-number-mode)
  :init
  (setq doom-modeline-bar-width 3
        doom-modeline-github nil
        doom-modeline-mu4e nil
        doom-modeline-persp-name nil
        doom-modeline-minor-modes nil
        doom-modeline-major-mode-icon t
        doom-modeline-icon t
        doom-modeline-buffer-file-name-style 'relative-from-project)
  :config
  (setq doom-modeline-height 25
        doom-modeline-column-zero-based nil
        doom-modeline-bar-width 3
        doom-modeline-buffer-file-size nil
        doom-modeline-buffer-encoding nil
        doom-modeline-percent-position nil
        doom-modeline-vcs-icon t
        doom-modeline-vcs-max-length 0))
        #+end_src

** Hide Modeline
#+begin_src emacs-lisp
(use-package hide-mode-line
  :autoload turn-off-hide-mode-line-mode
  :hook (((eat-mode org-agenda-mode
           eshell-mode shell-mode
           term-mode vterm-mode
           embark-collect-mode lsp-ui-imenu-mode
           pdf-annot-list-mode) . turn-on-hide-mode-line-mode)))
#+end_src

** Nerd Icons
#+begin_src emacs-lisp
(use-package nerd-icons
  :custom
  (nerd-icons-font-family "Symbols Nerd Font Mono")
  (nerd-icons-color-icons t)
  :config
  (setq inhibit-compacting-font-caches t))
#+end_src

** Dashboard
#+begin_src emacs-lisp
(use-package dashboard
  :init
  (setq dashboard-banner-logo-title "Welcome to Emacs!")
  (setq dashboard-startup-banner 'logo)
  (setq dashboard-items '((recents   . 10)
                         (bookmarks . 3)
                         (projects  . 6)
                         (agenda    . 5)))
  
  ;; CRITICAL: Close org files after reading them for agenda
  (setq dashboard-agenda-release-buffers t)
  
  (setq dashboard-center-content t)
  (setq dashboard-set-heading-icons t)
  (setq dashboard-set-file-icons t)
  (setq dashboard-icon-type 'nerd-icons)
  
  :config
  ;; CRITICAL FIX: Suppress "Followed link to" messages in daemon mode
  (defun ar/dashboard-setup-startup-hook-quiet ()
    "Setup dashboard but suppress messages during daemon startup."
    (if (daemonp)
        ;; In daemon mode: suppress all messages during dashboard init
        (let ((inhibit-message t)
              (message-log-max nil))  ; Don't even log to *Messages*
          (dashboard-setup-startup-hook))
      ;; Normal mode: show messages as usual
      (dashboard-setup-startup-hook)))
  
  (ar/dashboard-setup-startup-hook-quiet)
  
  ;; FIX: Use get-buffer-create instead of get-buffer to avoid nil error
  ;; This creates the buffer if it doesn't exist
  ;; Also handle command-line files gracefully
  (setq initial-buffer-choice
        (lambda ()
          ;; If files were passed on command line, don't force dashboard
          (if (> (length command-line-args) 2)
              nil
            (get-buffer-create "*dashboard*")))))
#+end_src

** Line Numbers
#+begin_src emacs-lisp
(use-package display-line-numbers
  :ensure nil
  :hook ((prog-mode
          conf-mode toml-ts-mode
          yaml-mode yaml-ts-mode)
         . display-line-numbers-mode)
  :init (setq display-line-numbers-width-start t))
#+end_src

** Display dividers between windows
#+begin_src emacs-lisp
(setq window-divider-default-places t
      window-divider-default-bottom-width 1
      window-divider-default-right-width 1)
(add-hook 'window-setup-hook #'window-divider-mode)
#+end_src

** Text Scaling
#+begin_src emacs-lisp
(use-package default-text-scale
  :hook (after-init . default-text-scale-mode))
#+end_src

** Scrolling
#+begin_src emacs-lisp
;; Scroll one line at a time (less "jumpy" than defaults)
(setq hscroll-step 1
      hscroll-margin 2
      scroll-step 1
      scroll-margin 0
      scroll-conservatively 100000
      scroll-preserve-screen-position t
      auto-window-vscroll nil
      ;; mouse
      mouse-wheel-scroll-amount-horizontal 1
      mouse-wheel-progressive-speed nil)

;; Smooth scrolling
(when (fboundp 'pixel-scroll-precision-mode) ;; 29+
  (use-package ultra-scroll
    :functions (hl-todo-mode diff-hl-flydiff-mode)
    :hook (after-init . ultra-scroll-mode)
    :config
    (add-hook 'ultra-scroll-hide-functions #'diff-hl-flydiff-mode)
    (add-hook 'ultra-scroll-hide-functions #'hl-todo-mode)
    (add-hook 'ultra-scroll-hide-functions #'jit-lock-mode)))

#+end_src

* Org Mode
** Dynamic Directory Structure
#+begin_src emacs-lisp
;; Define base directory with symlinks resolved
(defvar my/org-directory (file-truename (expand-file-name "~/org/"))
  "Base directory for all org files (symlinks resolved).")

;; Define subdirectories relative to base
(defvar my/org-subdirs
  '("roam" "downloads" "noter" "archive"
    "roam/projects" "roam/literature" "roam/ideas" "roam/zettel"
    "attachments" "reviews" "backups")
  "List of subdirectories to create under `my/org-directory'.")

;; Helper function to get org subdirectory paths
(defun my/org-subdir (subdir)
  "Return full path for SUBDIR under `my/org-directory'."
  (expand-file-name subdir my/org-directory))

;; Lazy directory creation - only when needed
(defun my/ensure-org-dir (subdir)
  "Ensure SUBDIR exists under `my/org-directory'."
  (let ((dir (my/org-subdir subdir)))
    (unless (file-directory-p dir)
      (make-directory dir t))
    dir))

;; Create all directories at startup
(mapc #'my/ensure-org-dir my/org-subdirs)

;; Define convenience variables
(defvar my/org-roam-directory (my/org-subdir "roam/"))
(defvar my/org-downloads-directory (my/org-subdir "downloads/"))
(defvar my/org-noter-directory (my/org-subdir "noter/"))
(defvar my/org-archive-directory (my/org-subdir "archive/"))
#+end_src

** Better Font Faces
#+begin_src emacs-lisp
(defun ar/org-font-setup ()
  ;; Replace list hyphen with dot
  (font-lock-add-keywords 'org-mode
                          '(("^ *\\([-]\\) "
                             (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "•"))))))

  ;; Set faces for heading levels
  (dolist (face '((org-level-1 . 1.20)
                  (org-level-2 . 1.13)
                  (org-level-3 . 1.10)
                  (org-level-4 . 1.07)
                  (org-level-5 . 1.05)
                  (org-level-6 . 1.03)
                  (org-level-7 . 1.02)
                  (org-level-8 . 1)))
    (set-face-attribute (car face) nil :font "JetBrains Mono" :weight 'bold :height (cdr face))))
#+end_src

** Core Configuration
#+begin_src emacs-lisp
(use-package org
  :ensure t
  :pretty-hydra
  ;; See `org-structure-template-alist'
  ((:title (pretty-hydra-title "Org Template" 'sucicon "nf-custom-orgmode" :face 'nerd-icons-green)
           :color blue :quit-key ("q" "C-g"))
   ("Basic"
    (("a" (hot-expand "<a") "ascii")
     ("c" (hot-expand "<c") "center")
     ("C" (hot-expand "<C") "comment")
     ("x" (hot-expand "<e") "example")
     ("E" (hot-expand "<E") "export")
     ("h" (hot-expand "<h") "html")
     ("l" (hot-expand "<l") "latex")
     ("n" (hot-expand "<n") "note")
     ("o" (hot-expand "<q") "quote")
     ("v" (hot-expand "<v") "verse"))
    "Head"
    (("i" (hot-expand "<i") "index")
     ("A" (hot-expand "<A") "ASCII")
     ("I" (hot-expand "<I") "INCLUDE")
     ("H" (hot-expand "<H") "HTML")
     ("L" (hot-expand "<L") "LaTeX"))
    "Source"
    (("s" (hot-expand "<s") "src")
     ("e" (hot-expand "<s" "emacs-lisp") "emacs-lisp")
     ("y" (hot-expand "<s" "python") "python")
     ("w" (hot-expand "<s" "powershell") "powershell")
     ("r" (hot-expand "<s" "ruby") "ruby")
     ("S" (hot-expand "<s" "sh") "sh")
     ("g" (hot-expand "<s" "go :imports '\(\"fmt\"\)") "golang"))
    "Misc"
    (("m" (hot-expand "<s" "mermaid :file chart.png") "mermaid")
     ("u" (hot-expand "<s" "plantuml :file chart.png") "plantuml")
     ("Y" (hot-expand "<s" "ipython :session :exports both :results raw drawer\n$0") "ipython")
     ("P" (progn
            (insert "#+HEADERS: :results output :exports both :shebang \"#!/usr/bin/env perl\"\n")
            (hot-expand "<s" "perl")) "Perl tangled")
     ("<" self-insert-command "ins"))))
  :bind (:map org-mode-map
              ("<" . (lambda ()
                       "Insert org template."
                       (interactive)
                       (if (or (region-active-p) (looking-back "^\s*" 1))
                           (org-hydra/body)
                         (self-insert-command 1)))))
  
  :hook ((org-mode . visual-line-mode)
         (org-mode . ar/org-font-setup)
         (org-mode . auto-fill-mode)
         (org-mode . (lambda () (setq-local yas-parents '(latex-mode))))
         (org-agenda-mode . (lambda ()
                              (visual-line-mode -1)
                              (toggle-truncate-lines 1)
                              (display-line-numbers-mode -1)
                              (setq mode-line-format nil)
                              (setq header-line-format nil)))
         (org-capture-mode . (lambda ()
                               (setq mode-line-format nil)
                               (setq header-line-format nil))))
  
  :custom
  (org-modules nil)
  (org-directory my/org-directory)
  (org-log-done 'time)
  (org-pretty-entities t)
  (org-log-into-drawer t)
  (org-return-follows-link t)
  (org-src-fontify-natively t)
  (org-element-use-cache t)
  (org-hide-leading-stars t)
  (org-hide-emphasis-markers t)
  (org-element-cache-persistent t)
  (org-fontify-whole-heading-line t)
  ;; (org-fontify-quote-and-verse-blocks nil)
  ;; (org-fontify-whole-heading-line nil)
  ;; (org-fontify-done-headline nil)
  (org-highlight-latex-and-related nil)
  (org-src-preserve-indentation t)
  (org-edit-src-content-indentation 0)
  (org-startup-folded 'overview)
  (org-startup-with-inline-images nil)
  (org-startup-with-latex-preview nil)
  (org-cycle-separator-lines 2)
  
  (org-todo-keywords
   '((sequence "TODO(t)" "NEXT(n)" "PROG(p)" "WAIT(w@/!)" "|" "DONE(d!)" "CANCEL(c@)")
	 (sequence "PLAN(P)" "ACTIVE(A)" "PAUSED(x)" "|" "ACHIEVED(a)" "DROPPED(D)")))
  
  (org-todo-keyword-faces
   '(("TODO"      . (:foreground "#f7768e" :weight bold))
     ("NEXT"      . (:foreground "#ff9e64" :weight bold))
     ("PROG"      . (:foreground "#7aa2f7" :weight bold))
     ("WAIT"      . (:foreground "#e0af68" :weight bold))
     ("DONE"      . (:foreground "#9ece6a" :weight bold))
     ("CANCEL"    . (:foreground "#565f89" :weight bold))
     ("PLAN"      . (:foreground "#7dcfff" :weight bold))
     ("ACTIVE"    . (:foreground "#bb9af7" :weight bold))
     ("PAUSED"    . (:foreground "#a9b1d6" :weight bold))
     ("ACHIEVED"  . (:foreground "#9ece6a" :weight bold))
     ("DROPPED"   . (:foreground "#565f89" :weight bold))))

  (org-tag-alist '(("@work"      . ?w)
		           ("@home"      . ?h)
		           ("@computer"  . ?c)
		           ("@errands"   . ?e)
		           ("read"       . ?r)
		           ("meeting"    . ?m)
		           ("urgent"     . ?u)
		           ("someday"    . ?s)))
  
  
  (org-priority-faces '((?A . error)
                        (?B . warning)
                        (?C . success)))
  
  ;; Agenda styling
  (org-agenda-files (list my/org-directory))
  (org-agenda-block-separator ?─)
  (org-agenda-time-grid
   '((daily today require-timed)
     (800 1000 1200 1400 1600 1800 2000)
     " ┄┄┄┄┄ " "┄┄┄┄┄┄┄┄┄┄┄┄┄┄┄"))
  (org-agenda-current-time-string
   "⭠ now ─────────────────────────────────────────────────")
  (org-agenda-inhibit-startup t)
  (org-agenda-dim-blocked-tasks nil)
  (org-agenda-use-tag-inheritance nil)
  (org-agenda-ignore-properties '(effort appt category))
  (org-agenda-span 'day)
  (org-tags-column -80)

  :config
  (defun hot-expand (str &optional mod)
    (let (text)
      (when (region-active-p)
        (setq text (buffer-substring (region-beginning) (region-end)))
        (delete-region (region-beginning) (region-end)))
      (insert str)
      (if (fboundp 'org-try-structure-completion)
          (org-try-structure-completion) ; < org 9
        (progn
          ;; New template expansion since org 9
          (require 'org-tempo nil t)
          (org-tempo-complete-tag)))
      (when mod (insert mod) (forward-line))
      (when text (insert text))))
  
  ;; Babel
  (setq org-confirm-babel-evaluate nil
        org-src-fontify-natively t
        org-src-tab-acts-natively t)
  
  (defconst load-language-alist
    '((emacs-lisp . t)
      (perl       . t)
      (python     . t)
      (ruby       . t)
      (js         . t)
      (css        . t)
      (sass       . t)
      (C          . t)
      (java       . t)
      (shell      . t)
      (plantuml   . t))
    "Alist of org ob languages.")
  
  (org-babel-do-load-languages 'org-babel-load-languages
                               load-language-alist)
  
  (add-to-list 'org-structure-template-alist '("sh" . "src shell"))
  (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
  (add-to-list 'org-structure-template-alist '("py" . "src python"))
  (add-to-list 'org-structure-template-alist '("jpy" . "src jupyter-python"))
  (add-to-list 'org-structure-template-alist '("tex" . "src latex")))
#+end_src

** Org Roam
#+begin_src emacs-lisp
;; Org-Roam Configuration
(when (and (fboundp 'sqlite-available-p) (sqlite-available-p))
  (use-package org-roam
    :defer t
    :functions org-roam-db-autosync-enable
    :defines org-roam-graph-viewer
    :init
    (setq org-roam-directory (expand-file-name "roam/" my/org-directory))
    :custom
    (org-roam-completion-everywhere t)
    (org-roam-node-display-template
     (concat "${title:*} "
             (propertize "${tags:20}" 'face 'org-tag)))
    :config
    (add-to-list 'org-agenda-files org-roam-directory)

    ;; Delay database sync until idle
    (run-with-idle-timer 5 nil #'org-roam-db-autosync-enable)
 

    ;; Templates for different kinds of notes (Zettelkasten)
    (setq org-roam-capture-templates
          '(("d" "default" plain "%?"
             :target (file+head "${slug}.org"
                                "#+title: ${title}\n#+filetags: \n\n")
             :unnarrowed t)

            ("p" "project" plain "* Goal\n\n%?\n\n* Tasks\n\n* Notes\n\n* Log\n"
             :target (file+head "projects/${slug}.org"
                                "#+title: Project: ${title}\n#+filetags: project\n")
             :unnarrowed t)

            ("l" "literature note" plain "* Source\n- Author: %?\n- Title: \n- Year: \n\n* Summary\n\n* Key Takeaways\n\n* Quotes\n"
             :target (file+head "literature/${slug}.org"
                                "#+title: ${title}\n#+filetags: literature\n")
             :unnarrowed t)

            ("i" "idea" plain "* %?"
             :target (file+head "ideas/${slug}.org"
                                "#+title: ${title}\n#+filetags: idea fleeting\n")
             :unnarrowed t)

            ("z" "zettel" plain "* %?\n\n* References\n\n"
             :target (file+head "zettel/${slug}.org"
                                "#+title: ${title}\n#+filetags: zettel permanent\n")
             :unnarrowed t)))

    ;; Dailies configuration
    (setq org-roam-dailies-directory "daily/"
          org-roam-dailies-capture-templates
          '(("d" "default" entry "* %?"
             :target (file+head "%<%Y-%m-%d>.org"
                                "#+title: %<%Y-%m-%d %A>\n\n"))

            ("j" "journal" entry "* %<%H:%M> - Journal\n\n%?"
             :target (file+head "%<%Y-%m-%d>.org"
                                "#+title: %<%Y-%m-%d %A>\n\n"))

            ("m" "meeting" entry "* %<%H:%M> - %^{Meeting Title}\n\n** Attendees\n- %?\n\n** Notes\n\n** Action Items\n"
             :target (file+head "%<%Y-%m-%d>.org"
                                "#+title: %<%Y-%m-%d %A>\n\n"))))))

(use-package org-roam-ui
  :after org-roam
  :commands (org-roam-ui-mode org-roam-ui-open)
  :custom
  (org-roam-ui-sync-theme t)
  (org-roam-ui-follow t)
  (org-roam-ui-update-on-save t)
  (org-roam-ui-open-on-start nil))

(use-package consult-org-roam
   :after org-roam
   :init
   (require 'consult-org-roam)
   ;; Activate the minor mode
   (consult-org-roam-mode 1)
   :custom
   ;; Use `ripgrep' for searching with `consult-org-roam-search'
   (consult-org-roam-grep-func #'consult-ripgrep)
   ;; Configure a custom narrow key for `consult-buffer'
   (consult-org-roam-buffer-narrow-key ?r)
   ;; Display org-roam buffers right after non-org-roam buffers
   ;; in consult-buffer (and not down at the bottom)
   (consult-org-roam-buffer-after-buffers t)
   :config
   ;; Eventually suppress previewing for certain functions
   (consult-customize
    consult-org-roam-forward-links
    :preview-key "M-."))
#+end_src

** Org Capture
#+begin_src emacs-lisp
(defun my/find-org-projects ()
  "Find all org files in the projects directory."
  (directory-files (expand-file-name "projects" my/org-directory) t "\\.org$"))

(use-package org-capture
  :defer t
  :ensure nil
  :after org
  :custom
  (org-capture-templates
   `(("t" "Task" entry
      (file+headline ,(expand-file-name "inbox.org" my/org-directory) "Tasks")
      "* TODO %?\n:PROPERTIES:\n:CREATED: %U\n:END:\n")

     ("n" "Note" entry
      (file+headline ,(expand-file-name "inbox.org" my/org-directory) "Notes")
      "* %? :note:\n:PROPERTIES:\n:CREATED: %U\n:SOURCE:\n:END:\n")

     ("j" "Journal" entry
      (file+olp+datetree ,(expand-file-name "journal.org" my/org-directory))
      "* %U %?\n")

     ("m" "Meeting" entry
      (file+headline ,(expand-file-name "inbox.org" my/org-directory) "Meetings")
      "* Meeting: %? :meeting:\n:PROPERTIES:\n:CREATED: %U\n:ATTENDEES:\n:END:\n** Agenda\n** Notes\n** Action Items\n")

     ("p" "Project" entry
      (file+headline ,(expand-file-name "projects.org" my/org-directory) "Projects")
      "* PLAN %? :project:\n:PROPERTIES:\n:CREATED: %U\n:GOAL:\n:DEADLINE:\n:END:\n** Goals\n** Tasks\n*** TODO Define project scope\n** Resources\n** Notes\n")

     ("P" "Project Task" entry
      (file (lambda ()
              (let* ((files (my/find-org-projects))
                     (file (completing-read "Select Project: "
                                           (mapcar #'file-name-nondirectory files)
                                           nil t)))
                (car (seq-filter
                      (lambda (f) (string= (file-name-nondirectory f) file))
                      files)))))
      "* TODO %?\n:PROPERTIES:\n:CREATED: %U\n:END:\n"
      :prepend t
      :headline "Tasks")

     ("b" "Book" entry
      (file+headline ,(expand-file-name "reading.org" my/org-directory) "Reading List")
      "* %? :book:read:\n:PROPERTIES:\n:CREATED: %U\n:AUTHOR:\n:GENRE:\n:PAGES:\n:STARTED:\n:FINISHED:\n:RATING:\n:END:\n** Summary\n** Key Takeaways\n** Quotes\n")

     ("h" "Habit" entry
      (file+headline ,(expand-file-name "habits.org" my/org-directory) "Habits")
      "* TODO %? :habit:\nSCHEDULED: %(format-time-string \"<%Y-%m-%d %a .+1d>\")\n:PROPERTIES:\n:CREATED: %U\n:STYLE: habit\n:END:\n")

     ("g" "Goal" entry
      (file+headline ,(expand-file-name "goals.org" my/org-directory) "Goals")
      "* GOAL %? :goal:\nDEADLINE: %(org-read-date nil nil \"+1y\")\n:PROPERTIES:\n:CREATED: %U\n:TYPE:\n:END:\n** Why this goal?\n** Success criteria\n** Action steps\n*** TODO Break down into smaller tasks\n** Resources needed\n** Potential obstacles\n** Progress tracking\n"))))
#+end_src

** Org Habit
#+begin_src emacs-lisp
(use-package org-habit
  :ensure nil
  :defer t
  :after org
  :custom
  (org-habit-graph-column 60)
  (org-habit-show-habits-only-for-today t)
  (org-habit-preceding-days 21)
  (org-habit-following-days 7)
  (org-habit-completed-glyph
   (string-to-char (nerd-icons-codicon "nf-cod-check")))
  (org-habit-today-glyph
   (string-to-char (nerd-icons-codicon "nf-cod-circle_filled"))))
#+end_src

** Org Download
#+begin_src emacs-lisp
(use-package org-download
  :defer t
  :after org
  :custom
  (org-download-screenshot-method "grim -g \"$(slurp)\" - | swappy -f - -o -")
  (org-download-image-dir "assets")
  (org-download-method 'attach)
  (org-download-timestamp "%Y-%m-%d-%H%M%S_")
  (org-download-display-inline t)
  (org-image-actual-width 600)
  :config
  (advice-add 'org-download-dnd :after #'org-download-display-inline-images))
#+end_src

** Org Modern
#+begin_src emacs-lisp
(use-package org-modern
  :hook (org-mode . org-modern-mode)
  :config
  (setq org-modern-hide-stars nil
        org-modern-star '("◉" "○" "◈" "◇" "◆" "▷")
        org-modern-list '((43 . "➤") (45 . "–") (42 . "•"))
        org-modern-table-vertical 1
        org-modern-table-horizontal 0.1
        org-modern-block-name
        '(("src" "»" "«")
          ("example" "»" "«")
          ("quote" """ """))

        org-modern-todo-faces
        '(("TODO"      . (:foreground "#f7768e" :weight bold))
          ("NEXT"      . (:foreground "#ff9e64" :weight bold))
          ("PROG"      . (:foreground "#7aa2f7" :weight bold))
          ("WAIT"      . (:foreground "#e0af68" :weight bold))
          ("DONE"      . (:background "#20293a" :foreground "#9ece6a" :weight bold))
          ("CANCEL"    . (:strike-through t :foreground "#565f89"))
          ("PLAN"      . (:foreground "#7dcfff" :weight bold))
          ("ACTIVE"    . (:foreground "#bb9af7" :weight bold))
          ("PAUSED"    . (:foreground "#a9b1d6" :weight bold))
          ("ACHIEVED"  . (:background "#20293a" :foreground "#9ece6a" :weight bold :box t))
          ("DROPPED"   . (:strike-through t :foreground "#565f89")))

        org-modern-tag-faces
        `((:foreground "#a9b1d6" :weight bold :box (:line-width (1 . -1) :color "#414868")))
        org-modern-checkbox '((todo . "☐") (done . "☑") (cancel . "☑") (priority . "⚑") (on . "◉") (off . "◯"))))
#+end_src

** Org Src Buffer Naming
#+begin_src emacs-lisp
;; Rename the org-edit-special buffer to a static "src code"
;; to save modeline space.
(defun ar/org-src-simplify-buffer-name (base-buffer-name lang)
  "Return static buffer name 'src code' for Org source blocks."
  "src code")

;; Override the internal Org function
(advice-add 'org-src--construct-edit-buffer-name :override #'ar/org-src-simplify-buffer-name)
#+end_src

** Org Pomodoro
#+begin_src emacs-lisp
(use-package org-pomodoro
  :defer t
  :config
  ;; configure pomodoro alerts to use growl or libnotify
  (alert-add-rule :category "org-pomodoro"
                  :style (cond (alert-growl-command
                                'growl)
                               (alert-notifier-command
                                'notifier)
                               (alert-libnotify-command
                                'libnotify)
                               (alert-default-style))))
#+end_src

** Org GTD
#+begin_src emacs-lisp
(use-package org-gtd
  :ensure t
  :after org
  :demand t
  :init
  ;; Suppress upgrade warnings (must be set before package loads)
  (setq org-gtd-update-ack "4.0.0")
  ;; Where org-gtd will keep its files (defaults to ~/gtd/)
  ;; (setq org-gtd-directory "~/my-gtd/")

  :custom
  ;; Configure TODO keyword states (options like "TODO(t)" or "DONE(d!)" are fine)
  (org-todo-keywords '((sequence "TODO" "NEXT" "WAIT" "|" "DONE" "CNCL")))

  ;; Map GTD semantic states to your keywords
  (org-gtd-keyword-mapping '((todo . "TODO")
                             (next . "NEXT")
                             (wait . "WAIT")
                             (canceled . "CNCL")))

  :config
  ;; REQUIRED: Enable org-edna for project dependencies
  (org-edna-mode 1)

  ;; Add org-gtd files to your agenda (must be in :config so org-gtd-directory is defined)
  (setq org-agenda-files (list org-gtd-directory))

  :bind
  ;; Global keybindings (work anywhere in Emacs)
  (("C-c d c" . org-gtd-capture)
   ("C-c d e" . org-gtd-engage)
   ("C-c d p" . org-gtd-process-inbox)
   ("C-c d n" . org-gtd-show-all-next)
   ("C-c d s" . org-gtd-reflect-stuck-projects)

   ;; Keybinding for organizing items (only works in clarify buffers)
   :map org-gtd-clarify-mode-map
   ("C-c c" . org-gtd-organize)

   ;; Quick actions on tasks in agenda views (optional but recommended)
   :map org-agenda-mode-map
   ("C-c ." . org-gtd-agenda-transient)))
#+end_src

* Treesitter/Folding
** Treesit
#+begin_src emacs-lisp
(use-package treesit
  :ensure nil
  :mode (("\\.tsx\\'" . tsx-ts-mode))
  :init
  (defvar ar/treesit-grammar-dir
    (file-truename (expand-file-name "tree-sitter/" user-emacs-directory))
    "Target directory for installing tree-sitter grammars.")

  (unless (file-exists-p ar/treesit-grammar-dir)
    (make-directory ar/treesit-grammar-dir t))

  (add-to-list 'treesit-extra-load-path ar/treesit-grammar-dir)

  :config
  (setq treesit-font-lock-level 4)

  (setq treesit-language-source-alist
        '((python "https://github.com/tree-sitter/tree-sitter-python")
          (bash "https://github.com/tree-sitter/tree-sitter-bash")
          (dockerfile "https://github.com/camdencheek/tree-sitter-dockerfile.git")
          (json "https://github.com/tree-sitter/tree-sitter-json")
          (nix "https://github.com/nix-community/tree-sitter-nix")
          (typescript "https://github.com/tree-sitter/tree-sitter-typescript" "master" "typescript/src")
          (tsx "https://github.com/tree-sitter/tree-sitter-typescript" "master" "tsx/src")))

  (setq major-mode-remap-alist
        '((python-mode . python-ts-mode)
          (json-mode . json-ts-mode)
          (js-mode . js-ts-mode)
          (typescript-mode . typescript-ts-mode)
          (conf-toml-mode . toml-ts-mode))))
#+end_src

** Treesit-Fold
#+begin_src emacs-lisp
(use-package treesit-fold
  :vc (:url "https://github.com/emacs-tree-sitter/treesit-fold"
       :branch "master")
  :custom
  (treesit-fold-indicators-priority -1)
  :config
  (global-treesit-fold-mode 1)
  (global-treesit-fold-indicators-mode 1))
#+end_src

** Evil Text Objects
#+begin_src emacs-lisp
(use-package evil-textobj-tree-sitter
  :after evil
  :config
  ;; Bindings for function objects (next/prev)
  (define-key evil-normal-state-map (kbd "]f")
              (lambda () (interactive) (evil-textobj-tree-sitter-goto-textobj "function.outer")))
  (define-key evil-normal-state-map (kbd "[f")
              (lambda () (interactive) (evil-textobj-tree-sitter-goto-textobj "function.outer" t)))
  (define-key evil-normal-state-map (kbd "]F")
              (lambda () (interactive) (evil-textobj-tree-sitter-goto-textobj "function.outer" nil t)))
  (define-key evil-normal-state-map (kbd "[F")
              (lambda () (interactive) (evil-textobj-tree-sitter-goto-textobj "function.outer" t t)))

  ;; Bind text objects (inner/outer)
  ;; 'f' for function, 'c' for class
  (define-key evil-inner-text-objects-map "f" (evil-textobj-tree-sitter-get-textobj "function.inner"))
  (define-key evil-outer-text-objects-map "f" (evil-textobj-tree-sitter-get-textobj "function.outer"))
  (define-key evil-inner-text-objects-map "c" (evil-textobj-tree-sitter-get-textobj "class.inner"))
  (define-key evil-outer-text-objects-map "c" (evil-textobj-tree-sitter-get-textobj "class.outer"))
  
  ;; Arguments (requires 'parameter' query support in the language grammar)
  (define-key evil-inner-text-objects-map "a" (evil-textobj-tree-sitter-get-textobj "parameter.inner"))
  (define-key evil-outer-text-objects-map "a" (evil-textobj-tree-sitter-get-textobj "parameter.outer")))
#+end_src

** Hideshow
Standard native folding, kept as fallback; disabling for now
#+begin_src emacs-lisp
;; (use-package hideshow
;;   :ensure nil
;;   :hook (prog-mode . hs-minor-mode)
;;   :config
;;   (defun hs-cycle (&optional level)
;;     (interactive "p")
;;     (let (message-log-max (inhibit-message t))
;;       (if (= level 1)
;;           (pcase last-command
;;             ('hs-cycle (hs-hide-level 1) (setq this-command 'hs-cycle-children))
;;             ('hs-cycle-children (save-excursion (hs-show-block)) (setq this-command 'hs-cycle-subtree))
;;             ('hs-cycle-subtree (hs-hide-block))
;;             (_ (if (not (hs-already-hidden-p)) (hs-hide-block) (hs-hide-level 1) (setq this-command 'hs-cycle-children))))
;;         (hs-hide-level level) (setq this-command 'hs-hide-level))))
  
;;   (defun hs-toggle-all () 
;;     (interactive) 
;;     (pcase last-command 
;;       ('hs-toggle-all (save-excursion (hs-show-all)) (setq this-command 'hs-global-show)) 
;;       (_ (hs-hide-all)))))
#+end_src

** Vimish Fold
#+begin_src emacs-lisp
;; (use-package vimish-fold
;;   :after evil
;;   :config (vimish-fold-global-mode 1))

;; (use-package evil-vimish-fold
;;   :after vimish-fold
;;   :init
;;   (setq evil-vimish-fold-mode-lighter " ⮒")
;;   (setq evil-vimish-fold-target-modes '(prog-mode conf-mode text-mode))
;;   :config (global-evil-vimish-fold-mode))
#+end_src

* Highlight
** Highlight the current line
#+begin_src emacs-lisp
(use-package hl-line
  :ensure nil
  :hook ((after-init . global-hl-line-mode)
         ((dashboard-mode eshell-mode shell-mode term-mode vterm-mode) .
          (lambda () (setq-local global-hl-line-mode nil))))
  :config
  ;; CRITICAL FIX: Disable hl-line in Evil Visual mode. 
  ;; Otherwise, the current line highlight masks the selection, making it look 
  ;; like the selection starts "late" or is broken.
  (defun ar/disable-hl-line-in-visual ()
    (when (bound-and-true-p hl-line-mode)
      (hl-line-mode -1)
      (setq-local ar/hl-line-was-on t)))

  (defun ar/enable-hl-line-after-visual ()
    (when (bound-and-true-p ar/hl-line-was-on)
      (hl-line-mode 1)
      (kill-local-variable 'ar/hl-line-was-on)))

  (add-hook 'evil-visual-state-entry-hook #'ar/disable-hl-line-in-visual)
  (add-hook 'evil-visual-state-exit-hook #'ar/enable-hl-line-after-visual))
#+end_src

** Highlight indentions
#+begin_src emacs-lisp
(use-package indent-bars
  :defer t
  :custom
  (indent-bars-color '(highlight :face-bg t :blend 0.225))
  (indent-bars-no-descend-string t)
  (indent-bars-treesit-ignore-blank-lines-types '("module"))
  (indent-bars-prefer-character t)
  (indent-bars-treesit-scope '((python function_definition class_definition for_statement
                                       if_statement with_statement while_statement)))
  :hook ((prog-mode yaml-mode) . indent-bars-mode)
  :init
  ;; CRITICAL FIX: Disable indent-bars in Visual mode to prevent lag during selection
  ;; The heavy calculation of bars/scopes slows down the cursor drag loop.
  (defun ar/disable-indent-bars-in-visual ()
    (when (bound-and-true-p indent-bars-mode)
      (indent-bars-mode -1)
      (setq-local ar/indent-bars-was-on t)))
  
  (defun ar/enable-indent-bars-after-visual ()
    (when (bound-and-true-p ar/indent-bars-was-on)
      (indent-bars-mode 1)
      (kill-local-variable 'ar/indent-bars-was-on)))
  
  (add-hook 'evil-visual-state-entry-hook #'ar/disable-indent-bars-in-visual)
  (add-hook 'evil-visual-state-exit-hook #'ar/enable-indent-bars-after-visual)
  :config
  (require 'indent-bars-ts))
#+end_src

** Highlight brackets according to their depth
#+begin_src emacs-lisp
(use-package rainbow-delimiters
  :defer t
  :hook ((LaTeX-mode . rainbow-delimiters-mode)
         (prog-mode . rainbow-delimiters-mode))
  :custom-face
  ;; Tokyo Night palette applied to delimiter depths
  (rainbow-delimiters-depth-1-face ((t (:foreground "#7aa2f7"))))  ; Blue
  (rainbow-delimiters-depth-2-face ((t (:foreground "#ff9e64"))))  ; Orange
  (rainbow-delimiters-depth-3-face ((t (:foreground "#9ece6a"))))  ; Green
  (rainbow-delimiters-depth-4-face ((t (:foreground "#7dcfff"))))  ; Cyan
  (rainbow-delimiters-depth-5-face ((t (:foreground "#bb9af7"))))  ; Magenta
  (rainbow-delimiters-depth-6-face ((t (:foreground "#e0af68"))))  ; Yellow
  (rainbow-delimiters-depth-7-face ((t (:foreground "#f7768e"))))  ; Red
  (rainbow-delimiters-depth-8-face ((t (:foreground "#9d7cd8"))))  ; Purple
  (rainbow-delimiters-depth-9-face ((t (:foreground "#73daca"))))) ; Teal
#+end_src

** Parenthesis Matching & Context
#+begin_src emacs-lisp
(use-package paren
  :ensure nil
  :hook (after-init . show-paren-mode)
  :custom
  (show-paren-delay 0.1)
  (show-paren-highlight-openparen t)
  (show-paren-when-point-inside-paren t)
  (show-paren-when-point-in-periphery t)
  ;; 1. Clean Visuals: Only highlight the bracket character itself
  (show-paren-style 'parenthesis)
  ;; 2. Disable default messages: We use the custom ElDoc logic below
  (show-paren-context-when-offscreen nil)
  
  :config
  ;; === CUSTOM ELDOC BACKEND FOR PARENTHESIS ===
  (defun ar/paren-eldoc-offscreen-context (callback &rest _)
    "Show off-screen parenthesis context in Echo Area via ElDoc."
    (let* ((pos (point))
           (match-pos nil)
           (label nil)
           (line-content nil)
           (syntax-at (when (char-after pos) (char-syntax (char-after pos))))
           (syntax-before (when (char-before pos) (char-syntax (char-before pos))))
           ;; Default bounds are the whole buffer
           (scan-beg (point-min))
           (scan-end (point-max)))

      ;; 0. ORG MODE FIX: Constrain search to current source block
      (when (and (derived-mode-p 'org-mode)
                 (org-in-src-block-p))
        (let ((elem (org-element-at-point)))
          (when elem
            (setq scan-beg (org-element-property :begin elem))
            (setq scan-end (org-element-property :end elem)))))

      (save-restriction
        ;; Safety check: Ensure bounds are valid numbers before narrowing
        (when (and (numberp scan-beg) (numberp scan-end) (< scan-beg scan-end))
          (narrow-to-region scan-beg scan-end))

        ;; 1. FIND MATCHING POSITION
        ;; Note: Comments below must not contain raw parens to avoid Org syntax issues
        (cond
         ;; Case A: At Opening Bracket
         ((eq syntax-at ?\()
          (setq match-pos (ignore-errors (scan-sexps pos 1)))
          (when match-pos
            (setq match-pos (1- match-pos))
            (setq label "Closing: ")))
         
         ;; Case B: At Closing Bracket (Evil/Block cursor)
         ((eq syntax-at ?\))
          (setq match-pos (ignore-errors (scan-sexps (1+ pos) -1)))
          (setq label "Opening: "))
         
         ;; Case C: After Closing Bracket (Standard Emacs)
         ((eq syntax-before ?\))
          (setq match-pos (ignore-errors (scan-sexps pos -1)))
          (setq label "Opening: ")))

        ;; 2. RETRIEVE CONTENT IF OFF-SCREEN
        (when (and match-pos 
                   label
                   (not (pos-visible-in-window-p match-pos)))
          (with-current-buffer (current-buffer)
            (save-excursion
              (goto-char match-pos)
              (setq line-content (string-trim (substring-no-properties (thing-at-point 'line t)))))))

        ;; 3. DISPLAY
        (when line-content
          (funcall callback 
                   (concat
                    (propertize label 'face '(:inherit shadow :weight bold))
                    (propertize line-content 'face '(:inherit font-lock-keyword-face :weight bold))))))))

  ;; Register with ElDoc (High Priority -50) to appear before general docs
  (add-hook 'eldoc-mode-hook
            (lambda ()
              (add-hook 'eldoc-documentation-functions #'ar/paren-eldoc-offscreen-context -50 t))))
#+end_src

** Highlight TODO and similar keywords in comments and strings
#+begin_src emacs-lisp
(use-package hl-todo
  :defer t
  :commands (hl-todo-rg-project)
  
  :custom-face
  (hl-todo ((t (:inherit default :height 0.9 :width condensed :weight bold :underline nil :inverse-video nil))))
  
  :hook ((after-init . global-hl-todo-mode)
         (org-mode . hl-todo-mode))
  
  :init
  (setq hl-todo-require-punctuation t
        hl-todo-highlight-punctuation ":")
  
  :config
  ;; Define keywords with Tokyo Night colors
  (setq hl-todo-keyword-faces
        '(("TODO"       . "#e0af68")  ; Yellow
          ("FIXME"      . "#f7768e")  ; Red
          ("BUG"        . "#f7768e")  ; Red
          ("ISSUE"      . "#f7768e")  ; Red
          ("PROG"       . "#7aa2f7")  ; Blue
          ("TRICK"      . "#ff9e64")  ; Orange
          ("WORKAROUND" . "#ff9e64")  ; Orange
          ("BENCHMARK"  . "#9ece6a")  ; Green
          ("REVIEW"     . "#7dcfff")  ; Cyan
          ("NOTE"       . "#bb9af7")  ; Purple
          ("DEPRECATED" . "#73daca")  ; Teal
          ("HACK"       . "#db4b4b")  ; Darker Red
          ("DEBUG"      . "#e0af68")  ; Yellow
          ("STUB"       . "#e0af68"))) ; Yellow

  ;; Custom project search function using Consult
  (defun hl-todo-rg-project ()
    "Use `consult-ripgrep' to find all TODO keywords in the current project."
    (interactive)
    (if (fboundp 'consult-ripgrep)
        ;; Pre-fill the search with a regex matching all defined keywords
        (consult-ripgrep nil (concat "\\b(" (string-join (mapcar #'car hl-todo-keyword-faces) "|") ")\\b"))
      (user-error "Consult-ripgrep is not available"))))
#+end_src

** Colorize color names in buffers
#+begin_src emacs-lisp
(use-package rainbow-mode
  :defer t
  :hook ((prog-mode . rainbow-mode)
	     (LaTeX-mode . rainbow-mode)
         (org-mode . rainbow-mode)))
#+end_src

* Prettify Symbols
** Core Configuration
#+begin_src emacs-lisp
(use-package prog-mode
  :ensure nil
  :pretty-hydra
  ((:title (pretty-hydra-title "Prettify Symbols" 'mdicon "nf-md-code_tags")
    :color amaranth :quit-key ("q" "C-g"))
   ("Toggle"
    (("s" prettify-symbols-mode "mode" :toggle t)
     ("a" ar/prettify-symbols-toggle-aggressive "aggressive")
     ("u" ar/prettify-symbols-toggle-unprettify-at-point "unprettify"))
    "Info"
    (("p" ar/prettify-symbols-show-at-point "at point")
     ("l" ar/prettify-symbols-list-all "list all")
     ("d" ar/prettify-symbols-describe-char "describe"))
    "Edit"
    (("r" ar/prettify-symbols-reload "reload")
     ("i" ar/prettify-symbols-insert-unicode "insert"))))
  :bind ("C-c s" . prog-mode-hydra/body)
  :init
  ;; Unprettify when cursor approaches symbol (critical for editing)
  (setq prettify-symbols-unprettify-at-point 'right-edge)
  
  ;; Core configuration variables
  (defvar ar/prettify-symbols-base-alist
    '(;; Relational operators
      ("!=" . ?≠)              ; U+2260 NOT EQUAL TO
      ("<=" . ?≤)              ; U+2264 LESS-THAN OR EQUAL TO
      (">=" . ?≥)              ; U+2265 GREATER-THAN OR EQUAL TO
      ("==" . ?≡)              ; U+2261 IDENTICAL TO
      
      ;; Arrows (common across languages)
      ("->" . ?→)              ; U+2192 RIGHTWARDS ARROW
      ("=>" . ?⇒)              ; U+21D2 RIGHTWARDS DOUBLE ARROW
      ("<-" . ?←)              ; U+2190 LEFTWARDS ARROW
      
      ;; Mathematical operators
      ("sqrt" . ?√)            ; U+221A SQUARE ROOT
      ("sum" . ?∑)             ; U+2211 N-ARY SUMMATION
      ("pi" . ?π)              ; U+03C0 GREEK SMALL LETTER PI
      
      ;; Common symbols
      ("..." . ?…)             ; U+2026 HORIZONTAL ELLIPSIS
      ("lambda" . ?λ))         ; U+03BB GREEK SMALL LETTER LAMBDA
    "Base prettify symbols shared across multiple modes.")
  
  ;; Enable globally but allow modes to opt out
  :config
  (global-prettify-symbols-mode +1))
#+end_src

** Custom Compose Predicate
#+begin_src emacs-lisp
(defun ar/prettify-symbols-compose-p-default (start end _match)
  "Custom compose predicate that's more permissive than default.
Allows prettification in more contexts while still being safe."
  (let* ((syntaxes-beg (if (memq (char-syntax (char-after start)) '(?w ?_))
                          '(?w ?_) '(?. ?\\)))
         (syntaxes-end (if (memq (char-syntax (char-before end)) '(?w ?_))
                          '(?w ?_) '(?. ?\\))))
    (and
     ;; Check that surrounding syntax matches
     (not (memq (char-syntax (or (char-before start) ?\s)) syntaxes-beg))
     (not (memq (char-syntax (or (char-after end) ?\s)) syntaxes-end))
     ;; Avoid prettifying in strings (usually)
     ;; Remove this check if you want prettification in strings
     (not (nth 3 (syntax-ppss))))))

(defun ar/prettify-symbols-compose-p-aggressive (start end match)
  "More aggressive compose predicate that prettifies in comments and strings.
Use this if you want maximum prettification."
  (let* ((syntaxes-beg (if (memq (char-syntax (char-after start)) '(?w ?_))
                          '(?w ?_) '(?. ?\\)))
         (syntaxes-end (if (memq (char-syntax (char-before end)) '(?w ?_))
                          '(?w ?_) '(?. ?\\))))
    (and
     ;; Check that surrounding syntax matches
     (not (memq (char-syntax (or (char-before start) ?\s)) syntaxes-beg))
     (not (memq (char-syntax (or (char-after end) ?\s)) syntaxes-end)))))
#+end_src

** Emacs Lisp
#+begin_src emacs-lisp
(defun ar/prettify-symbols-elisp-setup ()
  "Configure prettify-symbols for Emacs Lisp modes."
  (setq prettify-symbols-alist
        (append
         ar/prettify-symbols-base-alist
         '(;; Lisp-specific symbols
           ("defun" . ?ƒ)          ; U+0192 LATIN SMALL LETTER F WITH HOOK
           ("defmacro" . ?μ)       ; U+03BC GREEK SMALL LETTER MU
           
           ;; Logical operators
           ("not" . ?¬)            ; U+00AC NOT SIGN
           ("and" . ?∧)            ; U+2227 LOGICAL AND
           ("or" . ?∨)             ; U+2228 LOGICAL OR
           
           ;; Special comparisons
           ("/=" . ?≠)             ; U+2260 NOT EQUAL TO
           
           ;; Constants (commented out - can be confusing)
           ;; ("nil" . ?∅)         ; U+2205 EMPTY SET
           ;; ("t" . ?⊤)           ; U+22A4 DOWN TACK (true)
           ))))

;; Apply to all Emacs Lisp modes
(add-hook 'emacs-lisp-mode-hook #'ar/prettify-symbols-elisp-setup)
(add-hook 'lisp-interaction-mode-hook #'ar/prettify-symbols-elisp-setup)
(add-hook 'inferior-emacs-lisp-mode-hook #'ar/prettify-symbols-elisp-setup)
#+end_src

** Python
#+begin_src emacs-lisp
;; (defun ar/prettify-symbols-python-setup ()
;;   "Configure prettify-symbols for Python modes."
;;   (setq prettify-symbols-alist
;;         (append
;;          ar/prettify-symbols-base-alist
;;          '(;; Python keywords - selective to maintain readability
;;            ("def" . ?ƒ)            ; U+0192 LATIN SMALL LETTER F WITH HOOK
;;            ("class" . ?𝒞)          ; U+1D49E MATHEMATICAL SCRIPT CAPITAL C
;;            ("return" . ?⟼)         ; U+27FC LONG RIGHTWARDS ARROW FROM BAR
;;            ("yield" . ?⟻)          ; U+27FB LONG LEFTWARDS ARROW FROM BAR
           
;;            ;; Python operators
;;            ("not" . ?¬)            ; U+00AC NOT SIGN
;;            ("in" . ?∈)             ; U+2208 ELEMENT OF
;;            ("not in" . ?∉)         ; U+2209 NOT AN ELEMENT OF
;;            ("and" . ?∧)            ; U+2227 LOGICAL AND
;;            ("or" . ?∨)             ; U+2228 LOGICAL OR
           
;;            ;; Python constants
;;            ("None" . ?∅)           ; U+2205 EMPTY SET
;;            ("True" . ?⊤)           ; U+22A4 DOWN TACK
;;            ("False" . ?⊥)          ; U+22A5 UP TACK
           
;;            ;; Type hints (Python 3.5+)
;;            ("int" . ?ℤ)            ; U+2124 DOUBLE-STRUCK CAPITAL Z
;;            ("float" . ?ℝ)          ; U+211D DOUBLE-STRUCK CAPITAL R
;;            ("str" . ?𝕊)            ; U+1D54A MATHEMATICAL DOUBLE-STRUCK CAPITAL S
;;            ("bool" . ?𝔹)           ; U+1D539 MATHEMATICAL DOUBLE-STRUCK CAPITAL B
           
;;            ;; Common type hints (commented - can be too aggressive)
;;            ;; ("List" . ?𝐋)        ; U+1D40B MATHEMATICAL BOLD CAPITAL L
;;            ;; ("Dict" . ?𝐃)        ; U+1D403 MATHEMATICAL BOLD CAPITAL D
;;            ;; ("Set" . ?𝐒)         ; U+1D412 MATHEMATICAL BOLD CAPITAL S
;;            ;; ("Tuple" . ?𝐓)       ; U+1D413 MATHEMATICAL BOLD CAPITAL T
;;            ;; ("Optional" . ??)    ; U+2370 APL FUNCTIONAL SYMBOL QUAD QUESTION
;;            )))
  
;;   ;; Custom predicate for Python to avoid conflicts with docstrings
;;   (setq prettify-symbols-compose-predicate
;;         #'ar/prettify-symbols-compose-p-default))

;; ;; Apply to Python modes
;; (add-hook 'python-mode-hook #'ar/prettify-symbols-python-setup)
;; (add-hook 'python-ts-mode-hook #'ar/prettify-symbols-python-setup)
#+end_src

** JavaScript/TypeScript
#+begin_src emacs-lisp
(defun ar/prettify-symbols-javascript-setup ()
  "Configure prettify-symbols for JavaScript and TypeScript."
  (setq prettify-symbols-alist
        (append
         ar/prettify-symbols-base-alist
         '(;; JavaScript arrows and functions
           ("function" . ?ƒ)       ; U+0192 LATIN SMALL LETTER F WITH HOOK
           
           ;; Logical operators
           ("&&" . ?∧)             ; U+2227 LOGICAL AND
           ("||" . ?∨)             ; U+2228 LOGICAL OR
           
           ;; Strict equality (JavaScript-specific)
           ("===" . ?≡)            ; U+2261 IDENTICAL TO
           ("!==" . ?≢)            ; U+2262 NOT IDENTICAL TO
           
           ;; Special values
           ("null" . ?∅)           ; U+2205 EMPTY SET
           ("undefined" . ?⊥)      ; U+22A5 UP TACK
           
           ;; Math constants (when using Math object)
           ("Math.PI" . ?π)        ; U+03C0 GREEK SMALL LETTER PI
           ("Infinity" . ?∞)       ; U+221E INFINITY
           ))))

;; Apply to JavaScript and TypeScript modes
(add-hook 'javascript-mode-hook #'ar/prettify-symbols-javascript-setup)
(add-hook 'js-mode-hook #'ar/prettify-symbols-javascript-setup)
(add-hook 'js-ts-mode-hook #'ar/prettify-symbols-javascript-setup)
(add-hook 'typescript-mode-hook #'ar/prettify-symbols-javascript-setup)
(add-hook 'typescript-ts-mode-hook #'ar/prettify-symbols-javascript-setup)
(add-hook 'javascript-ts-mode-hook #'ar/prettify-symbols-javascript-setup)
#+end_src

** Shell/Bash
#+begin_src emacs-lisp
;; (defun ar/prettify-symbols-shell-setup ()
;;   "Configure prettify-symbols for shell scripts."
;;   (setq prettify-symbols-alist
;;         (append
;;          ar/prettify-symbols-base-alist
;;          '(;; Test operators (be conservative here)
;;            ("-eq" . ?=)            ; Keep simple for readability
;;            ("-ne" . ?≠)            ; U+2260 NOT EQUAL TO
;;            ("-le" . ?≤)            ; U+2264 LESS-THAN OR EQUAL TO
;;            ("-ge" . ?≥)            ; U+2265 GREATER-THAN OR EQUAL TO
           
;;            ;; Logical operators
;;            ("&&" . ?∧)             ; U+2227 LOGICAL AND
;;            ("||" . ?∨)             ; U+2228 LOGICAL OR
           
;;            ;; Don't prettify pipe | - it's too fundamental to shell syntax
;;            ))))

;; ;; Apply to shell modes
;; (add-hook 'sh-mode-hook #'ar/prettify-symbols-shell-setup)
;; (add-hook 'bash-ts-mode-hook #'ar/prettify-symbols-shell-setup)
#+end_src

** C/C++
#+begin_src emacs-lisp
(defun ar/prettify-symbols-c-setup ()
  "Configure prettify-symbols for C/C++ modes."
  (setq prettify-symbols-alist
        (append
         ar/prettify-symbols-base-alist
         '(;; C/C++ specific
           ("NULL" . ?∅)           ; U+2205 EMPTY SET
           ("nullptr" . ?∅)        ; U+2205 EMPTY SET (C++11)
           
           ;; Logical operators (standard C operators)
           ("&&" . ?∧)             ; U+2227 LOGICAL AND
           ("||" . ?∨)             ; U+2228 LOGICAL OR
           
           ;; Avoid prettifying -> as it's semantically important for pointers
           ;; Avoid prettifying ! as it's used frequently and would be confusing
           
           ;; Math constants
           ("M_PI" . ?π)           ; U+03C0 GREEK SMALL LETTER PI
           ("INFINITY" . ?∞)       ; U+221E INFINITY
           ))))

;; Apply to C/C++ modes
(add-hook 'c-mode-hook #'ar/prettify-symbols-c-setup)
(add-hook 'c++-mode-hook #'ar/prettify-symbols-c-setup)
(add-hook 'c-ts-mode-hook #'ar/prettify-symbols-c-setup)
(add-hook 'c++-ts-mode-hook #'ar/prettify-symbols-c-setup)
#+end_src

** Rust
#+begin_src emacs-lisp
(defun ar/prettify-symbols-rust-setup ()
  "Configure prettify-symbols for Rust mode."
  (setq prettify-symbols-alist
        (append
         ar/prettify-symbols-base-alist
         '(;; Rust-specific
           ("fn" . ?ƒ)             ; U+0192 LATIN SMALL LETTER F WITH HOOK
           
           ;; Logical operators
           ("&&" . ?∧)             ; U+2227 LOGICAL AND
           ("||" . ?∨)             ; U+2228 LOGICAL OR
           
           ;; Avoid prettifying -> as it's the return type operator in Rust
           ;; This is semantically important
           )))
  
  ;; Custom predicate for Rust to avoid conflicts with closure syntax ||
  (setq-local prettify-symbols-compose-predicate
              (lambda (start end match)
                (and (ar/prettify-symbols-compose-p-default start end match)
                     ;; Don't prettify || in closure parameters
                     (not (and (string= match "||")
                               (save-excursion
                                 (goto-char start)
                                 (looking-back "\\(?:\\<move\\|=\\) *" 
                                              (line-beginning-position)))))))))

;; Apply to Rust mode (if installed)
(with-eval-after-load 'rust-mode
  (add-hook 'rust-mode-hook #'ar/prettify-symbols-rust-setup))
(with-eval-after-load 'rust-ts-mode
  (add-hook 'rust-ts-mode-hook #'ar/prettify-symbols-rust-setup))
#+end_src

** Org Mode
#+begin_src emacs-lisp
(defun ar/prettify-symbols-org-setup ()
  "Configure prettify-symbols for Org mode.
Note: org-modern handles most visual improvements, so we're conservative here."
  (setq prettify-symbols-alist
        '(;; Arrows and implications (useful in text)
          ("->" . ?→)              ; U+2192 RIGHTWARDS ARROW
          ("<-" . ?←)              ; U+2190 LEFTWARDS ARROW
          ("=>" . ?⇒)              ; U+21D2 RIGHTWARDS DOUBLE ARROW
          ("<=" . ?≤)              ; U+2264 LESS-THAN OR EQUAL TO (when used as symbol)
          (">=" . ?≥)              ; U+2265 GREATER-THAN OR EQUAL TO (when used as symbol)
          
          ;; Common symbols in prose
          ("!=" . ?≠)              ; U+2260 NOT EQUAL TO
          ("..." . ?…)             ; U+2026 HORIZONTAL ELLIPSIS
          
          ;; Source block markers (subtle, won't conflict with org-modern)
          ;; Commented out if you prefer org-modern's handling
          ;; ("#+BEGIN_SRC" . ?»)   ; U+00BB RIGHT-POINTING DOUBLE ANGLE QUOTATION MARK
          ;; ("#+END_SRC" . ?«)     ; U+00AB LEFT-POINTING DOUBLE ANGLE QUOTATION MARK
          ;; ("#+begin_src" . ?»)
          ;; ("#+end_src" . ?«)
          ))
  
  ;; Don't prettify in code blocks (org-modern handles those)
  (setq-local prettify-symbols-compose-predicate
              (lambda (start end match)
                (and (ar/prettify-symbols-compose-p-default start end match)
                     ;; Don't prettify in src blocks
                     (not (org-in-src-block-p))))))

(add-hook 'org-mode-hook #'ar/prettify-symbols-org-setup)
#+end_src

** Markdown
#+begin_src emacs-lisp
(defun ar/prettify-symbols-markdown-setup ()
  "Configure prettify-symbols for Markdown mode."
  (setq prettify-symbols-alist
        '(;; Common symbols
          ("->" . ?→)              ; U+2192 RIGHTWARDS ARROW
          ("<-" . ?←)              ; U+2190 LEFTWARDS ARROW
          ("=>" . ?⇒)              ; U+21D2 RIGHTWARDS DOUBLE ARROW
          ("<=" . ?≤)              ; U+2264 LESS-THAN OR EQUAL TO
          (">=" . ?≥)              ; U+2265 GREATER-THAN OR EQUAL TO
          ("!=" . ?≠)              ; U+2260 NOT EQUAL TO
          ("..." . ?…)             ; U+2026 HORIZONTAL ELLIPSIS
          
          ;; Code fence markers (subtle)
          ("```" . ?‣)             ; U+2023 TRIANGULAR BULLET
          
          ;; Checkbox syntax (if using GitHub-flavored markdown)
          ;; Note: Some markdown modes handle these specially
          ("[ ]" . ?☐)          ; U+2610 BALLOT BOX
          ("[x]" . ?☑)          ; U+2611 BALLOT BOX WITH CHECK
          ("[X]" . ?☑)          ; U+2611 BALLOT BOX WITH CHECK
          )))

(add-hook 'markdown-mode-hook #'ar/prettify-symbols-markdown-setup)
(add-hook 'gfm-mode-hook #'ar/prettify-symbols-markdown-setup)
#+end_src

** Helper Functions
#+begin_src emacs-lisp
(defun ar/prettify-symbols-reload ()
  "Reload prettify-symbols-mode for current buffer.
Useful after changing symbols configuration."
  (interactive)
  (when (bound-and-true-p prettify-symbols-mode)
    (prettify-symbols-mode -1)
    (prettify-symbols-mode +1)
    (message "Prettify symbols reloaded")))

(defun ar/prettify-symbols-show-at-point ()
  "Show the actual text of prettified symbol at point in the echo area."
  (interactive)
  (let* ((pos (point))
         (props (text-properties-at pos))
         (composition (plist-get props 'composition)))
    (if composition
        (let ((start (previous-single-property-change 
                      (1+ pos) 'composition nil (point-min)))
              (end (next-single-property-change 
                    pos 'composition nil (point-max))))
          (message "Symbol: %S" (buffer-substring start end)))
      (message "No prettified symbol at point"))))

(defun ar/prettify-symbols-toggle-unprettify-at-point ()
  "Cycle through unprettify-at-point options.
nil -> right-edge -> t -> nil"
  (interactive)
  (setq prettify-symbols-unprettify-at-point
        (pcase prettify-symbols-unprettify-at-point
          ('nil 'right-edge)
          ('right-edge t)
          (_ nil)))
  (ar/prettify-symbols-reload)
  (message "Prettify unprettify-at-point: %s" 
           (pcase prettify-symbols-unprettify-at-point
             ('nil "disabled")
             ('right-edge "at right edge")
             (_ "when on symbol"))))

(defun ar/prettify-symbols-list-all ()
  "Display all prettified symbols in current buffer's alist."
  (interactive)
  (if prettify-symbols-alist
      (let ((buf (get-buffer-create "*Prettify Symbols*")))
        (with-current-buffer buf
          (erase-buffer)
          (insert (format "Prettify Symbols for %s\n" major-mode))
          (insert (make-string 50 ?=) "\n\n")
          (insert (format "%-20s %-10s %s\n" "Text" "Symbol" "Unicode"))
          (insert (make-string 50 ?-) "\n")
          (dolist (pair prettify-symbols-alist)
            (insert (format "%-20s %-10c U+%04X\n"
                           (car pair)
                           (cdr pair)
                           (cdr pair))))
          (insert "\n")
          (insert "Unprettify at point: " 
                  (format "%s" prettify-symbols-unprettify-at-point) "\n")
          (goto-char (point-min))
          (special-mode))
        (display-buffer buf))
    (message "No prettify-symbols-alist defined for this buffer")))

(defun ar/prettify-symbols-toggle-aggressive ()
  "Toggle between default and aggressive prettification predicates."
  (interactive)
  (if (eq prettify-symbols-compose-predicate 
          #'ar/prettify-symbols-compose-p-aggressive)
      (progn
        (setq-local prettify-symbols-compose-predicate
                    #'ar/prettify-symbols-compose-p-default)
        (message "Using default prettification (no strings/comments)"))
    (setq-local prettify-symbols-compose-predicate
                #'ar/prettify-symbols-compose-p-aggressive)
    (message "Using aggressive prettification (includes strings/comments)"))
  (ar/prettify-symbols-reload))

(defun ar/prettify-symbols-insert-unicode ()
  "Insert a Unicode character by name or code point.
Helpful for finding symbols to use in configuration."
  (interactive)
  (call-interactively #'insert-char))

(defun ar/prettify-symbols-describe-char ()
  "Describe the character at point, showing Unicode info."
  (interactive)
  (describe-char (point)))

;; Advice to prevent prettification from breaking indentation/alignment
;; This isn't usually needed but can help in edge cases
(defun ar/prettify-symbols-before-save ()
  "Temporarily disable prettify-symbols before saving.
Re-enables it after save. Prevents indentation issues."
  (when (bound-and-true-p prettify-symbols-mode)
    (prettify-symbols-mode -1)
    (add-hook 'after-save-hook #'ar/prettify-symbols-after-save nil t)))

(defun ar/prettify-symbols-after-save ()
  "Re-enable prettify-symbols after saving."
  (prettify-symbols-mode +1)
  (remove-hook 'after-save-hook #'ar/prettify-symbols-after-save t))

;; Uncomment to enable save protection (usually not needed)
;; (add-hook 'before-save-hook #'ar/prettify-symbols-before-save)

#+end_src

* Elisp
#+begin_src emacs-lisp
(use-package elisp-mode
  :ensure nil
  :config
  (with-no-warnings
    (defun my-lisp-indent-function (indent-point state)
      (let ((normal-indent (current-column))
            (orig-point (point)))
        (goto-char (1+ (elt state 1)))
        (parse-partial-sexp (point) calculate-lisp-indent-last-sexp 0 t)
        (cond
         ((and (elt state 2)
               (or (not (looking-at "\\sw\\|\\s_"))
                   (looking-at ":")))
          (if (not (> (save-excursion (forward-line 1) (point))
                     calculate-lisp-indent-last-sexp))
              (progn (goto-char calculate-lisp-indent-last-sexp)
                     (beginning-of-line)
                     (parse-partial-sexp (point)
                                         calculate-lisp-indent-last-sexp 0 t)))
          (backward-prefix-chars)
          (current-column))
         ((and (save-excursion
                 (goto-char indent-point)
                 (skip-syntax-forward " ")
                 (not (looking-at ":")))
               (save-excursion
                 (goto-char orig-point)
                (looking-at ":")))
         (save-excursion
           (goto-char (+ 2 (elt state 1)))
           (current-column)))
        (t
         (let ((function (buffer-substring (point)
                                           (progn (forward-sexp 1) (point))))
               method)
           (setq method (or (function-get (intern-soft function)
                                          'lisp-indent-function)
                            (get (intern-soft function) 'lisp-indent-hook)))
           (cond ((or (eq method 'defun)
                      (and (null method)
                           (length> function 3)
                           (string-match "\\`def" function)))
                  (lisp-indent-defform state indent-point))
                 ((integerp method)
                  (lisp-indent-specform method state
                                        indent-point normal-indent))
                 (method
                  (funcall method indent-point state))))))))
   (add-hook 'emacs-lisp-mode-hook
             (lambda () (setq-local lisp-indent-function #'my-lisp-indent-function)))

   ;; Add remove buttons for advices
   (add-hook 'help-mode-hook 'cursor-sensor-mode)

   (defun function-advices (function)
     "Return FUNCTION's advices."
     (let ((flist (indirect-function function)) advices)
       (while (advice--p flist)
         (setq advices `(,@advices ,(advice--car flist)))
         (setq flist (advice--cdr flist)))
       advices))

   (defun add-remove-advice-button (advice function)
     (when (and (functionp advice) (functionp function))
       (let ((inhibit-read-only t)
             (msg (format "Remove advice `%s'" advice)))
         (insert "\t")
         (insert-button
          "Remove"
          'face 'custom-button
          'cursor-sensor-functions `((lambda (&rest _) ,msg))
          'help-echo msg
          'action (lambda (_)
                    (when (yes-or-no-p msg)
                      (message "%s from function `%s'" msg function)
                      (advice-remove function advice)
                      (if (eq major-mode 'helpful-mode)
                          (helpful-update)
                        (revert-buffer nil t))))
          'follow-link t))))

   (defun add-button-to-remove-advice (buffer-or-name function)
     "Add a button to remove advice."
     (with-current-buffer buffer-or-name
       (save-excursion
         (goto-char (point-min))
         (let ((ad-list (function-advices function)))
           (while (re-search-forward "^\\(?:This function has \\)?:[-a-z]+ advice: \\(.+\\)$" nil t)
             (let ((advice (car ad-list)))
               (add-remove-advice-button advice function)
               (setq ad-list (delq advice ad-list))))))))

   (define-advice describe-function-1 (:after (function) advice-remove-button)
     (add-button-to-remove-advice (help-buffer) function))
   (with-eval-after-load 'helpful
     (define-advice helpful-update (:after () advice-remove-button)
       (when helpful--callable-p
         (add-button-to-remove-advice (current-buffer) helpful--sym))))

   ;; Remove hooks
   (defun remove-hook-at-point ()
     "Remove the hook at the point in the *Help* buffer."
     (interactive)
     (unless (memq major-mode '(help-mode helpful-mode))
       (error "Only for help-mode or helpful-mode"))

     (let ((orig-point (point)))
       (save-excursion
         (when-let*
             ((hook (progn (goto-char (point-min)) (symbol-at-point)))
              (func (when (and
                           (or (re-search-forward (format "^Value:?[\s|\n]") nil t)
                               (goto-char orig-point))
                           (sexp-at-point))
                      (end-of-sexp)
                      (backward-char 1)
                      (catch 'break
                        (while t
                          (condition-case _err
                              (backward-sexp)
                            (scan-error (throw 'break nil)))
                          (let ((bounds (bounds-of-thing-at-point 'sexp)))
                            (when (<= (car bounds) orig-point (cdr bounds))
                              (throw 'break (sexp-at-point)))))))))
           (when (yes-or-no-p (format "Remove %s from %s? " func hook))
             (remove-hook hook func)
             (if (eq major-mode 'helpful-mode)
                 (helpful-update)
               (revert-buffer nil t)))))))
   (bind-key "r" #'remove-hook-at-point help-mode-map)))

;; Syntax highlighting of known Elisp symbols
(if (boundp 'elisp-fontify-semantically)
    (setq elisp-fontify-semantically t)
  (use-package highlight-defined
    :hook ((emacs-lisp-mode inferior-emacs-lisp-mode) . highlight-defined-mode)))

(use-package macrostep)

;; A better *Help* buffer
(use-package helpful
  :bind (([remap describe-function] . helpful-callable)
         ([remap describe-command]  . helpful-command)
         ([remap describe-variable] . helpful-variable)
         ([remap describe-key]      . helpful-key)
         ([remap describe-symbol]   . helpful-symbol))
  :hook (helpful-mode . cursor-sensor-mode) ; for remove-advice button
  :init
  (with-no-warnings
    (with-eval-after-load 'apropos
      ;; patch apropos buttons to call helpful instead of help
      (dolist (fun-bt '(apropos-function apropos-macro apropos-command))
        (button-type-put
         fun-bt 'action
         (lambda (button)
           (helpful-callable (button-get button 'apropos-symbol)))))
      (dolist (var-bt '(apropos-variable apropos-user-option))
        (button-type-put
         var-bt 'action
         (lambda (button)
           (helpful-variable (button-get button 'apropos-symbol))))))))
#+end_src

* Completion Framework
** Orderless
#+begin_src emacs-lisp
(use-package orderless
  :custom
  (completion-styles '(orderless basic))
  (completion-category-overrides '((file (styles partial-completion))))
  (completion-category-defaults nil) ;; Disable defaults, use our settings
  (completion-pcm-leading-wildcard t)) ;; Emacs 31: partial-completion behaves like substring
#+end_src

** Vertico
VERTical Interactive COmpletion
#+begin_src emacs-lisp
(use-package vertico
  :custom
  (vertico-count 10)
  (vertico-resize nil)
  (vertico-cycle t)
  :bind (:map vertico-map
         ("RET" . vertico-directory-enter)
         ("DEL" . vertico-directory-delete-char)
         ("M-DEL" . vertico-directory-delete-word))
  :hook ((after-init . vertico-mode)
         (rfn-eshadow-update-overlay . vertico-directory-tidy)))
#+end_src

** Marginalia
Enrich existing commands with completion annotations
#+begin_src emacs-lisp
(use-package marginalia
  :hook (after-init . marginalia-mode)
  :config
  (setq marginalia-max-relative-age 0 ; Always show absolute time
        marginalia-align 'right))
#+end_src

** Nerd Icons Completion
Add icons to completion candidates
#+begin_src emacs-lisp
(use-package nerd-icons-completion
  :after marginalia
  :hook (marginalia-mode . nerd-icons-completion-marginalia-setup)
  :config
  (nerd-icons-completion-mode))
#+end_src

** Consult
Consulting completing-read
#+begin_src emacs-lisp
(use-package consult
  :defines (xref-show-xrefs-function xref-show-definitions-function)
  :defines shr-color-html-colors-alist
  :autoload (consult-register-format consult-register-window consult-xref)
  :autoload (consult--read consult--customize-put)
  :commands (consult-narrow-help)
  :functions (list-colors-duplicates consult-colors--web-list)
  :bind (([remap switch-to-buffer] . consult-buffer)
         ([remap switch-to-buffer-other-window] . consult-buffer-other-window)
         ([remap switch-to-buffer-other-frame] . consult-buffer-other-frame)
         ([remap bookmark-jump] . consult-bookmark)
         ([remap evil-show-marks] . consult-mark)
         ([remap evil-show-jumps] . consult-jump-list)
         ([remap goto-line] . consult-goto-line)
         ([remap imenu] . consult-imenu)
         ([remap load-theme] . consult-theme)
         ([remap recentf-open-files] . consult-recent-file)
         ([remap yank-pop] . consult-yank-pop)
         ([remap Info-search]        . consult-info)
         ([remap isearch-forward]    . consult-line))
  :hook (completion-list-mode . consult-preview-at-point-mode)
  :init
  (setq register-preview-delay 0.5
        register-preview-function #'consult-register-format)
  (advice-add #'register-preview :override #'consult-register-window)

  (with-eval-after-load 'xref
    (setq xref-show-xrefs-function #'consult-xref
          xref-show-definitions-function #'consult-xref))

  (defvar consult-colors-history nil
    "History for `consult-colors-emacs' and `consult-colors-web'.")

  (autoload 'list-colors-duplicates "facemenu")
  (autoload 'consult--read "consult")

  (defun consult-colors-emacs (color)
    "Show a list of all supported colors for a particular frame."
    (interactive
     (list (consult--read (list-colors-duplicates (defined-colors))
                          :prompt "Emacs color: "
                          :require-match t
                          :category 'color
                          :history '(:input consult-colors-history))))
    (insert color))

  (defun consult-colors--web-list nil
    "Return list of CSS colors for `counsult-colors-web'."
    (require 'shr-color)
    (sort (mapcar #'downcase (mapcar #'car shr-color-html-colors-alist)) #'string-lessp))

  (defun consult-colors-web (color)
    "Show a list of all CSS colors."
    (interactive
     (list (consult--read (consult-colors--web-list)
                          :prompt "Color: "
                          :require-match t
                          :category 'color
                          :history '(:input consult-colors-history))))
    (insert color))
  :config
  (setq consult-preview-key nil)

  (defvar consult--source-file-visiting-buffer
    `(:name "Main"
      :narrow ?f
      :category buffer
      :face consult-buffer
      :history buffer-name-history
      :state ,#'consult--buffer-state
      :default t
      :items ,(lambda ()
                (consult--buffer-query
                 :sort 'visibility
                 :predicate (lambda (buf)
                              (buffer-file-name buf))
                 :as #'buffer-name)))
    "Buffer source for file-visiting buffers only.")

  (setq consult-buffer-sources
        '(consult--source-file-visiting-buffer))

  ;; Removed :initial. Use M-n to insert symbol-at-point dynamically.
  (consult-customize
   consult-line consult-line-multi :preview-key 'any
   consult-buffer consult-recent-file consult-theme :preview-key '(:debounce 1.0 any)
   consult-goto-line :preview-key '(:debounce 0.5 any)
   consult-ripgrep consult-git-grep consult-grep
   :preview-key '(:debounce 0.5 any))

  (setq consult-narrow-key "<")
  (define-key consult-narrow-map (vconcat consult-narrow-key "?") #'consult-narrow-help))

(use-package consult-dir :defer t)
(use-package consult-todo :defer t)
#+end_src

** Embark
#+begin_src emacs-lisp
(use-package embark
  :commands embark-prefix-help-command
  :init
  (setq prefix-help-command #'embark-prefix-help-command)
  :config
  ;; Manual preview for non-Consult commands using Embark
  (defun my-embark-preview ()
    "Previews candidate in vertico buffer, unless it's a consult command."
    (interactive)
    (unless (bound-and-true-p consult--preview-function)
      (save-selected-window
        (let ((embark-quit-after-action nil))
          (embark-dwim)))))

  ;; Hide the mode line of the Embark live/completions buffers
  (add-to-list 'display-buffer-alist
               '("\\`\\*Embark Collect \\(Live\\|Completions\\)\\*"
                 nil
                 (window-parameters (mode-line-format . none))))

  (with-no-warnings
    (with-eval-after-load 'which-key
      (defun embark-which-key-indicator ()
        "An embark indicator that displays keymaps using which-key.
The which-key help message will show the type and value of the
current target followed by an ellipsis if there are further
targets."
        (lambda (&optional keymap targets prefix)
          (if (null keymap)
              (which-key--hide-popup-ignore-command)
            (which-key--show-keymap
             (if (eq (plist-get (car targets) :type) 'embark-become)
                 "Become"
               (format "Act on %s '%s'%s"
                       (plist-get (car targets) :type)
                       (embark--truncate-target (plist-get (car targets) :target))
                       (if (cdr targets) "…" "")))
             (if prefix
                 (pcase (lookup-key keymap prefix 'accept-default)
                   ((and (pred keymapp) km) km)
                   (_ (key-binding prefix 'accept-default)))
               keymap)
             nil nil t (lambda (binding)
                         (not (string-suffix-p "-argument" (cdr binding))))))))

      (setq embark-indicators
            '(embark-which-key-indicator
              embark-highlight-indicator
              embark-isearch-highlight-indicator))

      (defun embark-hide-which-key-indicator (fn &rest args)
        "Hide the which-key indicator immediately when using the completing-read prompter."
        (which-key--hide-popup-ignore-command)
        (let ((embark-indicators
               (remq #'embark-which-key-indicator embark-indicators)))
          (apply fn args)))

      (advice-add #'embark-completing-read-prompter
                  :around #'embark-hide-which-key-indicator))))
#+end_src

** Embark Consult
#+begin_src emacs-lisp
(use-package embark-consult
  :defer t
  :after (embark consult)
  :hook
  (embark-collect-mode . consult-preview-at-point-mode))
#+end_src

** Corfu
#+begin_src emacs-lisp
(use-package corfu
  :autoload (corfu-quit consult-completion-in-region)
  :functions (persistent-scratch-save corfu-move-to-minibuffer)
  :custom
  (corfu-cycle t)
  (corfu-auto t)
  (corfu-auto-prefix 3)
  (corfu-count 12)
  (corfu-preselect 'prompt)
  (corfu-preview-current nil)
  (corfu-on-exact-match nil)
  (corfu-auto-delay 0.5)
  (global-corfu-modes '((not erc-mode
                             circe-mode
                             help-mode
                             gud-mode
                             vterm-mode)
                        t))
  :custom-face
  (corfu-border ((t (:inherit region :background unspecified))))
  :bind
  (:map corfu-map
        ("TAB" . corfu-next)
        ([tab] . corfu-next)
        ("S-TAB" . corfu-previous)
        ([backtab] . corfu-previous)
        ("M-h" . corfu-info-documentation))
  :hook ((after-init . global-corfu-mode)
         (global-corfu-mode . corfu-history-mode))
  :config
  ;;Quit completion before saving
  (add-hook 'before-save-hook #'corfu-quit)
  (advice-add #'persistent-scratch-save :before #'corfu-quit))
#+end_src

** Basic Completion
#+begin_src emacs-lisp
(use-package emacs
  :ensure nil
  :custom
  (tab-always-indent 'complete)
  (text-mode-ispell-word-completion nil)
  (read-extended-command-predicate #'command-completion-default-include-p))
#+end_src

** Nerd Icons Corfu
#+begin_src emacs-lisp
(use-package nerd-icons-corfu
  :autoload nerd-icons-corfu-formatter
  :after corfu
  :init (add-to-list 'corfu-margin-formatters #'nerd-icons-corfu-formatter))
#+end_src

** Cape

#+begin_comment
===========================================================================
CAPE / CORFU CONFIGURATION & DEBUGGING GUIDE
===========================================================================

CONTEXT:
Emacs completion in Org Mode source blocks is complex due to three conflicting factors:
1. Org Mode's Native Completion: Org has its own CAPF (`org-block-capf`) that
   often aggressively claims priority, preventing other backends from running.
2. Large File Optimization (JIT Lock): In files >3000 lines, Emacs delays
   fontification (syntax highlighting). `cape-elisp-block` natively relies on
   detecting the `org-block` face to know it's inside code. In large files,
   this face is often missing, causing Cape to fail (return nil).
3. Syntax Corruption: Even if detection works, standard `elisp-completion-at-point`
   uses the buffer's global syntax table (Org syntax). In large files, parsing
   state (`syntax-ppss`) gets corrupted by previous Org headings/drawers, causing
   the Lisp parser to think it's inside a string/comment and return no results.

THE SOLUTION (`ar/org-elisp-capf`):
We implement a custom Completion-At-Point Function (CAPF) that:
1. STRUCTURAL DETECTION: Uses `org-element-at-point` instead of visual faces,
   guaranteeing detection even in un-fontified large files.
2. NARROWING: We `narrow-to-region` around the code block content. This is
   critical because it resets the `syntax-ppss` parser cache, forcing it to
   parse the Lisp code from a clean state (point-min) without pollution from
   the preceding 50,000 lines of Org text.
3. BOUNDARY SAFETY: We manually calculate start/end points to exclude
   `#+begin_src` headers so the parser sees only pure Lisp code.
4. SYNTAX SWAPPING: We temporarily enforce `emacs-lisp-mode-syntax-table`
   so atoms/lists are read correctly.

DEBUGGING / TESTING:
If completion fails in the future, check these:
1. Run `M-x describe-char` inside the block. If `face: org-block` is missing,
   the structural fix is doing its job.
2. Verify `completion-at-point-functions` variable (C-h v). `ar/org-elisp-capf`
   must be at the START of the list (depth -100).
3. If Corfu crashes with "Point outside narrowing", ensure the manual
   boundary calculation logic handles the current cursor position.

===========================================================================
#+end_comment

#+begin_src emacs-lisp
(use-package cape
  :bind ("C-c p" . cape-prefix-map)
  :init
  ;; 1. Global Setup
  (add-hook 'completion-at-point-functions #'cape-tex)
  (add-hook 'completion-at-point-functions #'cape-dict)
  (add-hook 'completion-at-point-functions #'cape-dabbrev)
  (add-hook 'completion-at-point-functions #'cape-file)
  (add-hook 'completion-at-point-functions #'cape-keyword)
  
  ;; NOTE: We do NOT add cape-elisp-block globally here.
  ;; We use the custom function below for Org mode instead.

  ;; -----------------------------------------------------------------------
  ;; 2. Robust Elisp Completion for Large Org Files (Narrowing Strategy)
  ;; -----------------------------------------------------------------------
  (defun ar/org-elisp-capf ()
    "A robust Capf for Elisp in Org mode.
    It identifies the block, narrows to the inner content (excluding headers),
    switches to Elisp syntax, and then completes."
    ;; Cheap check first to avoid overhead
    (when (org-in-src-block-p)
      (let ((element (org-element-at-point)))
        (when (and (eq (org-element-type element) 'src-block)
                   (member (org-element-property :language element)
                           '("elisp" "emacs-lisp")))
          (let* ((block-start (org-element-property :post-affiliated element))
                 (block-end (org-element-property :end element))
                 ;; Calculate inner start: Jump to header start, move down 1 line
                 (code-beg (save-excursion
                             (goto-char block-start)
                             (forward-line 1)
                             (point)))
                 ;; Calculate inner end: Jump to block end, move up 1 line
                 ;; (This excludes the #+end_src line)
                 (code-end (save-excursion
                             (goto-char block-end)
                             (forward-line -1)
                             (point))))
            ;; ROBUSTNESS CHECK 1: Ensure boundaries are valid numbers
            (when (and (numberp code-beg) 
                       (numberp code-end) 
                       (< code-beg code-end))
              ;; ROBUSTNESS CHECK 2: Ensure point is strictly inside the code.
              ;; If we are on #+begin_src, narrowing would fail/crash.
              ;; Returning nil lets Org handle header completion instead.
              (when (and (>= (point) code-beg)
                         (<= (point) code-end))
                (save-restriction
                  ;; Narrow to the code itself so the parser doesn't see Org headers
                  ;; and the syntax-ppss cache is reset for this region.
                  (narrow-to-region code-beg code-end)
                  (with-syntax-table emacs-lisp-mode-syntax-table
                    (elisp-completion-at-point))))))))))

  (defun ar/setup-org-cape-completion ()
    ;; Add to the VERY FRONT (-100) to bypass Org's native completion
    (add-hook 'completion-at-point-functions #'ar/org-elisp-capf -100 t))

  (add-hook 'org-mode-hook #'ar/setup-org-cape-completion)
  
  ;; -----------------------------------------------------------------------
  ;; 3. Advice Wrappers
  ;; -----------------------------------------------------------------------
  (advice-add #'lsp-completion-at-point :around #'cape-wrap-noninterruptible)
  (advice-add #'lsp-completion-at-point :around #'cape-wrap-nonexclusive)
  (advice-add #'comint-completion-at-point :around #'cape-wrap-nonexclusive)
  (advice-add #'eglot-completion-at-point :around #'cape-wrap-nonexclusive)
  (advice-add #'pcomplete-completions-at-point :around #'cape-wrap-nonexclusive))
#+end_src

** Dabbrev
#+begin_src emacs-lisp
(use-package dabbrev
  :ensure nil
  :config
  (add-to-list 'dabbrev-ignored-buffer-regexps "\\` ")
  (add-to-list 'dabbrev-ignored-buffer-modes 'doc-view-mode)
  (add-to-list 'dabbrev-ignored-buffer-modes 'pdf-view-mode)
  (add-to-list 'dabbrev-ignored-buffer-modes 'vterm-mode)
  (add-to-list 'dabbrev-ignored-buffer-modes 'tags-table-mode))
#+end_src

* Snippet Engine
#+begin_src emacs-lisp
(defvar my/snippets-directory (expand-file-name "snippets" user-emacs-directory)
  "Directory for personal yasnippet snippets.")

;; Create the custom snippets directory if it doesn't exist.
(unless (file-directory-p my/snippets-directory)
  (make-directory my/snippets-directory t))

;; The engine itself
(use-package yasnippet
  :defer t
  :hook ((prog-mode text-mode org-mode) . yas-minor-mode)
  :custom
  (yas-prompt-functions '(yas-completing-prompt))
  :config
  ;; Add personal snippets to the list
  (add-to-list 'yas-snippet-dirs my/snippets-directory)
  
  ;; --- Robust, Save-Based Automatic Snippet Reloading ---
  (defun ar/yas-reload-snippets-on-save ()
    "Reload all snippets if a snippet file is being saved."
    (when (string-prefix-p my/snippets-directory (buffer-file-name))
      (yas-reload-all)
      (message "Yasnippet collection reloaded.")))

  (add-hook 'after-save-hook #'ar/yas-reload-snippets-on-save))

;; The official snippet collection
(use-package yasnippet-snippets
  :defer t
  :after yasnippet
  :config
  (yas-reload-all))

;; Search snippets with Consult
(use-package consult-yasnippet
  :defer t
  :after (consult yasnippet)
  :config
  (consult-customize consult-yasnippet :preview-key 'any))

;; Add snippets to completion-at-point (Corfu)
(use-package yasnippet-capf
  :defer t
  :after cape
  :init
  ;; Add with high depth (low priority) so it doesn't block other capfs
  (add-hook 'completion-at-point-functions #'yasnippet-capf 70))
#+end_src

* Development Tools
** Direnv
#+begin_src emacs-lisp
(use-package envrc
  :hook (after-init . envrc-global-mode))
#+end_src

** LSP
#+begin_src emacs-lisp
(use-package eglot-booster
  :vc (:url "https://github.com/jdtsmith/eglot-booster.git"
       :branch "main")
  :after eglot
  :config
  (eglot-booster-mode))

(use-package eglot
  :ensure t
  :defer t
  :hook
  ((prog-mode . (lambda ()
                  (unless (derived-mode-p
                           'emacs-lisp-mode 'lisp-mode
                           'makefile-mode 'snippet-mode
                           'ron-mode)
                    (eglot-ensure))))
   ((markdown-mode yaml-mode yaml-ts-mode) . eglot-ensure))

  :init
  ;; Optimization: disable logging events to improve performance
  (setq eglot-events-buffer-size 0)

  ;; Optimization: delay sending changes to the server slightly (0.5s)
  (setq eglot-send-changes-idle-time 0.5)

  ;; Shutdown server when the last buffer managed by it is closed
  (setq eglot-autoshutdown t)) ;consider setting this to nil

(use-package org-src-context
 :ensure nil ; Relies on lisp/org-src-context.el
 :hook (after-init . org-src-context-mode)
 :custom
 ;; Visual: Set to 'nil' to see the grayed-out context surrounding your block.
 ;; Set to 't' if you prefer a focused view (narrowed to just your code).
 (org-src-context-narrow-p nil)

 ;; Safety: Disable context injection for files larger than 500KB to prevent lag.
 (org-src-context-max-filesize 500000)

 :config
 ;; Enable Apheleia (auto-formatting) inside the edit buffer.
 ;; The patched org-src-context ensures a valid file path exists for this to work.
 (with-eval-after-load 'apheleia
   (add-hook 'org-src-mode-hook
             (lambda ()
               (apheleia-mode 1)))))

;; Integration with Consult for searching symbols
(use-package consult-eglot
  :defer t
  :after (consult eglot)
  :bind (:map eglot-mode-map
         ("C-M-." . consult-eglot-symbols)))
#+end_src

** DAP
#+begin_src emacs-lisp
(use-package dape
  :defer t
  :bind (("<f5>" . dape)
         ("M-<f5>" . dape-hydra/body))
  :custom (dape-buffer-window-arrangment 'right)
  :pretty-hydra
  ((:title (pretty-hydra-title "Debug" 'codicon "nf-cod-debug")
    :color pink :quit-key ("q" "C-g"))
   ("Stepping"
    (("n" dape-next "next")
     ("s" dape-step-in "step in")
     ("o" dape-step-out "step out")
     ("c" dape-continue "continue")
     ("p" dape-pause "pause")
     ("k" dape-kill "kill")
     ("r" dape-restart "restart")
     ("D" dape-disconnect-quit "disconnect"))
    "Switch"
    (("m" dape-read-memory "memory")
     ("t" dape-select-thread "thread")
     ("w" dape-watch-dwim "watch")
     ("S" dape-select-stack "stack")
     ("i" dape-info "info")
     ("R" dape-repl "repl"))
    "Breakpoints"
    (("b" dape-breakpoint-toggle "toggle")
     ("l" dape-breakpoint-log "log")
     ("e" dape-breakpoint-expression "expression")
     ("B" dape-breakpoint-remove-all "clear"))
    "Debug"
    (("d" dape "dape")
     ("Q" dape-quit "quit" :exit t))))
  :config
  ;; Save buffers on startup, useful for interpreted languages
  (add-hook 'dape-on-start-hooks
            (defun dape--save-on-start ()
              (save-some-buffers t t)))
  ;; Display hydra on startup
  (add-hook 'dape-on-start-hooks #'dape-hydra/body))
#+end_src

** Syntax Checking
#+begin_src emacs-lisp
(use-package flymake
  :ensure nil
  :hook (prog-mode . flymake-mode)
  :custom
  ;; Doom-inspired behavior: better navigation and visual density
  (flymake-fringe-indicator-position 'right-fringe)
  (flymake-wrap-around t)
  (flymake-suppress-zero-counters t)
  (flymake-no-changes-timeout 0.5)
  (flymake-start-on-save-buffer t)
  :config
  ;; ;; Integration with hl-todo
  ;; ;; Adds TODO/FIXME/NOTE items to the Flymake diagnostics list
  ;; (with-eval-after-load 'hl-todo
  ;;   (add-hook 'flymake-mode-hook
  ;;             (lambda ()
  ;;               (add-hook 'flymake-diagnostic-functions #'hl-todo-flymake nil t))))

  ;; UI: Doom Emacs Double-Arrow Fringe Bitmaps
  (define-fringe-bitmap 'ar/flymake-double-arrow
    [#b00000000 #b00010000 #b00011000 #b00011100 #b00011110 
     #b00011100 #b00011000 #b00010000 #b00000000])

  (setq flymake-error-bitmap   '(ar/flymake-double-arrow flymake-error)
        flymake-warning-bitmap '(ar/flymake-double-arrow flymake-warning)
        flymake-note-bitmap    '(ar/flymake-double-arrow flymake-note))

  ;; UI: Nerd Icons in Diagnostics Buffer (SPC c B)
  (defun ar/flymake-diagnostics-buffer-icons ()
    "Apply nerd-icons to the Flymake diagnostics buffer."
    (when (derived-mode-p 'flymake-diagnostics-mode)
      (save-excursion
        (goto-char (point-min))
        (while (re-search-forward "^\\s-*\\(error\\|warning\\|note\\)" nil t)
          (let* ((type (match-string 1))
                 (icon (pcase type
                         ("error"   (nerd-icons-faicon "nf-fa-times_circle" :face 'flymake-error))
                         ("warning" (nerd-icons-faicon "nf-fa-exclamation_circle" :face 'flymake-warning))
                         ("note"    (nerd-icons-faicon "nf-fa-info_circle" :face 'flymake-note)))))
            (add-text-properties (match-beginning 1) (match-end 1) `(display ,icon)))))))
  (add-hook 'flymake-diagnostics-mode-hook #'ar/flymake-diagnostics-buffer-icons)

  ;; Navigation Logic
  (defun ar/flymake-next-error () (interactive) (flymake-goto-next-error 1 '(:error :warning) t))
  (defun ar/flymake-prev-error () (interactive) (flymake-goto-prev-error 1 '(:error :warning) t)))

(use-package flymake-popon
  :hook (flymake-mode . flymake-popon-mode)
  :custom
  (flymake-popon-method 'posframe)
  (flymake-popon-delay 0.2)
  :config
  ;; Border logic to match your custom posframe configuration
  (set-face-attribute 'flymake-popon-posframe-border nil 
                      :background (face-attribute 'posframe-border :background nil t)))
#+end_src

** Formatting
#+begin_src emacs-lisp
(use-package apheleia
  :hook (after-init . apheleia-global-mode)
  :custom
  (apheleia-log-only-errors t)
  :config
  ;; === THE CORRECTED LSP BRIDGE ===
  ;; We use cl-defun because Apheleia v3+ passes arguments as keywords.
  ;; Note: 'buffer' is the original buffer, 'scratch' is where formatting should happen.
  (cl-defun ar/apheleia-format-with-eglot (&key buffer scratch callback &allow-other-keys)
    "Format with Eglot if available; otherwise return nil to fallback."
    (with-current-buffer buffer
      (if (and (bound-and-true-p eglot--managed-mode)
               (eglot-server-capable :documentFormattingProvider))
          (progn
            ;; Silence eglot messages during the background format
            (cl-letf (((symbol-function 'message) #'ignore))
              (eglot-format-buffer))
            ;; After eglot-format-buffer runs on the original, 
            ;; we copy the result to scratch so Apheleia can diff it.
            (with-current-buffer scratch
              (erase-buffer)
              (insert-buffer-substring buffer))
            ;; Tell Apheleia we are done and provide the formatted scratch buffer
            (funcall callback scratch))
        ;; Return nil to tell Apheleia to move to the next CLI tool in the list
        (funcall callback nil))))

  ;; Add the corrected eglot bridge to formatters
  (push '(eglot . ar/apheleia-format-with-eglot) apheleia-formatters)

  ;; Inhibit for prose where whitespace is sensitive
  (setq apheleia-inhibit-functions
        (list (lambda () (derived-mode-p 'org-mode)))))
#+end_src

** Hydra
#+begin_src emacs-lisp
(pretty-hydra-define dev-hydra
  (:title (pretty-hydra-title "Development" 'codicon "nf-cod-code" :face 'nerd-icons-blue)
   :color amaranth :quit-key ("q" "C-g"))
  ("LSP (Eglot)"
   (("a" eglot-code-actions "actions")
    ("r" eglot-rename "rename")
    ("d" xref-find-definitions "definition")
    ("D" xref-find-references "references")
    ("s" consult-eglot-symbols "symbols")
    ("h" eldoc "hover doc"))
   
   "Syntax (Flymake)"
   (("n" ar/flymake-next-error "next error")
    ("p" ar/flymake-prev-error "prev error")
    ("b" flymake-show-buffer-diagnostics "buffer list")
    ("B" consult-flymake "project list")
    ("!" flymake-running-backends "backends"))

   "Formatting"
   (("f" apheleia-format-buffer "format buffer")
    ("F" (lambda () (interactive) (let ((apheleia-formatter 'eglot)) (apheleia-format-buffer))) "format w/ LSP"))

   "Toggles"
   (("t l" eglot-managed-p "LSP mode" :toggle t)
    ("t s" flymake-mode "syntax mode" :toggle t)
    ("t f" apheleia-mode "auto-format" :toggle t)
    ("t e" eldoc-mode "eldoc" :toggle t)
    ("t p" flymake-popon-mode "popon tooltips" :toggle t))))
#+end_src

* Podman
** Container Configuration
#+begin_src emacs-lisp
;; (defcustom ar/dev-container-name "dev-env"
;;   "Name of the development container."
;;   :type 'string
;;   :group 'ar)

;; (defcustom ar/dev-container-image "dev-env:latest"
;;   "Podman image name for the development container."
;;   :type 'string
;;   :group 'ar)

;; (defcustom ar/dev-container-user "devuser"
;;   "Username inside the container."
;;   :type 'string
;;   :group 'ar)

;; (defcustom ar/dev-container-workspace "/home/devuser/workspace"
;;   "Workspace path inside the container."
;;   :type 'string
;;   :group 'ar)
#+end_src

** TRAMP Configuration for Podman
#+begin_src emacs-lisp
;; (with-eval-after-load 'tramp
;;   (setq remote-file-name-inhibit-locks t
;;         remote-file-name-inhibit-auto-save-visited t
;;         tramp-use-scp-direct-remote-copying t
;;         tramp-verbose 1
;;         tramp-default-remote-shell "/bin/bash")

;;   ;; Enable direct async process (Critical for Eglot performance)
;;   (connection-local-set-profile-variables
;;    'remote-direct-async
;;    '((tramp-direct-async-process . t)))

;;   ;; Add all paths where LSP servers are installed
;;   (connection-local-set-profile-variables
;;    'container-path
;;    '((tramp-remote-path . (tramp-own-remote-path
;;                            "/usr/local/bin"
;;                            "/home/devuser/.cargo/bin"
;;                            tramp-default-remote-path))))

;;   (connection-local-set-profiles
;;    '(:application tramp :protocol "podman")
;;    'container-path 'remote-direct-async))
#+end_src

** Eglot Integration
#+begin_src emacs-lisp
;; (with-eval-after-load 'eglot
;;   ;; Optimize Eglot for TRAMP
;;   (connection-local-set-profile-variables
;;    'eglot-container
;;    '((eglot-connect-timeout . 60)
;;      (eglot-sync-connect . nil)
;;      (eglot-events-buffer-config . (:size 0))))

;;   (connection-local-set-profiles
;;    '(:application tramp :protocol "podman")
;;    'eglot-container))
#+end_src

** Dockerfile Editing Support
#+begin_src emacs-lisp
(use-package dockerfile-ts-mode
  :ensure nil
  :mode ("Dockerfile\\'" "\\.dockerfile\\'"
         "Containerfile\\'" "\\.containerfile\\'"
         "Dockerfile\\..*\\'" "Containerfile\\..*\\'")
  :config
  (with-eval-after-load 'eglot
    (put 'dockerfile-ts-mode 'eglot-language-id "dockerfile")
    (add-to-list 'eglot-server-programs
                 '(dockerfile-ts-mode . ("docker-language-server" "start"))))
  
  ;; Inject Bash grammar into RUN instructions for better syntax highlighting
  (defun ar/dockerfile-inject-bash ()
    "Inject Bash grammar into shell commands."
    (when (treesit-ready-p 'bash)
      (setq-local treesit-range-settings
                  (treesit-range-rules
                   :embed 'bash
                   :host 'dockerfile
                   '((run_instruction (shell_command) @injection.content))))
      (treesit-major-mode-setup)))
  
  (add-hook 'dockerfile-ts-mode-hook #'ar/dockerfile-inject-bash))
#+end_src

** Docker/Podman Management
#+begin_src emacs-lisp
;; (use-package docker
;;   :defer t
;;   :commands (docker docker-containers docker-images)
;;   :bind ("C-c C-p" . container-hydra/body)
;;   :config
;;   ;; Force docker.el to use Podman
;;   (setq docker-command "podman"
;;         docker-compose-command "podman-compose"
;;         docker-container-tramp-method "podman")
  
;;   ;; Define the Hydra explicitly here to avoid 'use-package' keyword errors
;;   (pretty-hydra-define container-hydra
;;     (:title (pretty-hydra-title "Container Management" 'devicon "nf-dev-docker" :face 'nerd-icons-blue)
;;      :color amaranth :quit-key ("q" "C-g"))
;;     ("Lifecycle"
;;      (("r" ar/podman-run "create new (run)" :exit t)
;;       ("s" ar/podman-start "resume (start)")
;;       ("S" ar/podman-stop "pause (stop)")
;;       ("R" ar/podman-restart "restart")
;;       ("k" ar/podman-kill "kill")
;;       ("x" ar/podman-remove "destroy (rm)"))
     
;;      "Access"
;;      (("o" ar/podman-open "open workspace" :exit t)
;;       ("t" ar/podman-shell "terminal" :exit t)
;;       ("e" ar/podman-exec "exec command" :exit t)
;;       ("l" ar/podman-logs "view logs" :exit t))
     
;;      "Images & Info"
;;      (("b" ar/podman-build "build image" :exit t)
;;       ("c" docker-containers "list containers" :exit t)
;;       ("i" docker-images "list images" :exit t)
;;       ("?" ar/podman-status "status check")))))
#+end_src

** Container Management Functions
#+begin_src emacs-lisp
;; (defun ar/podman--exists-p ()
;;   "Check if container exists (running or stopped).
;; Checks if 'podman ps -a' returns a non-empty string."
;;   (with-temp-buffer
;;     (call-process "podman" nil t nil "ps" "-a" "-q" "-f" (format "name=^%s$" ar/dev-container-name))
;;     (> (length (string-trim (buffer-string))) 0)))

;; (defun ar/podman--running-p ()
;;   "Check if container is currently running.
;; Checks if 'podman ps' returns a non-empty string."
;;   (with-temp-buffer
;;     (call-process "podman" nil t nil "ps" "-q" "-f" (format "name=^%s$" ar/dev-container-name))
;;     (> (length (string-trim (buffer-string))) 0)))

;; (defun ar/podman-build ()
;;   "Build the container image. Prefers 'build-podman.sh' if available."
;;   (interactive)
;;   (let* ((dockerfile-dir (locate-dominating-file default-directory "Dockerfile"))
;;          (build-script (and dockerfile-dir (expand-file-name "build-podman.sh" dockerfile-dir))))
;;     (unless dockerfile-dir
;;       (user-error "No Dockerfile found in project hierarchy"))
    
;;     (let ((default-directory dockerfile-dir)
;;           (compile-command (if (and build-script (file-exists-p build-script))
;;                                (format "./%s" (file-name-nondirectory build-script))
;;                              (format "podman build --build-arg USER_UID=$(id -u) --build-arg USER_GID=$(id -g) -t %s ." 
;;                                      ar/dev-container-image))))
;;       (compile compile-command))))

;; (defun ar/podman-run ()
;;   "Create and run a new persistent development container. Prefers 'run-podman.sh'."
;;   (interactive)
;;   (if (ar/podman--exists-p)
;;       (user-error "Container '%s' exists. Use 'ar/podman-start' or remove it first"
;;                   ar/dev-container-name)
;;     (let* ((project-root (if (project-current)
;;                              (project-root (project-current))
;;                            (read-directory-name "Project root: ")))
;;            (run-script (expand-file-name "run-podman.sh" project-root)))
      
;;       (let ((command (if (file-exists-p run-script)
;;                          (format "./%s" (file-name-nondirectory run-script))
;;                        (format "podman run -d --name %s --hostname devbox --userns=keep-id -v %s:%s -p 3000:3000 -p 8080:8080 %s tail -f /dev/null"
;;                                ar/dev-container-name
;;                                (expand-file-name project-root)
;;                                ar/dev-container-workspace
;;                                ar/dev-container-image))))
;;         (let ((default-directory project-root))
;;           (async-shell-command command "*podman-run*")
;;           (message "Creating container '%s'..." ar/dev-container-name))))))

;; (defun ar/podman-start () 
;;   "Start the persistent container."
;;   (interactive) 
;;   (if (ar/podman--running-p)
;;       (message "Container '%s' is already running." ar/dev-container-name)
;;     (async-shell-command (format "podman start %s" ar/dev-container-name) "*podman-start*")))

;; (defun ar/podman-stop () 
;;   "Stop the persistent container."
;;   (interactive) 
;;   (if (ar/podman--running-p)
;;       (async-shell-command (format "podman stop %s" ar/dev-container-name) "*podman-stop*")
;;     (message "Container '%s' is not running." ar/dev-container-name)))

;; (defun ar/podman-kill () 
;;   (interactive) 
;;   (when (yes-or-no-p (format "Force kill '%s'?" ar/dev-container-name))
;;     (async-shell-command (format "podman kill %s" ar/dev-container-name) "*podman-kill*")))

;; (defun ar/podman-remove () 
;;   (interactive)
;;   (if (ar/podman--running-p)
;;       (user-error "Stop container first.")
;;     (when (yes-or-no-p (format "Remove container '%s'?" ar/dev-container-name))
;;       (async-shell-command (format "podman rm %s" ar/dev-container-name) "*podman-rm*"))))

;; (defun ar/podman-restart ()
;;   (interactive)
;;   (async-shell-command (format "podman restart %s" ar/dev-container-name) "*podman-restart*"))

;; (defun ar/podman-open ()
;;   "Open workspace in container via TRAMP.
;; Includes TRAMP cleanup to fix connection errors if container was restarted."
;;   (interactive)
;;   (if (ar/podman--running-p)
;;       (find-file (format "/podman:%s@%s:%s"
;;                         ar/dev-container-user
;;                         ar/dev-container-name
;;                         ar/dev-container-workspace))
;;     (when (y-or-n-p (format "Container '%s' is not running. Start it? "
;;                            ar/dev-container-name))
;;       ;; Cleanup TRAMP cache to prevent 'Error: can only create exec sessions'
;;       (tramp-cleanup-connection 
;;        (tramp-dissect-file-name (format "/podman:%s@%s:" 
;;                                       ar/dev-container-user 
;;                                       ar/dev-container-name)) 
;;        nil nil)
;;       (ar/podman-start)
;;       (run-with-timer 2 nil
;;                      (lambda ()
;;                        (find-file (format "/podman:%s@%s:%s"
;;                                         ar/dev-container-user
;;                                         ar/dev-container-name
;;                                         ar/dev-container-workspace)))))))

;; ;; ;; Uses 'vterm' to open in the running container
;; ;; (defun ar/podman-shell ()
;; ;;   "Open Vterm in the running container."
;; ;;   (interactive)
;; ;;   (if (ar/podman--running-p)
;; ;;       (let ((default-directory (format "/podman:%s@%s:%s"
;; ;;                                       ar/dev-container-user
;; ;;                                       ar/dev-container-name
;; ;;                                       ar/dev-container-workspace)))
;; ;;         ;; Opens vterm. If a vterm buffer exists, it opens a new one.
;; ;;         (vterm (format "*vterm-%s*" ar/dev-container-name)))
;; ;;     (user-error "Container '%s' is not running." ar/dev-container-name)))


;; ;; Uses 'eshell' to open in the running container
;; (defun ar/podman-shell ()
;;   "Open Eshell in the running container."
;;   (interactive)
;;   (if (ar/podman--running-p)
;;       (let ((default-directory (format "/podman:%s@%s:%s"
;;                                       ar/dev-container-user
;;                                       ar/dev-container-name
;;                                       ar/dev-container-workspace)))
;;         ;; Pass 't' to create a new eshell buffer if one exists
;;         (eshell t))
;;     (user-error "Container '%s' is not running." ar/dev-container-name)))

;; (defun ar/podman-exec (command)
;;   "Execute COMMAND in the running container."
;;   (interactive "sCommand: ")
;;   (if (ar/podman--running-p)
;;       (async-shell-command
;;        (format "podman exec %s %s" ar/dev-container-name command)
;;        (format "*podman-exec-%s*" ar/dev-container-name))
;;     (user-error "Container '%s' is not running" ar/dev-container-name)))

;; (defun ar/podman-logs ()
;;   "View logs from the container."
;;   (interactive)
;;   (if (ar/podman--exists-p)
;;       (async-shell-command
;;        (format "podman logs -f %s" ar/dev-container-name)
;;        (format "*podman-logs-%s*" ar/dev-container-name))
;;     (user-error "Container '%s' doesn't exist" ar/dev-container-name)))

;; (defun ar/podman-status ()
;;   "Display container status information."
;;   (interactive)
;;   (cond
;;    ((not (ar/podman--exists-p))
;;     (message "Container '%s' does not exist" ar/dev-container-name))
;;    ((ar/podman--running-p)
;;     (message "Container '%s' is RUNNING" ar/dev-container-name))
;;    (t
;;     (message "Container '%s' exists but is STOPPED" ar/dev-container-name))))
#+end_src

* Version Control
** Magit: The Git Porcelain
#+begin_src emacs-lisp
(use-package transient :defer t)

(use-package magit
  :defer t
  :commands (magit-status 
             magit-blame 
             magit-log-buffer-file
             magit-stage-file
             magit-unstage-file
             magit-stage
             magit-unstage
             magit-commit
             magit-push-current-to-pushremote
             magit-pull-from-upstream
             magit-fetch
             magit-run-git
             magit-branch
             magit-merge
             magit-rebase
             magit-diff-unstaged
             magit-diff-staged
             magit-stash
             magit-dispatch)
  :custom
  ;; Display settings
  (magit-display-buffer-function #'magit-display-buffer-fullframe-status-v1)
  (magit-bury-buffer-function #'magit-restore-window-configuration)
  
  ;; Performance
  (magit-refresh-status-buffer t)
  (magit-refresh-verbose nil)
  (magit-git-executable "git")
  
  ;; Behavior
  (magit-save-repository-buffers 'dontask)
  (magit-commit-ask-to-stage 'stage)
  (magit-commit-show-diff t)
  (magit-revision-show-gravatars nil)
  
  ;; Diff settings
  (magit-diff-refine-hunk 'all)
  (magit-diff-paint-whitespace t)
  (magit-diff-highlight-trailing t)
  
  :config
  ;; Only use Git. Disables bzr, svn, hg, etc. to prevent TRAMP errors.
  (setq vc-handled-backends '(Git))
  ;; Enable gravatars if desired (requires imagemagick)
  (when (and (display-graphic-p)
             (image-type-available-p 'imagemagick))
    (setq magit-revision-show-gravatars '("^Author:     " . "^Commit:     "))))
#+end_src

** Forge: GitHub/GitLab Integration
#+begin_src emacs-lisp
(use-package forge
  :after magit
  :defer t
  :config
  ;; Configure forge to use sqlite for storage
  (setq forge-database-connector 'sqlite-builtin))
#+end_src

** Magit Todos: Track TODOs in Commits
#+begin_src emacs-lisp
(use-package magit-todos
  :after magit
  :defer t
  :commands (magit-todos-mode)
  :hook (magit-status-mode . magit-todos-mode)
  :config
  (setq magit-todos-keywords-list '("TODO" "FIXME" "HACK" "REVIEW" "NOTE" "DEPRECATED"))
  
  ;; Integrate with hl-todo for consistent highlighting
  (with-eval-after-load 'hl-todo
    (setq magit-todos-keyword-suffix "\\(?:([^)]+)\\)?:?"))
  
  ;; Optional: Custom scanner for better performance
  (when (executable-find "rg")
    (setq magit-todos-scanner #'magit-todos--scan-with-rg)))

(with-eval-after-load 'magit
  (add-hook 'magit-log-wash-summary-hook
            #'hl-todo-search-and-highlight t)
  (add-hook 'magit-revision-wash-message-hook
            #'hl-todo-search-and-highlight t))
#+end_src

** Git-Timemachine: Step Through File History
#+begin_src emacs-lisp
(use-package git-timemachine
  :defer t
  :commands (git-timemachine git-timemachine-toggle)
  
  :init
  ;; CRITICAL FIX: Only configure native-comp if it's actually available
  (when (and (fboundp 'native-comp-available-p)
             (native-comp-available-p))
    (with-eval-after-load 'comp
      (when (boundp 'native-comp-deferred-compilation-deny-list)
        (add-to-list 'native-comp-deferred-compilation-deny-list "git-timemachine"))))
  
  ;; Disable interfering modes
  (with-eval-after-load 'lsp-mode
    (add-to-list 'lsp-diagnostics-disabled-modes 'git-timemachine-mode))
  
  :custom
  (git-timemachine-abbreviation-length 7)
  (git-timemachine-show-minibuffer-details t)
  
  :config
  ;; Improve integration with other modes
  (defun ar/git-timemachine-setup ()
    "Disable interfering modes when entering git-timemachine."
    ;; Disable git-gutter to avoid conflicts
    (when (bound-and-true-p git-gutter-mode)
      (git-gutter-mode -1)
      (setq-local ar/git-gutter-was-active t))
    
    ;; Disable flycheck/flymake (we're viewing history, not editing)
    (when (bound-and-true-p flycheck-mode)
      (flycheck-mode -1)
      (setq-local ar/flycheck-was-active t))
    
    (when (bound-and-true-p flymake-mode)
      (flymake-mode -1)
      (setq-local ar/flymake-was-active t))
    
    ;; Make buffer read-only
    (read-only-mode 1))
  
  (defun ar/git-timemachine-teardown ()
    "Re-enable modes when exiting git-timemachine."
    ;; Restore git-gutter
    (when (bound-and-true-p ar/git-gutter-was-active)
      (git-gutter-mode 1)
      (kill-local-variable 'ar/git-gutter-was-active))
    
    ;; Restore flycheck
    (when (bound-and-true-p ar/flycheck-was-active)
      (flycheck-mode 1)
      (kill-local-variable 'ar/flycheck-was-active))
    
    ;; Restore flymake
    (when (bound-and-true-p ar/flymake-was-active)
      (flymake-mode 1)
      (kill-local-variable 'ar/flymake-was-active))
    
    ;; Disable read-only
    (read-only-mode -1))
  
  (add-hook 'git-timemachine-mode-hook #'ar/git-timemachine-setup)
  (add-hook 'git-timemachine-mode-exit-hook #'ar/git-timemachine-teardown))
#+end_src

** Git-Gutter: Show Changes in Fringe
#+begin_src emacs-lisp
(use-package git-gutter
  :defer t
  :commands (git-gutter-mode 
             git-gutter:next-hunk 
             git-gutter:previous-hunk 
             git-gutter:stage-hunk 
             git-gutter:revert-hunk 
             git-gutter:popup-hunk)
  
  :hook ((prog-mode . git-gutter-mode)
         (conf-mode . git-gutter-mode)
         (org-mode . git-gutter-mode)
         (markdown-mode . git-gutter-mode)
         (text-mode . git-gutter-mode))
  
  :init
  (setq git-gutter:update-interval 0.5
        git-gutter:ask-p nil
        git-gutter:modified-sign "▎"
        git-gutter:added-sign "▎"
        git-gutter:deleted-sign "▁"
        git-gutter:hide-gutter t)
  
  :config
  ;; Jump to next/previous hunk with wrapping
  (defun ar/git-gutter-next-hunk ()
    "Jump to next hunk, wrapping around."
    (interactive)
    (if (git-gutter:search-here-or-after-p (point) t)
        (git-gutter:next-hunk 1)
      (progn
        (goto-char (point-min))
        (git-gutter:next-hunk 1))))
  
  (defun ar/git-gutter-previous-hunk ()
    "Jump to previous hunk, wrapping around."
    (interactive)
    (if (git-gutter:search-here-or-before-p (point))
        (git-gutter:previous-hunk 1)
      (progn
        (goto-char (point-max))
        (git-gutter:previous-hunk 1))))
  
  ;; Enable globally with smart exclusions
  (global-git-gutter-mode +1))
#+end_src

** Git-Gutter-Fringe: Better Visual Appearance
#+begin_src emacs-lisp
(use-package git-gutter-fringe
  :after git-gutter
  :demand t
  :config
  ;; Clean, modern vertical bars in the fringe
  (define-fringe-bitmap 'git-gutter-fr:added 
    [224] nil nil '(center repeated))
  (define-fringe-bitmap 'git-gutter-fr:modified 
    [224] nil nil '(center repeated))
  (define-fringe-bitmap 'git-gutter-fr:deleted 
    [128 192 224 240] nil nil 'bottom)
  
  ;; Tokyo Night Colors (adjust to match your theme)
  :custom-face
  (git-gutter-fr:added    ((t (:foreground "#9ece6a"))))
  (git-gutter-fr:modified ((t (:foreground "#e0af68"))))
  (git-gutter-fr:deleted  ((t (:foreground "#f7768e")))))
#+end_src

** Magit Custom Functions
#+begin_src emacs-lisp
(defun ar/magit-stage-all ()
  "Stage all changes in the project (equivalent to 'git add .')."
  (interactive)
  (magit-run-git "add" ".")
  (message "Staged all changes."))
#+end_src

** Comprehensive VC Hydra
#+begin_src emacs-lisp
(with-eval-after-load 'hydra
  (pretty-hydra-define version-control-hydra
    (:title (pretty-hydra-title "Version Control" 'octicon "nf-oct-git_branch" :face 'nerd-icons-green)
     :color amaranth
     :quit-key ("q" "C-g"))
    
    ("Magit Core"
     (("s" magit-status "status" :exit t)
      ("a" ar/magit-stage-all "stage all") 
      ("c" magit-commit "commit" :exit t)
      ("p" magit-push-current-to-pushremote "push")
      ("P" magit-pull-from-upstream "pull")
      ("f" magit-fetch "fetch")
      ("F" magit-fetch-all "fetch all"))
     
     "Branch & Log"
     (("b" magit-branch "branches" :exit t)
      ("B" magit-checkout "checkout" :exit t)
      ("m" magit-merge "merge" :exit t)
      ("r" magit-rebase "rebase" :exit t)
      ("l" ar/magit-log-buffer-file-and-follow "log file" :exit t)
      ("L" magit-log-current "log branch" :exit t))
     
     "Diff & Stage"
     (("d" magit-diff-unstaged "diff unstaged" :exit t)
      ("D" magit-diff-staged "diff staged" :exit t)
      ("S" magit-stage-file "stage file")
      ("u" magit-unstage-file "unstage file")
      ("U" magit-unstage-all "unstage all"))
     
     "Advanced"
     (("z" magit-stash "stash" :exit t)
      ("Z" magit-stash-pop "stash pop" :exit t)
      ("x" magit-reset "reset" :exit t)
      ("!" magit-run "run cmd" :exit t)
      ("R" magit-refresh "refresh")
      ("?" magit-dispatch "dispatch" :exit t)))))
#+end_src

* Workflow Management
** Dired
#+begin_src emacs-lisp
(use-package dired
  :ensure nil
  :commands (dired dired-jump)
  :hook (dired-mode . dired-hide-details-mode)
  :hook (dired-mode . hl-line-mode)
  :custom 
  ;; Doom-like cleanup: Don't confirm simple recursive deletes
  (dired-recursive-copies 'always)
  (dired-recursive-deletes 'top)
  (delete-by-moving-to-trash t)
  (dired-dwim-target t)
  
  ;; Performance & UX
  (dired-auto-revert-buffer t)
  (dired-kill-when-opening-new-dired-buffer t) 
  (insert-directory-program "/usr/bin/ls")
  (dired-listing-switches "-l --almost-all --human-readable --time-style=long-iso --group-directories-first --no-group"))

#+end_src

** Dirvish
#+begin_src emacs-lisp
(use-package dirvish
  :after dired
  :init
  ;; MAGIC: This makes ALL dired commands use Dirvish interfaces
  (dirvish-override-dired-mode)
  
  :custom
  (dirvish-cache-dir (expand-file-name "dirvish/" user-emacs-directory))
  
  ;; Restored your specific layout ratios
  (dirvish-default-layout '(1 0.11 0.55))
  (dirvish-layout-recipes
   '((0 0   0.4)   ; No parent, smaller preview
     (1 0.1 0.5)   ; Balanced layout
     (1 0.15 0.6))) ; More preview space

  ;; Restored your shortcuts
  (dirvish-quick-access-entries
   `(("h" "~/" "Home")
     ("e" ,user-emacs-directory "Emacs")
     ("d" "~/Downloads/" "Downloads")
     ("D" "~/Documents/" "Documents")
     ("p" "~/projects/" "Projects")
     ("o" ,my/org-directory "Org")
     ("c" "~/.config/" "Config")
     ("t" "~/.local/share/Trash/files/" "Trash")))

 ;; Attributes
  (dirvish-attributes
   '(subtree-state nerd-icons collapse vc-state))
  
  ;; Doom-like UI: Headers and Mode lines
  (dirvish-use-header-line t)
  (dirvish-use-mode-line t)
  (dirvish-header-line-height '(25 . 35))
  (dirvish-mode-line-height 25)
  (dirvish-header-line-format '(:left (path) :right (free-space)))
  (dirvish-mode-line-format
   '(:left (sort file-time " " file-size symlink)
     :right (omit yank index)))
  
  ;; Restored: Ranger-like session behavior (kill on quit)
  (dirvish-reuse-session nil)
  (dirvish-hide-cursor t)
  (dirvish-hide-details '(dirvish dirvish-side))
  
  ;; Restored: Emerge groups
  (dirvish-emerge-groups
   '(("Recent files" (predicate . recent-files-2h))
     ("Documents" (extensions "pdf" "tex" "bib" "epub"))
     ("Video" (extensions "mp4" "mkv" "webm"))
     ("Pictures" (extensions "jpg" "png" "svg" "gif"))
     ("Audio" (extensions "mp3" "flac" "wav" "ape"))
     ("Archives" (extensions "gz" "rar" "zip"))))
  
  ;; Previews
  (dirvish-preview-dispatchers
   '(image gif video audio epub archive pdf))
  
  :config
  (setq dirvish-mode-line-format
        '(:left (sort symlink) :right (omit yank index)))
  ;; open large directory (over 20000 files) asynchronously with `fd' command
  (setq dirvish-large-directory-threshold 20000)
  (require 'dirvish-fd)
  (require 'dirvish-emerge)

  (require 'dirvish-side)
  
  ;; Side window configuration (Restored to Left side)
  (setq dirvish-side-width 35
        dirvish-side-follow-buffer-file t)
        dirvish-side-display-alist '((side . bottom) (slot . -1))


        )      
#+end_src


** File Management Extensions
#+begin_src emacs-lisp
(use-package dired-x
  :ensure nil
  :hook (dired-mode . dired-omit-mode)
  :config
  (setq dired-omit-verbose nil
        dired-omit-files
        (concat "^\\.?#\\|^\\.$\\|^\\.\\.$"
                "\\|^\\.DS_Store\\'"
                "\\|^flycheck_.*"
                "\\|^\\.project\\(?:ile\\)?\\'"
                "\\|^\\.\\(?:svn\\|git\\)\\'"
                "\\|^\\.ccls-cache\\'"
                "\\|\\(?:\\.js\\)?\\.meta\\'"
                "\\|\\.\\(?:elc\\|o\\|pyo\\|swp\\|class\\)\\'"))
 
  (setq dired-clean-confirm-killing-deleted-buffers nil))

(use-package diredfl
  :hook
  ((dired-mode . diredfl-mode)
   ;; highlight parent and directory preview as well
   (dirvish-directory-view-mode . diredfl-mode))
  :config
  (set-face-attribute 'diredfl-dir-name nil :bold t))


(use-package wdired
  :ensure nil
  :after dired
  :custom
  (wdired-allow-to-change-permissions t)
  (wdired-create-parent-directories t))

#+end_src

** iBuffer
#+begin_src emacs-lisp
(use-package ibuffer
  :defer t
  :ensure nil
  :init (setq ibuffer-filter-group-name-face '(:inherit (font-lock-string-face bold))))

;; Display icons for buffers
(use-package nerd-icons-ibuffer
  :defer t
  :hook (ibuffer-mode . nerd-icons-ibuffer-mode)
  :init
  (setq nerd-icons-ibuffer-icon t)
  (setq nerd-icons-ibuffer-color-icon t)
  (setq nerd-icons-ibuffer-icon-size 1.0)
  (setq nerd-icons-ibuffer-human-readable-size t))

;; Group ibuffer's list by project
(use-package ibuffer-project
  :defer t
  :autoload (ibuffer-project-generate-filter-groups ibuffer-do-sort-by-project-file-relative)
  :functions icons-displayable-p
  :hook (ibuffer . (lambda ()
                     "Group ibuffer's list by project."
                     (setq ibuffer-filter-groups (ibuffer-project-generate-filter-groups))
                     (unless (eq ibuffer-sorting-mode 'project-file-relative)
                       (ibuffer-do-sort-by-project-file-relative))))
  :init (setq ibuffer-project-use-cache t)
  :config
  (with-no-warnings
    (defun my-ibuffer-project-group-name (root type)
      "Return group name for project ROOT and TYPE."
      (if (and (stringp type) (> (length type) 0))
          (format "%s %s" type root)
        (format "%s" root)))
    (if (icons-displayable-p)
        (progn
          (advice-add #'ibuffer-project-group-name :override #'my-ibuffer-project-group-name)
          (setq ibuffer-project-root-functions
                `((ibuffer-project-project-root . ,(nerd-icons-octicon "nf-oct-repo" :height 1.2 :face ibuffer-filter-group-name-face))
                  (file-remote-p . ,(nerd-icons-codicon "nf-cod-radio_tower" :height 1.2 :face ibuffer-filter-group-name-face)))))
      (progn
        (advice-remove #'ibuffer-project-group-name #'my-ibuffer-project-group-name)
        (setq ibuffer-project-root-functions
              '((ibuffer-project-project-root . "Project")
                (file-remote-p . "Remote")))))))
#+end_src

** Tabspaces
#+begin_src emacs-lisp
(use-package tabspaces
  :hook (after-init . tabspaces-mode)
  :custom
  (tab-bar-show nil) ;; We use the modeline/consult to visualize tabs
  
  ;; Filter buffers by default so Project A only sees Project A's buffers
  (tabspaces-use-filtered-buffers-as-default t)
  (tabspaces-default-tab "Default")
  (tabspaces-remove-to-default t)
  (tabspaces-include-buffers '("*scratch*" "*Messages*"))
  
  ;; CRITICAL FIX: Set to nil to ensure terminals are ISOLATED to their workspace.
  ;; If we added *vterm* here, it would be visible in ALL tabs (Global).
  (tabspaces-exclude-buffers nil)

  ;; Session management settings
  (tabspaces-session t)
  (tabspaces-session-auto-restore nil) ; We use a custom robust timer for this

  :config
  ;; --- Consult Integration ---
  ;; This safety check is REQUIRED because consult might not be loaded yet.
  (with-eval-after-load 'consult
    ;; Hide the full buffer list by default (so we rely on the workspace source)
    (consult-customize consult--source-buffer :hidden t :default nil)

    ;; Define a source that lists ONLY buffers in the current tabspace
    (defvar consult--source-workspace
      (list :name     "Workspace Buffer"
            :narrow   ?w
            :history  'buffer-name-history
            :category 'buffer
            :state    #'consult--buffer-state
            :default  t
            :items    (lambda () (consult--buffer-query
                               :predicate #'tabspaces--local-buffer-p
                               :sort 'visibility
                               :as #'buffer-name)))
      "Set workspace buffer list for consult-buffer.")
    
    ;; Add the workspace source to consult
    (add-to-list 'consult-buffer-sources 'consult--source-workspace))

  ;; --- Crash Prevention & Cleanup ---

  (defun my-tabspaces-delete-childframe (&rest _)
    "Delete all child frames (posframes) before saving session to prevent read errors."
    (when (and (fboundp 'posframe-delete-all)
               (featurep 'posframe))
      (ignore-errors (posframe-delete-all))))

  (defun my-tabspaces-clean-markers (&rest _)
    "Clean up dead markers in buffer-local variables to prevent invalid-read-syntax errors."
    (dolist (buf (buffer-list))
      (with-current-buffer buf
        (dolist (var (buffer-local-variables))
          (when (and (consp var) (markerp (cdr var)))
            (let ((marker (cdr var)))
              (unless (and (marker-buffer marker)
                           (buffer-live-p (marker-buffer marker)))
                (set (car var) nil))))))))

  (defun my-tabspaces-bury-window (&rest _)
    "Bury *Messages* buffer after restoring session to keep the view clean."
    (run-with-idle-timer 0.5 nil
      (lambda ()
        (ignore-errors
          (when-let ((msg-buf (get-buffer "*Messages*")))
            (quit-windows-on msg-buf))))))

  ;; --- Session Management Wrappers ---

  (defun my-tabspaces-save-wrapper (orig-fun &rest args)
    "Advice wrapper to clean environment before saving tabspaces."
    (my-tabspaces-delete-childframe)
    (my-tabspaces-clean-markers)
    (apply orig-fun args))

  (defun my-tabspaces-safe-restore ()
    "Safely restore tabspaces session with error handling."
    (interactive)
    ;; Don't restore if Emacs was launched with a specific file argument
    (unless command-line-args-left
      (condition-case err
          (progn
            (tabspaces-restore-session)
            (message "Tabspaces session restored."))
        (error
         (message "Tabspaces restore failed: %s" (error-message-string err))
         ;; Fallback: Create a default tab if restore fails
         (ignore-errors
           (tab-bar-new-tab)
           (tab-bar-rename-tab "Default"))
         nil))))

  (defun my-tabspaces-safe-save ()
    "Safely save tabspaces session with error handling (for timer)."
    (interactive)
    (condition-case err
        (progn
          (my-tabspaces-delete-childframe)
          (my-tabspaces-clean-markers)
          (tabspaces-save-session)
          (message "Tabspaces session saved."))
      (error
       (message "Tabspaces save failed: %s" (error-message-string err)))))

  ;; Apply Advice
  (advice-add 'tabspaces-save-session :around #'my-tabspaces-save-wrapper)
  (advice-add 'tabspaces-restore-session :after #'my-tabspaces-bury-window)

  ;; Auto-save session every 5 minutes when idle
  (run-with-idle-timer 300 t #'my-tabspaces-safe-save)

  ;; Attempt restore 2 seconds after startup
  (add-hook 'after-init-hook
            (lambda ()
              (run-with-idle-timer 2 nil #'my-tabspaces-safe-restore))))
#+end_src

** Treemacs
#+begin_src emacs-lisp
(use-package treemacs
  :defer t

  :custom-face
  ;; Matches the border color to your theme's posframe border
  (cfrs-border-color ((t (:inherit posframe-border))))
  :custom
  (treemacs-width 30)
  (treemacs-position 'left)
  (treemacs-is-never-other-window t) ;; Prevent 'other-window' from jumping into sidebar
  (treemacs-space-between-root-nodes t)
  (treemacs-silent-refresh t)
  (treemacs-silent-filewatch t)
  (treemacs-file-follow-delay 0.2)
  :config
  (treemacs-follow-mode t)      ;; Highlight file in sidebar when selected in buffer
  (treemacs-filewatch-mode t)   ;; Watch file system for changes
  (treemacs-fringe-indicator-mode 'always)

  ;; Check for python3 and git to determine how to run git integration
  (pcase (cons (not (null (executable-find "git")))
               (not (null (executable-find "python3"))))
    (`(t . t) (treemacs-git-mode 'deferred)) ;; Best performance (requires python3)
    (`(t . _) (treemacs-git-mode 'simple))))

;; Consistent icons with the rest of your config
(use-package treemacs-nerd-icons
  :after treemacs
  :functions treemacs-load-theme
  :config (treemacs-load-theme "nerd-icons"))

;; Magit integration (refresh sidebar on git changes)
(use-package treemacs-magit
  :after (treemacs magit)
  :hook ((magit-post-commit
          git-commit-post-finish
          magit-post-stage
          magit-post-unstage)
         . treemacs-magit--schedule-update))

;; Scope Treemacs to Tabs (Essential for Tabspaces integration)
(use-package treemacs-tab-bar
  :after treemacs
  :functions treemacs-set-scope-type
  :config (treemacs-set-scope-type 'Tabs))

;; Evil navigation in the sidebar
(use-package treemacs-evil
  :after (treemacs evil)
  :config
  (evil-define-key 'normal treemacs-mode-map
    (kbd "TAB") #'treemacs-TAB-action
    (kbd "o")   #'treemacs-visit-node-ace
    (kbd "q")   #'treemacs-quit))
#+end_src

* Markdown
#+begin_src emacs-lisp
(use-package markdown-mode
  :defer t
  :mode (("README\\.md\\'" . gfm-mode)
         ("\\.md\\'" . markdown-mode)
         ("\\.markdown\\'" . markdown-mode))
  :init
  ;; Basic settings
  (setq markdown-italic-underscore t
        markdown-asymmetric-header t
        markdown-fontify-code-blocks-natively t
        markdown-gfm-uppercase-checkbox t
        markdown-enable-math nil
        markdown-header-scaling t
        markdown-header-scaling-values '(1.2 1.15 1.1 1.05 1.0 1.0)
        markdown-display-message nil
        markdown-hide-urls t
        markdown-hide-markup t)

  ;; List indentation
  (setq markdown-list-indent-width 2)
  
  ;; PERFORMANCE OPTIMIZATIONS - Reduce fontification overhead
  (setq markdown-fontify-whole-heading-line nil
        markdown-fontify-quote-and-verse-blocks nil)
  
  ;; Defer expensive calculations
  (setq jit-lock-defer-time 0.05  ;; Small delay before fontifying
        jit-lock-stealth-time 1)   ;; Fontify invisible text after 1s idle

  :hook ((markdown-mode . eglot-ensure)
         (markdown-mode . visual-line-mode)
         (markdown-mode . ar/markdown-setup-rendering)
         (markdown-mode . ar/markdown-optimize-fontification))

  :config
  ;; EGLOT & MARKSMAN SETUP
  (with-eval-after-load 'eglot
    (add-to-list 'eglot-server-programs '(markdown-mode . ("marksman"))))
  
  ;; FORMATTING INTEGRATION
  (with-eval-after-load 'apheleia
    (setf (alist-get 'markdown-mode apheleia-mode-alist) '(eglot)))

  ;; FIX: Set imenu expression in :config after markdown-mode is loaded
  (setq markdown-imenu-generic-expression 
        '(("^\\(.*\\)[ \t]*\n=+$" 1)
          ("^\\(.*\\)[ \t]*\n-+$" 1)
          ("^\\(#+ .*\\)" 1)))
  
  ;; OPTIMIZATION: Reduce fontification frequency
  (defun ar/markdown-optimize-fontification ()
    "Optimize font-lock for markdown to reduce sluggishness."
    (setq-local font-lock-multiline t)
    (setq-local font-lock-maximum-decoration 2) 
    (setq-local jit-lock-chunk-size 1000)
    (setq-local jit-lock-defer-time 0.1))
  
  ;; DYNAMIC RENDERING: Detect when cursor is in markdown element
  (defun ar/markdown-in-element-p ()
    "Check if point is inside a markdown element that should show raw markup."
    (let ((face (get-text-property (point) 'face)))
      (or
       (and face 
            (or (eq face 'markdown-code-face)
                (eq face 'markdown-inline-code-face)
                (eq face 'markdown-pre-face)
                (eq face 'markdown-gfm-code-block-face)
                (and (listp face)
                     (or (memq 'markdown-code-face face)
                         (memq 'markdown-inline-code-face face)
                         (memq 'markdown-pre-face face)
                         (memq 'markdown-gfm-code-block-face face)))))
       (and face
            (or (eq face 'markdown-bold-face)
                (eq face 'markdown-italic-face)
                (and (listp face)
                     (or (memq 'markdown-bold-face face)
                         (memq 'markdown-italic-face face)))))
       (and face
            (or (eq face 'markdown-link-face)
                (eq face 'markdown-url-face)
                (and (listp face)
                     (or (memq 'markdown-link-face face)
                         (memq 'markdown-url-face face)))))
       (save-excursion
         (and (looking-at-p "[*_`\\[\\]()]")
              (or (looking-back "[*_`]" 1)
                  (looking-at "[*_`]")))))))
  
  ;; ANTI-FLICKER: Throttled markup toggling
  (defvar ar/markdown-toggle-timer nil
    "Timer to throttle markup visibility toggling.")
  
  (defun ar/markdown-toggle-markup-at-point ()
    "Toggle markup hiding based on cursor position."
    (when (derived-mode-p 'markdown-mode)
      (when ar/markdown-toggle-timer
        (cancel-timer ar/markdown-toggle-timer))
      (setq ar/markdown-toggle-timer
            (run-with-idle-timer 0.05 nil
              (lambda ()
                (when (buffer-live-p (current-buffer))
                  (with-current-buffer (current-buffer)
                    (if (ar/markdown-in-element-p)
                        (remove-text-properties (line-beginning-position) (line-end-position)
                                              '(display nil composition nil invisible nil))
                      (when markdown-hide-markup
                        (font-lock-flush (line-beginning-position) (line-end-position)))))))))))
  
  (defun ar/markdown-setup-rendering ()
    (add-hook 'post-command-hook #'ar/markdown-toggle-markup-at-point nil t))
  
  ;; CHECKBOX RENDERING
  (defface ar/markdown-checkbox-face
    '((t (:inherit markdown-list-face :box (:line-width 1 :style released-button))))
    "Face for markdown checkboxes.")
  
  (defun ar/markdown-fontify-checkboxes (last)
    (when (markdown-match-generic-metadata "^[ \t]*- \\[[ X]\\]" last)
      (let ((checkbox-begin (match-beginning 0))
            (checkbox-end (match-end 0))
            (checkbox-state (match-string 0)))
        (if (string-match-p "\\[X\\]" checkbox-state)
            (compose-region (+ checkbox-begin 2) (+ checkbox-begin 5) "☑")
          (compose-region (+ checkbox-begin 2) (+ checkbox-begin 5) "☐"))
        (add-text-properties checkbox-begin checkbox-end
                           '(face ar/markdown-checkbox-face)))
      t))
  
  (font-lock-add-keywords 'markdown-mode
                         '((ar/markdown-fontify-checkboxes)) 'append))

(use-package markdown-toc
  :defer t
  :after markdown-mode
  :hook (markdown-mode . markdown-toc-mode)
  :config
  (setq markdown-toc-header-toc-title "\n## Table of Contents"
        markdown-toc-user-toc-structure-manipulation-fn 'cdr))
#+end_src

* Languages
** Nix
#+begin_src emacs-lisp
(use-package nix-mode
  :defer t
  :mode "\\.nix\\'")

(use-package nix-ts-mode
  :defer t
  :mode "\\.nix\\'")
#+end_src

** Python
#+begin_src emacs-lisp
(use-package python-pytest
  :defer t
  :after python
  :custom
  (python-pytest-confirm t))

(with-eval-after-load 'eglot
  (add-to-list 'eglot-server-programs
               '((python-mode python-ts-mode) "basedpyright-langserver" "--stdio")))

(use-package flymake-ruff
  :hook (eglot-managed-mode . flymake-ruff-load))

(with-eval-after-load 'apheleia
  (setf (alist-get 'python-mode apheleia-mode-alist) '(ruff-isort ruff))
  (setf (alist-get 'python-ts-mode apheleia-mode-alist) '(ruff-isort ruff)))
#+end_src

* Utils
** Which Key
#+begin_src emacs-lisp
(use-package which-key
  :defer 0.5
  :hook (after-init . which-key-mode)
  :custom
  (which-key-idle-delay 0.1)
  (which-key-separator " → ")
  (which-key-popup-type 'minibuffer))
#+end_src

** Wgrep
Writable `grep' buffer
#+begin_src emacs-lisp
(use-package wgrep
  :defer t
  :init (setq wgrep-auto-save-buffer t
              wgrep-change-readonly-file t))
#+end_src

** Echo Area Context Display
#+begin_src emacs-lisp
;; Cache for Git branch to avoid repeated VC calls
(defvar ar/git-branch-cache nil
  "Cached Git branch for current buffer.")
(defvar ar/git-branch-cache-time nil
  "Time when Git branch was last cached.")

(defun ar/get-git-branch-cached ()
  "Get Git branch with caching and robust fallbacks."
  (when (and buffer-file-name
             (vc-backend buffer-file-name))
    (let ((current-time (float-time)))
      ;; Refresh cache if older than 5 seconds or not set
      (when (or (null ar/git-branch-cache-time)
                (> (- current-time ar/git-branch-cache-time) 5.0))
        (setq ar/git-branch-cache
              (let ((str (vc-git-mode-line-string buffer-file-name)))
                (if str
                    (substring-no-properties str 4) ;; Remove " Git-" prefix
                  ;; Fallback if mode-line string is not ready
                  (car (vc-git-branches)))))
        (setq ar/git-branch-cache-time current-time))
      ar/git-branch-cache)))

(defun ar/show-buffer-context ()
  "Display useful buffer context in echo area instantly.
Shows: file path, position, mode, and cached Git branch.
Includes optimization for large buffers and nerd-icons integration."
  (interactive)
  (let* ((file (if buffer-file-name
                   (abbreviate-file-name buffer-file-name)
                 (buffer-name)))
         (icon (if (and (display-graphic-p) (featurep 'nerd-icons))
                   (nerd-icons-icon-for-mode major-mode)
                 ""))
         (line (line-number-at-pos))
         ;; Optimization: Don't count lines in massive buffers (>1MB) to prevent freeze
         (total (if (> (buffer-size) 1000000)
                    "?"
                  (count-lines (point-min) (point-max))))
         ;; FIX: Use 'equal' instead of 'string=' because total can be an integer
         (percent (if (equal total "?")
                      "--"
                    (/ (* 100 (- (point) (point-min)))
                       (max 1 (- (point-max) (point-min))))))
         (mode mode-name)
         (branch (ar/get-git-branch-cached))
         (branch-icon (if (and (display-graphic-p) (featurep 'nerd-icons))
                          (nerd-icons-octicon "nf-oct-git_branch" :face 'font-lock-function-name-face)
                        "")))
    (message "%s %s%s │ %s │ L%d/%s (%s%%)%s"
             icon
             (propertize file 'face 'font-lock-string-face)
             (if (buffer-modified-p)
                 (propertize "*" 'face 'error)
               "")
             (propertize (format "%s" mode) 'face 'font-lock-keyword-face)
             line total percent
             (if branch
                 (concat " │ " branch-icon " " (propertize branch 'face 'font-lock-constant-face))
               ""))))
#+end_src

** Buffer State Information
#+begin_src emacs-lisp
(defun ar/show-buffer-state ()
  "Show buffer state information in echo area.
Displays: Narrowed, Read-only, Macro recording, Evil state."
  (interactive)
  (let ((states '()))
    ;; Check narrowing
    (when (buffer-narrowed-p)
      (push (propertize "NARROWED" 'face 'warning) states))
    
    ;; Check read-only
    (when buffer-read-only
      (let ((icon (if (featurep 'nerd-icons) (nerd-icons-octicon "nf-oct-lock") "🔒")))
        (push (propertize (concat icon " READ-ONLY") 'face 'error) states)))
    
    ;; Check macro recording
    (when defining-kbd-macro
      (push (propertize "● REC" 'face '(:foreground "#f7768e" :weight bold)) states))
    
    ;; Check evil state
    (when (and (featurep 'evil) (bound-and-true-p evil-state))
      (push (propertize 
             (format "Evil: %s" (upcase (symbol-name evil-state)))
             'face 'font-lock-constant-face)
            states))
    
    ;; Check modified
    (when (buffer-modified-p)
      (push (propertize "MODIFIED" 'face 'success) states))
    
    (if states
        (message (string-join states " │ "))
      (message (propertize "Buffer State: Clean" 'face 'shadow)))))
#+end_src

** Macro Recording Indicator
#+begin_src emacs-lisp
;; Show macro recording status in echo area automatically
(defun ar/kmacro-start-message (&rest _)
  "Display message when macro recording starts."
  (message (propertize "● Recording keyboard macro..." 
                       'face '(:foreground "#f7768e" :weight bold))))

(defun ar/kmacro-end-message (&rest _)
  "Display message when macro recording ends."
  (message (propertize "✓ Keyboard macro defined" 
                       'face '(:foreground "#9ece6a" :weight bold))))

(advice-add 'kmacro-start-macro :after #'ar/kmacro-start-message)
(advice-add 'kmacro-end-macro :after #'ar/kmacro-end-message)
#+end_src

* Reader
** PDF Tools
#+begin_src emacs-lisp
(use-package pdf-view
  :ensure pdf-tools
  :defines pdf-annot-activate-created-annotations
  :functions pdf-tools-install
  :hook ((pdf-tools-enabled . pdf-view-auto-slice-minor-mode)
         (pdf-tools-enabled . pdf-isearch-minor-mode))
  :mode ("\\.[pP][dD][fF]\\'" . pdf-view-mode)
  :magic ("%PDF" . pdf-view-mode)
  :init
  (setq pdf-view-use-scaling t
        pdf-view-use-imagemagick nil
        pdf-annot-activate-created-annotations t
        ;; Tokyo Night theme colors
        pdf-view-midnight-colors '("#c0caf5" . "#1a1b26"))
  :config
  (pdf-tools-install t nil t nil)

  ;; Tokyo Night theme custom faces
  (custom-set-faces
   '(pdf-view-rectangle-face ((t (:background "#7aa2f7" :foreground "#1a1b26"))))
   '(pdf-isearch-match ((t (:background "#e0af68" :foreground "#1a1b26"))))
   '(pdf-isearch-lazy ((t (:background "#414868" :foreground "#c0caf5"))))
   '(pdf-isearch-batch ((t (:background "#414868" :foreground "#c0caf5"))))
   '(pdf-view-region ((t (:background "#364a82"))))
   '(pdf-links-read-link ((t (:background "#7aa2f7" :foreground "#1a1b26"))))
   '(pdf-occur-document-face ((t (:foreground "#7aa2f7"))))
   '(pdf-occur-page-face ((t (:foreground "#9ece6a"))))
   '(pdf-annot-list-annotation-face ((t (:foreground "#bb9af7"))))
   '(pdf-annot-list-location-face ((t (:foreground "#565f89"))))
   '(pdf-history-debug-message-face ((t (:foreground "#565f89"))))
   '(pdf-sync-forward-matching-page-label ((t (:foreground "#e0af68"))))
   '(pdf-sync-backward-matching-page-label ((t (:foreground "#e0af68"))))))
#+end_src

** Save PDF View
#+begin_src emacs-lisp
(use-package saveplace-pdf-view
  :when (ignore-errors (pdf-info-check-epdfinfo) t)
  :autoload (saveplace-pdf-view-find-file-advice saveplace-pdf-view-to-alist-advice)
  :functions pdf-info-check-epdfinfo
  :init
  (advice-add 'save-place-find-file-hook :around #'saveplace-pdf-view-find-file-advice)
  (advice-add 'save-place-to-alist :around #'saveplace-pdf-view-to-alist-advice))
#+end_src

* LaTeX Writing Environment
** Project Awareness
#+begin_src emacs-lisp
(defun ar/get-project-bibliographies ()
  "Find .bib files in the current project root recursively.
Returns a list of absolute paths to bibliography files, or nil if none found."
  (when-let* ((project (project-current nil))
              (root (project-root project)))
    (directory-files-recursively root "\\.bib$")))

(defun ar/setup-project-bibliography ()
  "Create a references.bib file in the project root if it doesn't exist."
  (interactive)
  (if-let* ((project (project-current t))
            (root (project-root project))
            (bib-file (expand-file-name "references.bib" root)))
      (if (file-exists-p bib-file)
          (message "Bibliography already exists: %s" bib-file)
        (with-temp-file bib-file
          (insert "% Bibliography for " (file-name-nondirectory (directory-file-name root)) "\n")
          (insert "% Created: " (format-time-string "%Y-%m-%d") "\n\n"))
        (message "Created project bibliography: %s" bib-file))
    (message "Not currently visiting a project.")))
#+end_src

** Core AUCTeX Backend
#+begin_src emacs-lisp
(use-package tex
  :defer t
  :ensure auctex
  :defer t
  :hook ((LaTeX-mode . visual-line-mode)
         (LaTeX-mode . prettify-symbols-mode)
         (LaTeX-mode . TeX-source-correlate-mode)
         (LaTeX-mode . TeX-fold-mode)
         (LaTeX-mode . eglot-ensure))
  :custom
  (TeX-engine 'luatex)
  (TeX-process-asynchronous t)
  (TeX-check-TeX nil)
  (TeX-clean-confirm nil)
  (TeX-electric-math nil)
  (TeX-display-help nil)
  (TeX-master t)
  (TeX-fold-auto t)
  (font-latex-fontify-script 'multi-level)
  (font-latex-fontify-sectioning 1.2)
  (TeX-view-program-selection '((output-pdf "PDF Tools")))
  (TeX-source-correlate-mode t)
  (TeX-source-correlate-method 'synctex)
  (TeX-source-correlate-start-server nil)
  (TeX-PDF-mode t)
  (TeX-auto-save t)
  (TeX-parse-self t)
  (TeX-save-query nil)
  (LaTeX-indent-level 2)
  (LaTeX-item-indent 0)
  (TeX-brace-indent-level 2)
  (TeX-auto-local ".auctex-auto")
  (TeX-style-local ".auctex-style")

  :config
  (with-eval-after-load 'eglot
    (add-to-list 'eglot-server-programs
                 '((latex-mode LaTeX-mode tex-mode) "texlab")))
  
  ;; ChkTeX Setup
  (let ((check-cmd (assoc "Check" TeX-command-list)))
    (when check-cmd
      (setcar (cdr check-cmd) "chktex -v6 -H %s")))
  
  ;; Hooks
  (add-hook 'TeX-mode-hook
            (lambda ()
              (setq-local ispell-parser 'tex)
              (visual-line-mode 1)
              (when (fboundp 'texmathp)
                (setq-local fill-nobreak-predicate
                            (cons #'texmathp fill-nobreak-predicate)))))
  
  ;; Delay flymake in LaTeX to avoid sentinel errors
  (add-hook 'LaTeX-mode-hook
            (lambda ()
              (run-with-idle-timer 0.5 nil
                                   (lambda ()
                                     (when (derived-mode-p 'LaTeX-mode)
                                       (flymake-mode 1))))))

  ;; Latexmk Configurations
  (add-to-list 'TeX-command-list
               '("LuaLaTeXmk" "latexmk -pdflua -pdflualatex='lualatex -shell-escape -interaction=nonstopmode' -f -outdir=%o %S"
                 TeX-run-TeX nil t
                 :help "Run latexmk with LuaLaTeX") t)

  (add-to-list 'TeX-command-list
               '("LaTeXmk-Watch" "latexmk -pvc -pdflua -pdflualatex='lualatex -shell-escape -interaction=nonstopmode' -outdir=%o %S"
                 TeX-run-TeX nil t
                 :help "Run latexmk in continuous preview mode") t)

  (setq-default TeX-command-default
                (cond ((eq TeX-engine 'luatex) "LuaLaTeXmk")
                      ((eq TeX-engine 'xetex) "XeLaTeXmk")
                      (t "LaTeXmk")))

  ;; Clean intermediate files
  (setq LaTeX-clean-intermediate-suffixes
        '("\\.aux" "\\.bbl" "\\.blg" "\\.brf" "\\.fot"
          "\\.glo" "\\.gls" "\\.idx" "\\.ilg" "\\.ind"
          "\\.lof" "\\.log" "\\.lot" "\\.nav" "\\.out"
          "\\.snm" "\\.toc" "\\.url" "\\.synctex\\.gz"
          "\\.fdb_latexmk" "\\.fls" "\\.xdv"
          "-blx\\.bib" "\\.run\\.xml"))

  (add-hook 'TeX-after-compilation-finished-functions #'TeX-revert-document-buffer)
  (add-to-list 'auto-mode-alist '("\\.map\\'" . TeX-mode))
  (setq LaTeX-command-style '(("" "%(latex)"))))
#+end_src

** Evil Integration for LaTeX
#+begin_src emacs-lisp
(use-package evil-tex
  :defer t
  :after (tex evil)
  :hook (LaTeX-mode . evil-tex-mode))
#+end_src

** Bibliography Management
*** Citar
#+begin_src emacs-lisp
(use-package citar
  :defer t
  :custom
  (citar-bibliography '("~/references.bib")) ;; Fallback
  (citar-library-paths '("~/Zotero/storage"))
  (citar-notes-paths (list my/org-roam-directory))
  (citar-symbols
   `((file ,(nerd-icons-mdicon "nf-md-file_document") . " ")
     (note ,(nerd-icons-mdicon "nf-md-note_text") . " ")
     (link ,(nerd-icons-mdicon "nf-md-link") . " ")))
  :config
  ;; === Project.el Integration ===
  (defun ar/citar-set-local-bibliography ()
    "Set buffer-local bibliography variables if in a project."
    (when-let ((bibs (ar/get-project-bibliographies)))
      (setq-local citar-bibliography bibs)
      (setq-local org-cite-global-bibliography bibs)))

  (add-hook 'LaTeX-mode-hook #'ar/citar-set-local-bibliography)
  (add-hook 'org-mode-hook #'ar/citar-set-local-bibliography))
#+end_src

*** Citar Extensions
#+begin_src emacs-lisp
(use-package citar-embark
  :after (citar embark)
  :config
  (citar-embark-mode)
  (with-eval-after-load 'embark
    (add-to-list 'embark-keymap-alist '(citar-reference . citar-embark-map))))

(use-package citar-org-roam
  :after (citar org-roam)
  :config
  (citar-org-roam-mode 1)
  (setq citar-org-roam-subdir "literature"))
#+end_src

*** Org-Cite Integration
#+begin_src emacs-lisp
(use-package oc
  :ensure nil
  :after org
  :config
  (require 'citar)
  (setq org-cite-insert-processor 'citar
        org-cite-follow-processor 'citar
        org-cite-activate-processor 'citar))
#+end_src

*** RefTeX
#+begin_src emacs-lisp
(use-package reftex
  :ensure nil
  :after tex
  :hook (LaTeX-mode . reftex-mode)
  :custom
  (reftex-plug-into-AUCTeX t)
  (reftex-default-bibliography nil)
  (reftex-toc-split-windows-horizontally t)
  (reftex-toc-split-windows-fraction 0.2))
#+end_src

** Fast LaTeX Input
#+begin_src emacs-lisp
(use-package cdlatex
  :defer t
  :hook ((LaTeX-mode . cdlatex-mode)
         (org-mode . org-cdlatex-mode))
  :custom
  (cdlatex-math-modify-alist
   '((?B "\\mathbb" nil t nil nil)
     (?C "\\mathcal" nil t nil nil)
     (?F "\\mathfrak" nil t nil nil)
     (?S "\\mathscr" nil t nil nil)))
  (cdlatex-env-alist
   '(("equation*" "\\begin{equation*}\n?\n\\end{equation*}" nil)
     ("align*" "\\begin{align*}\n?\n\\end{align*}" nil)))
  :config
  (define-key cdlatex-mode-map (kbd "TAB") #'cdlatex-tab))
#+end_src

** Preview LaTeX (AUCTeX)
#+begin_src emacs-lisp
(use-package preview
  :ensure nil
  :after tex
  :hook (LaTeX-mode . LaTeX-preview-setup)
  :custom
  (preview-auto-cache-preamble t)
  (preview-fast-conversion t)
  :config
  (setq-default preview-scale 1.4
                preview-scale-function
                (lambda () (* (/ 10.0 (face-attribute 'default :height))
                              (face-attribute 'default :height)))))
#+end_src

** Org Mode LaTeX Integration

#+begin_src emacs-lisp
(with-eval-after-load 'org
  ;; LaTeX preview process configuration for LuaLaTeX with dvisvgm
  ;; CRITICAL: LuaLaTeX needs --output-format=dvi to work with dvisvgm
  (add-to-list 'org-preview-latex-process-alist
               '(luamagick
                 :programs ("lualatex" "magick")
                 :description "pdf > png"
                 :message "you need to install lualatex and imagemagick."
                 :use-xcolor t
                 :image-input-type "pdf"
                 :image-output-type "png"
                 :image-size-adjust (1.0 . 1.0)
                 :latex-compiler ("lualatex -interaction nonstopmode -output-directory %o %f")
                 :image-converter ("magick convert -density 130 -trim -antialias %f -quality 100 %O")))

  ;; Alternative: LuaLaTeX with ImageMagick (PNG) - uncomment if dvisvgm issues
  ;; (add-to-list 'org-preview-latex-process-alist
  ;;              '(lualatex-png
  ;;                :programs ("lualatex" "convert")
  ;;                :description "pdf > png (LuaLaTeX)"
  ;;                :message "You need to install lualatex and imagemagick."
  ;;                :image-input-type "pdf"
  ;;                :image-output-type "png"
  ;;                :image-size-adjust (1.0 . 1.0)
  ;;                :latex-compiler ("lualatex -interaction=nonstopmode -output-directory=%o %f")
  ;;                :image-converter ("convert -density %D -trim -antialias %f -quality 100 %O")))

  ;; Set default preview process
  (setq org-preview-latex-default-process 'luamagick)

  ;; Preview appearance settings
  (setq org-format-latex-options
        (plist-put org-format-latex-options :scale 2.0))
  (setq org-format-latex-options
        (plist-put org-format-latex-options :foreground 'default))
  (setq org-format-latex-options
        (plist-put org-format-latex-options :background 'default))

  ;; Directory for preview images
  (setq org-preview-latex-image-directory "ltximg/")

  ;; LaTeX packages for preview
  ;; These are added to org-latex-packages-alist in the LaTeX section
  ;; but we ensure they're available for preview too
  (setq org-latex-packages-alist
        '(("" "fontspec" t ("lualatex" "xelatex"))
          ("" "unicode-math" t ("lualatex" "xelatex"))
          ("" "amsmath" t)
          ("" "amssymb" t)
          ("" "mathtools" nil)))

  ;; Don't start with previews enabled (can be slow on large files)
  (setq org-startup-with-latex-preview nil))
#+end_src

#+begin_src emacs-lisp
;; (with-eval-after-load 'org
;;   (require 'ox-latex)
;;   (setq org-latex-compiler "lualatex")
;;   (setq org-pretty-entities-include-sub-superscripts nil)

;;   (setq org-image-actual-width nil)

;;   (defun my/org-latex-preview-scale ()
;;     "Force the scale of latex previews."
;;     (setq org-format-latex-options
;;           (plist-put org-format-latex-options :scale 2.5)))

;;   (add-hook 'org-mode-hook #'my/org-latex-preview-scale)

;;   (setq org-format-latex-options
;;         (plist-put org-format-latex-options :background "Transparent"))
;;   (setq org-format-latex-options
;;         (plist-put org-format-latex-options :foreground "Auto"))

;;   (add-to-list 'org-preview-latex-process-alist
;;                '(lualatex-png
;;                  :programs ("lualatex" "magick")
;;                  :description "pdf > png (LuaLaTeX with proper transparency)"
;;                  :image-input-type "pdf"
;;                  :image-output-type "png"
;;                  :image-size-adjust (1.0 . 1.0)
;;                  :latex-compiler ("lualatex -interaction nonstopmode -output-directory %o %f")
;;                  :image-converter ("magick %f -density %D -trim +repage -transparent white -colorspace sRGB -quality 100 %O")))

;;   ;; Set default process
;;   (setq org-preview-latex-default-process 'lualatex-png))

(use-package org-fragtog
  :defer t
  :hook (org-mode . org-fragtog-mode))
#+end_src

** Completions: LSP + AUCTeX + Cape
#+begin_src emacs-lisp
(defun ar/latex-setup-completion ()
  "Configure super-powered completion for LaTeX: LSP + AUCTeX + Dabbrev."
  (require 'cape)
  (setq-local completion-at-point-functions
              (list (cape-capf-super
                     #'eglot-completion-at-point      ; LSP intelligence (citations, refs)
                     #'LaTeX-completion-at-point    ; AUCTeX specific macros
                     #'TeX--completion-at-point)    ; TeX primitives
                    #'cape-dabbrev                  ; Words in buffer
                    #'cape-file)))                 ; File paths

;; Hook into LaTeX mode to activate this setup
(add-hook 'LaTeX-mode-hook #'ar/latex-setup-completion)
#+end_src

* Window
** Winner
#+begin_src emacs-lisp
(use-package winner
  :ensure nil
  :commands (winner-undo winner-redo)
  :hook (after-init . winner-mode)
  :init (setq winner-boring-buffers '("*Completions*"
                                      "*Compile-Log*"
                                      "*inferior-lisp*"
                                      "*Fuzzy Completions*"
                                      "*Apropos*"
                                      "*Help*"
                                      "*cvs*"
                                      "*Buffer List*"
                                      "*Ibuffer*"
                                      "*esh command on file*")))
#+end_src

** Ace window
#+begin_src emacs-lisp
(use-package ace-window
  :defer t
  :commands (toggle-window-split) 
  :pretty-hydra
  ((:title (pretty-hydra-title "Window Management" 'faicon "nf-fa-th")
    :foreign-keys warn :quit-key ("q" "C-g"))
   ("Actions"
    (("TAB" other-window "switch")
     ("x" ace-delete-window "delete")
     ("X" ace-delete-other-windows "delete other" :exit t)
     ("s" ace-swap-window "swap")
     ("a" ace-select-window "select" :exit t)
     ("m" toggle-frame-maximized "maximize" :exit t)
     ("u" toggle-frame-fullscreen "fullscreen" :exit t))
    "Resize"
    (("h" shrink-window-horizontally "←")
     ("j" enlarge-window "↓")
     ("k" shrink-window "↑")
     ("l" enlarge-window-horizontally "→")
     ("n" balance-windows "balance"))
    "Split"
    (("r" split-window-right "horizontally")
     ("R" split-window-horizontally-instead "horizontally instead")
     ("v" split-window-below "vertically")
     ("V" split-window-vertically-instead "vertically instead")
     ("t" toggle-window-split "toggle"))
    "Zoom"
    (("+" text-scale-increase "in")
     ("=" text-scale-increase "in")
     ("-" text-scale-decrease "out")
     ("0" (text-scale-increase 0) "reset"))
    "Misc"
    (("o" set-frame-font "frame font")
     ("f" make-frame-command "new frame")
     ("d" delete-frame "delete frame")
     ("<left>" winner-undo "winner undo")
     ("<right>" winner-redo "winner redo"))))
  :custom-face
  (aw-leading-char-face ((t (:inherit font-lock-keyword-face :foreground unspecified :bold t :height 3.0))))
  (aw-minibuffer-leading-char-face ((t (:inherit font-lock-keyword-face :bold t :height 1.0))))
  (aw-mode-line-face ((t (:inherit mode-line-emphasis :bold t))))
  :bind (([remap other-window] . ace-window)
         ("C-c w" . ace-window-hydra/body)
         ("C-x |" . split-window-horizontally-instead)
         ("C-x _" . split-window-vertically-instead))
  ;; REMOVED: :hook (emacs-startup . ace-window-display-mode)
  ;; This prevents the "1" from appearing in the modeline.
  :config
  (defun toggle-window-split ()
    (interactive)
    (if (= (count-windows) 2)
        (let* ((this-win-buffer (window-buffer))
               (next-win-buffer (window-buffer (next-window)))
               (this-win-edges (window-edges (selected-window)))
               (next-win-edges (window-edges (next-window)))
               (this-win-2nd (not (and (<= (car this-win-edges)
                                           (car next-win-edges))
                                       (<= (cadr this-win-edges)
                                           (cadr next-win-edges)))))
               (splitter
                (if (= (car this-win-edges)
                       (car (window-edges (next-window))))
                    'split-window-horizontally
                  'split-window-vertically)))
          (delete-other-windows)
          (let ((first-win (selected-window)))
            (funcall splitter)
            (if this-win-2nd (other-window 1))
            (set-window-buffer (selected-window) this-win-buffer)
            (set-window-buffer (next-window) next-win-buffer)
            (select-window first-win)
            (if this-win-2nd (other-window 1))))
      (user-error "`toggle-window-split' only supports two windows")))

  ;; Bind hydra to dispatch list
  (add-to-list 'aw-dispatch-alist '(?w ace-window-hydra/body) t)

  ;; Select window via `M-1'...`M-9'
  (defun aw--select-window (number)
    "Select the specified window."
    (when (numberp number)
      (let ((found nil))
        (dolist (win (aw-window-list))
          (when (and (window-live-p win)
                     (eq number
                         (string-to-number
                          (window-parameter win 'ace-window-path))))
            (setq found t)
            (aw-switch-to-window win)))
        (unless found
          (message "No specified window: %d" number)))))
  (dotimes (n 9)
    (bind-key (format "M-%d" (1+ n))
              (lambda ()
                (interactive)
                (aw--select-window (1+ n))))))
#+end_src

** Popper
#+begin_src emacs-lisp
(use-package popper
  :custom
  (popper-group-function #'popper-group-by-directory)
  (popper-echo-dispatch-actions t)
  :hook (after-init . popper-echo-mode)
  :init
  (setq popper-mode-line ""
        popper-reference-buffers
        '("\\*Messages\\*$"
          "\\*Warnings\\*$"
          "Output\\*$"
          "\\*Pp Eval Output\\*$"
          "\\*Pp Macroexpand Output\\*$"
          "^\\*eldoc.*\\*$"
          "\\*Compile-Log\\*$"
          "\\*compilation\\*$"
          "\\*Completions\\*$"
          "\\*Async Shell Command\\*$"
          "\\*Shell Command Output\\*$"
          "\\*Apropos\\*$"
          "\\*Backtrace\\*$"
          "\\*Calendar\\*$"
          "\\*Calculator\\*$"
          "\\*Calc Trail\\*$"
          "\\*Fd\\*$"
          "\\*Find\\*$"
          "\\*Finder\\*$"
          "\\*Kill Ring\\*$"
          "\\*Occur\\*$"
          "\\*xref\\*$"
          "\\*Embark \\(Collect\\|Live\\|Export\\):.*\\*$"
          "\\*Embark Actions\\*$"
          "\\*Org Select\\*$"
          "\\*Org Agenda.*\\*$"
          "\\*Agenda Commands\\*$"
          "\\*Capture\\*$"
          "^CAPTURE-.*\\.org*"
          "\\*Local variables\\*$"
          "\\*Org-Babel Error Output\\*$"
          "\\*Org Export Dispatcher\\*$"
          "\\*Org Note\\*$"

          ;; Helpful, Help, Info
          bookmark-bmenu-mode
          comint-mode
          compilation-mode
          help-mode
          helpful-mode
          tabulated-list-mode
          Buffer-menu-mode
          Info-mode
          Custom-mode
          special-mode

          ;; Flycheck/Flymake
          flymake-diagnostics-buffer-mode
          flymake-project-diagnostics-mode
          flycheck-error-list-mode
          flycheck-verify-mode

          ;; Dictionary and translation
          gnus-article-mode
          devdocs-mode
          dictionary-mode
          youdao-dictionary-mode
          osx-dictionary-mode
          fanyi-mode
          "^\\*gt-result\\*$"
          "^\\*gt-log\\*$"
          "^\\*DeepL\\*$"
          "^\\*Google Translate\\*$"

          ;; Search results
          grep-mode
          occur-mode
          rg-mode
          deadgrep-mode
          ag-mode
          pt-mode
          ivy-occur-grep-mode
          ivy-occur-mode

          ;; Process and environment
          "^\\*Process List\\*$"
          process-menu-mode
          list-environment-mode
          cargo-process-mode

          ;; Terminals
          "^\\*.*eat.*\\*.*$"
          "^\\*.*eshell.*\\*.*$"
          "^\\*.*shell.*\\*.*$"
          "^\\*.*terminal.*\\*.*$"
          "^\\*.*vterm[inal]*.*\\*.*$"
          "^\\*.*ansi-term.*\\*.*$"

          ;; Development tools
          "\\*DAP Templates\\*$"
          dap-server-log-mode
          "^\\*dap-ui.*\\*$"
          "\\*ELP Profiling Results\\*$"
          profiler-report-mode
          "\\*package update results\\*$"
          "\\*Package-Lint\\*$"
          "\\*Packages\\*$"
          "\\*Native-compile-Log\\*$"
          "\\*Paradox Report\\*$"
          "^\\*straight-.*\\*$"
          "^\\*elpaca-.*\\*$"

          ;; Documentation
          "\\*[Wo]*Man.*\\*$"
          woman-mode
          Man-mode

          ;; Testing
          "\\*ert\\*$"
          overseer-buffer-mode
          "^\\*Buttercup\\*$"
          "\\*cider-test-report\\*$"

          ;; Debugging
          "\\*gud-debug\\*$"
          "\\*LLDB.*\\*$"
          "\\*breakpoints of.*\\*$"
          "\\*gdb-.*\\*$"

          ;; LSP
          "\\*lsp-help\\*$"
          "\\*lsp session\\*$"
          "\\*lsp-log\\*$"
          "\\*lsp-ui-doc\\*$"
          "\\*lsp-ui-imenu\\*$"
          "\\*LSP Error\\*$"
          "\\*eglot.*\\*$"

          ;; Misc development
          "\\*quickrun\\*$"
          "\\*tldr\\*$"
          "\\*vc-.*\\*$"
          "\\*vc-diff\\*$"
          "\\*vc-change-log\\*$"
          "\\*diff-hl\\*$"
          "^\\*macro expansion\\*$"
          "^\\*Macrostep\\*$"
          "\\*ediff-.*\\*$"
          "\\*grep\\*$"
          "\\*nosetests\\*$"
          "\\*pytest\\*$"

          ;; Language-specific
          "\\*Gofmt Errors\\*$"
          "\\*Go Test\\*$"
          godoc-mode
          "\\*rustfmt\\*$"
          "\\*cargo-.*\\*$"
          rustic-compilation-mode
          rustic-cargo-clippy-mode
          rustic-cargo-outdated-mode
          rustic-cargo-run-mode
          rustic-cargo-test-mode
          "\\*Python\\*$"
          "\\*inferior-python.*\\*$"
          inferior-python-mode
          inf-ruby-mode
          swift-repl-mode
          "\\*prolog\\*$"
          "\\*sly-.*\\*$"
          "\\*slime-.*\\*$"

          ;; Docker
          "\\*docker-.+\\*"
          "\\*docker-compose.*\\*$"
          "\\*dockerfile.*\\*$"

          ;; Miscellaneous
          "\\*Semantic SymRef\\*$"
          "\\*Messages\\*$"
          "\\*Warnings\\*$"
          "\\*Error\\*$"
          "\\*Flycheck.*\\*$"
          "\\*Format All Errors\\*$"
          "\\*Pp Eval Output\\*$"))
  :config
  (with-no-warnings
    (defun my-popper-fit-window-height (win)
      "Adjust the height of popup window WIN to fit the buffer's content."
      (let ((desired-height (floor (/ (frame-height) 3))))
        (fit-window-to-buffer win desired-height desired-height)))
    (setq popper-window-height #'my-popper-fit-window-height)

    (defun popper-close-window-hack (&rest _args)
      "Close popper window via `C-g'."
      (when (and (not (region-active-p))
                 popper-open-popup-alist)
        (let ((window (caar popper-open-popup-alist))
              (buffer (cdar popper-open-popup-alist)))
          (when (and (window-live-p window)
                     (buffer-live-p buffer)
                     (not (with-current-buffer buffer
                            (derived-mode-p 'eshell-mode
                                            'shell-mode
                                            'term-mode
                                            'vterm-mode
                                            'eat-mode))))
            (delete-window window)))))
    (advice-add #'keyboard-quit :before #'popper-close-window-hack)))
#+end_src

* General Keybindings
#+begin_src emacs-lisp
(use-package general
  :after evil
  :defer t
  :config
  ;; Set up leader keys
  (general-create-definer ar/global-leader
    :states '(normal insert visual emacs)
    :prefix "SPC"
    :global-prefix "C-SPC")

  (general-create-definer ar/local-leader
    :states '(normal insert visual emacs)
    :prefix "SPC m"
    :global-prefix "C-SPC m")
  
  (ar/global-leader
    "SPC" '(execute-extended-command :wk "M-x")
    ":" '(eval-expression :wk "Eval")
    "u" '(universal-argument :wk "Universal arg")
    "!" '(shell-command :wk "Shell cmd")
    "X" '(org-capture :wk "Org capture"))

  ;; Buffer operations (SPC b)
  (ar/global-leader
    "b" '(:ignore t :wk "buffer")
    "b b" '(consult-buffer :wk "Switch buffer")
    "b i" '(ibuffer :wk "Ibuffer")
    "b k" '(kill-current-buffer :wk "Kill buffer")
    "b K" '(kill-buffer :wk "Kill buffer (choose)")
    "b n" '(next-buffer :wk "Next buffer")
    "b p" '(previous-buffer :wk "Previous buffer")
    "b r" '(revert-buffer-quick :wk "Revert buffer")
    "b R" '(rename-buffer :wk "Rename buffer")
    "b S" '(save-buffer :wk "Save buffer")
    "b z" '(bury-buffer :wk "Bury buffer")
    "b [" '(previous-buffer :wk "Previous buffer")
    "b ]" '(next-buffer :wk "Next buffer"))

  ;; Dired/Dirvish operations (Doom-like)
  (ar/global-leader
    "d" '(:ignore t :wk "dired")
    "d d" '(dired-jump :wk "Open dired here")
    "d D" '(dired :wk "Open dired...")
    "d s" '(dirvish-side :wk "Dirvish sidebar")
    "d f" '(dirvish-dwim :wk "Dirvish fullscreen")
    "d h" '(dirvish-show-history :wk "History")
    "d a" '(dirvish-quick-access :wk "Quick access")
    "d j" '(dirvish-fd-jump :wk "Jump fd"))
 
  ;; File operations (SPC f)
  (ar/global-leader
    "f" '(:ignore t :wk "file")
    "f f" '(find-file :wk "Find file")
    "f r" '(consult-recent-file :wk "Recent files")
    "f s" '(save-buffer :wk "Save file")
    "f S" '(write-file :wk "Save as")
    "f E" '(sudo-edit :wk "Sudo edit")
    "f l" '(reload-init-file :wk "Reload config")
    "f e" '(:ignore t :wk "emacs")
    "f e d" '((lambda () (interactive) (find-file user-emacs-directory)) :wk "Open emacs.d")
    "f e i" '((lambda () (interactive) (find-file user-init-file)) :wk "Open init.el")
    "f d" '(consult-dir :wk "Change directory")
    "f j" '(consult-dir-jump-file :wk "Jump to file in dir"))

  ;; Search operations (SPC s)
  (ar/global-leader
    "s" '(:ignore t :wk "search")
    "s s" '(consult-line :wk "Search buffer")
    "s S" '(consult-line-multi :wk "Search buffers")
    "s p" '(consult-ripgrep :wk "Search project")
    "s d" '(consult-dir :wk "Search directory")
    "s i" '(consult-imenu :wk "Imenu")
    "s I" '(consult-imenu-multi :wk "Imenu multi")
    "s o" '(consult-outline :wk "Outline")
    "s b" '(consult-line :wk "Search buffer")
    "s j" '(evil-show-jumps :wk "Jump list")
    "s m" '(consult-mark :wk "Mark ring")
    "s e" '(consult-isearch-history :wk "Search history")
    "s y" '(consult-yasnippet :wk "Yasnippet")
    "s t" '(hl-todo-occur :wk "Todo list")
    "s T" '(hl-todo-rg-project :wk "Todo in project"))

  ;; Window operations (SPC w)
  (ar/global-leader
    "w" '(:ignore t :wk "window")
    "w w" '(other-window :wk "Other window")
    "w d" '(delete-window :wk "Delete window")
    "w D" '(delete-other-windows :wk "Delete other windows")
    "w s" '(split-window-below :wk "Split below")
    "w v" '(split-window-right :wk "Split right")
    "w h" '(evil-window-left :wk "Window left")
    "w j" '(evil-window-down :wk "Window down")
    "w k" '(evil-window-up :wk "Window up")
    "w l" '(evil-window-right :wk "Window right")
    "w H" '(evil-window-move-far-left :wk "Move far left")
    "w J" '(evil-window-move-very-bottom :wk "Move bottom")
    "w K" '(evil-window-move-very-top :wk "Move top")
    "w L" '(evil-window-move-far-right :wk "Move far right")
    "w =" '(balance-windows :wk "Balance windows")
    "w m" '(delete-other-windows :wk "Maximize")
    "w o" '(other-window :wk "Other window")
    "w u" '(winner-undo :wk "Winner undo")
    "w r" '(winner-redo :wk "Winner redo")
    "w t" '(toggle-window-split :wk "Toggle split")
    "w _" '(split-window-vertically-instead :wk "Split vertically instead")
    "w |" '(split-window-horizontally-instead :wk "Split horizontally instead"))

  ;; Project operations (SPC p)
  (ar/global-leader
    "p" '(:ignore t :wk "project")
    "p p" '(project-switch-project :wk "Switch project")
    "p f" '(project-find-file :wk "Find file in project")
    "p s" '(consult-ripgrep :wk "Search project")
    "p b" '(consult-project-buffer :wk "Project buffers")
    "p k" '(project-kill-buffers :wk "Kill project buffers")
    "p c" '(project-compile :wk "Compile project")
    "p d" '(project-dired :wk "Dired project root")
    "p e" '(project-eshell :wk "Eshell in project")
    "p t" '(treemacs :wk "Treemacs"))

  ;; Toggle operations (SPC t)
  (ar/global-leader
    "t" '(:ignore t :wk "toggle")
    "t l" '(display-line-numbers-mode :wk "Line numbers")
    "t w" '(whitespace-mode :wk "Whitespace")
    "t f" '(toggle-frame-fullscreen :wk "Fullscreen")
    "t m" '(markdown-toggle-markup-hiding :wk "Markdown markup")
    "t t" '(consult-theme :wk "Theme")
    "t v" '(visual-line-mode :wk "Visual line mode")
    "t r" '(rainbow-mode :wk "Rainbow mode")
    "t h" '(global-hl-line-mode :wk "Highlight line")
    "t p" '(show-paren-mode :wk "Show paren")
    "t T" '(toggles-hydra/body :wk "Toggles hydra"))

 
  ;; Open operations (SPC o)
  (ar/global-leader
    "o" '(:ignore t :wk "open")
    "o c" '(container-hydra/body :wk "Container menu")
    "o p" '(treemacs :wk "Treemacs")
    "o e" '(ar/eshell-toggle :wk "Toggle eshell")
    "o E" '(ar/eshell-new :wk "Eshell (new)")
    "o t" '(vterm :wk "Vterm")
    "o s" '(shell :wk "Shell")
    "o d" '(dired :wk "Dired")
    "o D" '((lambda () (interactive) (switch-to-buffer "*dashboard*")) :wk "Dashboard")
    "o P" '(popper-toggle :wk "Toggle popup"))

  ;; Help operations (SPC h)
  (ar/global-leader
    "h" '(:ignore t :wk "help")
    "h f" '(helpful-callable :wk "Describe function")
    "h v" '(helpful-variable :wk "Describe variable")
    "h k" '(helpful-key :wk "Describe key")
    "h m" '(describe-mode :wk "Describe mode")
    "h o" '(helpful-symbol :wk "Describe symbol")
    "h c" '(helpful-command :wk "Describe command")
    "h C" '(ar/show-char-info :wk "Char info")
    "h ." '(helpful-at-point :wk "Describe at point")
    "h i" '(info :wk "Info")
    "h I" '(consult-info :wk "Consult info")
    "h p" '(describe-package :wk "Describe package")
    "h a" '(apropos :wk "Apropos")
    "h w" '(woman :wk "Woman")
    "h r" '(:ignore t :wk "reload")
    "h r r" '(reload-init-file :wk "Reload config"))

  (general-define-key
   :states 'motion
    "] e" 'ar/flymake-next-error
    "[ e" 'ar/flymake-prev-error)

  (ar/global-leader
    "c" '(:ignore t :wk "code")
    "c ." '(dev-hydra/body :wk "Dev dashboard")
    "c e" '(consult-flymake :wk "Search errors")
    "c B" '(flymake-show-buffer-diagnostics :wk "Buffer diagnostics")
    "c n" '(ar/flymake-next-error :wk "Next error")
    "c p" '(ar/flymake-prev-error :wk "Prev error")
    "c f" '(apheleia-format-buffer :wk "Format buffer")
    "c r" '(eglot-rename :wk "Rename")
    "c a" '(eglot-code-actions :wk "Code action"))
  
  ;; Git operations (SPC g)
  (ar/global-leader
    "g" '(:ignore t :wk "git")
    "g s" '(magit-status :wk "Status")
    "g c" '(magit-commit :wk "Commit")
    "g C" '(magit-commit-amend :wk "Commit amend")
    "g p" '(magit-push-current-to-pushremote :wk "Push")
    "g P" '(magit-pull-from-upstream :wk "Pull")
    "g b" '(magit-branch :wk "Branches")
    "g l" '(magit-log-buffer-file :wk "Log current file")
    "g L" '(magit-log-current :wk "Log current branch")
    "g d" '(magit-diff-unstaged :wk "Diff")
    "g f" '(magit-fetch :wk "Fetch")
    "g h" '(version-control-hydra/body :wk "VC hydra")
    "g m" '(magit-merge :wk "Merge")
    "g r" '(magit-rebase :wk "Rebase")
    "g t" '(git-timemachine-toggle :wk "Timemachine")
    "g n" '(git-gutter:next-hunk :wk "Next hunk")
    "g N" '(git-gutter:previous-hunk :wk "Previous hunk")
    "g S" '(git-gutter:stage-hunk :wk "Stage hunk")
    "g R" '(git-gutter:revert-hunk :wk "Revert hunk")
    "g H" '(git-gutter:popup-hunk :wk "Preview hunk"))

  ;; Register/Bookmark operations (SPC r)
  (ar/global-leader
    "r" '(:ignore t :wk "register/bookmark")
    "r r" '(consult-register :wk "Registers")
    "r s" '(consult-register-store :wk "Store register")
    "r l" '(consult-register-load :wk "Load register")
    "r b" '(consult-bookmark :wk "Bookmarks")
    "r B" '(bookmark-set :wk "Set bookmark")
    "r d" '(bookmark-delete :wk "Delete bookmark")
    "r m" '(bookmark-bmenu-list :wk "Bookmark menu"))

  ;; Notes operations (SPC n)
  (ar/global-leader
    "n" '(:ignore t :wk "notes")
    "n a" '(org-agenda :wk "Agenda")
    "n c" '(org-capture :wk "Capture")
    "n f" '(org-roam-node-find :wk "Find node")
    "n F" '(consult-org-roam-file-find :wk "Find file")
    "n i" '(org-roam-node-insert :wk "Insert node")
    "n l" '(org-roam-buffer-toggle :wk "Roam buffer")
    "n g" '(org-roam-graph :wk "Roam graph")
    "n u" '(org-roam-ui-open :wk "Roam UI")
    "n j" '(org-roam-dailies-capture-today :wk "Daily note")
    "n b" '(consult-org-roam-backlinks :wk "Backlinks")
    "n B" '(consult-org-roam-backlinks-recursive :wk "Backlinks recursive")
    "n L" '(consult-org-roam-forward-links :wk "Forward links")
    "n o" '(ar/org-show-breadcrumb :wk "Show outline path")
    "n s" '(consult-org-roam-search :wk "Search roam")
    "n t" '(:ignore t :wk "table")
    "n t c" '(org-table-create :wk "Create table")
    "n t a" '(org-table-align :wk "Align table")
    "n t r" '(org-table-recalculate :wk "Recalculate"))

  ;; Quit/Session operations (SPC q)
  (ar/global-leader
    "q" '(:ignore t :wk "quit/session")
    "q q" '(save-buffers-kill-terminal :wk "Quit Emacs")
    "q Q" '(kill-emacs :wk "Quit (no save)")
    "q f" '(delete-frame :wk "Delete frame")
    "q s" '(tabspaces-save-session :wk "Save session")
    "q l" '(tabspaces-restore-session :wk "Load session"))

  ;; Insert operations (SPC i)
  (ar/global-leader
    "i" '(:ignore t :wk "insert")
    "i s" '(consult-yasnippet :wk "Snippet")
    "i u" '(insert-char :wk "Unicode char"))

  ;; Global Zoom Shortcuts (Browser-style)
  (general-define-key
   "C-=" 'text-scale-increase
   "C--" 'text-scale-decrease)
  
  ;; Workspace operations (SPC TAB)
  (ar/global-leader
    "TAB" '(:ignore t :wk "workspace")
    "TAB TAB" '(tabspaces-switch-or-create-workspace :wk "Switch workspace")
    "TAB n" '(tabspaces-switch-or-create-workspace :wk "New workspace")
    "TAB d" '(tabspaces-close-workspace :wk "Delete workspace")
    "TAB r" '(tab-bar-rename-tab :wk "Rename workspace")
    "TAB [" '(tab-bar-switch-to-prev-tab :wk "Previous workspace")
    "TAB ]" '(tab-bar-switch-to-next-tab :wk "Next workspace")
    "TAB s" '(tabspaces-save-session :wk "Save session")
    "TAB l" '(tabspaces-restore-session :wk "Load session"))

  ;; Macro operations (SPC @)
  (ar/global-leader
    "@" '(:ignore t :wk "macro")
    "@ @" '(kmacro-end-and-call-macro :wk "End and call")
    "@ s" '(kmacro-start-macro :wk "Start recording")
    "@ e" '(kmacro-end-macro :wk "End recording")
    "@ c" '(kmacro-call-macro :wk "Call macro")
    "@ n" '(kmacro-name-last-macro :wk "Name last")
    "@ b" '(kmacro-bind-to-key :wk "Bind to key")
    "@ v" '(kmacro-view-macro :wk "View macro")
    "@ d" '(kmacro-delete-ring-head :wk "Delete"))

  ;; Kill ring (SPC k)
  (ar/global-leader
    "k" '(browse-kill-ring :wk "Kill ring"))

  ;; Iedit (SPC e)
  (ar/global-leader
    "e" '(:ignore t :wk "iedit")
    "e e" '(iedit-mode :wk "Iedit mode")
    "e r" '(iedit-rectangle-mode :wk "Rectangle mode"))

  ;; Treemacs (SPC 0)
  (ar/global-leader
    "0" '(treemacs-select-window :wk "Select treemacs"))

  (general-define-key
   :states 'motion
   ;; Avy bindings
   "g s" 'avy-goto-char-2
   "g S" 'avy-goto-line
   "g w" 'avy-goto-word-1
   "g e" 'avy-goto-word-0

   ;; Xref/LSP navigation
   "g d" 'xref-find-definitions
   "g D" 'xref-find-references
   "g i" 'lsp-find-implementation
   "g t" 'lsp-find-type-definition
   "g ," 'xref-go-back)

  (general-define-key
   :states '(normal visual)
   ;; Better navigation
   "j" 'evil-next-visual-line
   "k" 'evil-previous-visual-line
   "gj" 'evil-next-line
   "gk" 'evil-previous-line

   ;; Avy zap
   "z" 'avy-zap-to-char-dwim
   "Z" 'avy-zap-up-to-char-dwim)

  (general-define-key
   :states 'normal
   ;; Undo-fu
   "u" 'undo-fu-only-undo
   "C-r" 'undo-fu-only-redo)

  (general-define-key
   ;; Ace-link
   "M-o" 'ace-link-addr

   ;; Popper
   "C-`" 'popper-toggle
   "C-~" 'popper-cycle
   "M-`" 'popper-toggle-type)

  (general-define-key
   :states 'normal
   :keymaps 'dired-mode-map
   "h" 'dired-up-directory
   "l" 'dired-find-file
   "o" 'dired-find-file-other-window
   "v" 'dired-view-file
   "m" 'dired-mark
   "u" 'dired-unmark
   "U" 'dired-unmark-all-marks
   "c" 'dired-create-directory
   "C" 'dired-do-copy
   "D" 'dired-do-delete
   "R" 'dired-do-rename
   "Y" 'dired-do-copy
   "!" 'dired-do-shell-command
   "&" 'dired-do-async-shell-command
   "q" 'quit-window
   "g r" 'revert-buffer
   "g g" 'evil-goto-first-line
   "G" 'evil-goto-line
   "s" 'dired-sort-toggle-or-edit
   "(" 'dired-hide-details-mode
   ")" 'dired-omit-mode)

  ;; Dirvish specific bindings (Moved from use-package dirvish)
  (general-define-key
   :states 'normal
   :keymaps 'dirvish-mode-map
   ;; Navigation (Arrow Keys)
   "<left>"  'dired-up-directory
   "<right>" 'dired-find-file
   "<up>"    'dired-previous-line
   "<down>"  'dired-next-line
   
   ;; Core Actions
   "q"   'dirvish-quit
   "TAB" 'dirvish-subtree-toggle
   "?"   'dirvish-dispatch
   
   ;; Layout & History
   "M-t" 'dirvish-layout-switch
   "F"   'dirvish-layout-toggle
   "b"   'dirvish-history-go-backward
   "f"   'dirvish-history-go-forward
   
   ;; Menus & Tools
   "a"   'dirvish-quick-access
   "s"   'dirvish-quicksort
   "y"   'dirvish-yank-menu
   "v"   'dirvish-vc-menu
   "M-e" 'dirvish-emerge-menu
   "M-j" 'dirvish-fd-jump
   "M-s" 'dirvish-setup-menu)
  
  (general-define-key
   :states 'normal
   :keymaps 'helpful-mode-map
   "q" 'quit-window
   "g r" 'helpful-update
   "TAB" 'forward-button
   "S-TAB" 'backward-button
   "RET" 'push-button)

  (general-define-key
   :states 'normal
   :keymaps 'pdf-view-mode-map
   "j" 'pdf-view-next-line-or-next-page
   "k" 'pdf-view-previous-line-or-previous-page
   "h" 'image-backward-hscroll
   "l" 'image-forward-hscroll
   "J" 'pdf-view-next-page
   "K" 'pdf-view-previous-page
   "g g" 'pdf-view-first-page
   "G" 'pdf-view-last-page
   "g t" 'pdf-view-goto-page
   "g l" 'pdf-view-goto-label
   "+" 'pdf-view-enlarge
   "=" 'pdf-view-enlarge
   "-" 'pdf-view-shrink
   "0" 'pdf-view-scale-reset
   "W" 'pdf-view-fit-width-to-window
   "H" 'pdf-view-fit-height-to-window
   "P" 'pdf-view-fit-page-to-window
   "n" 'pdf-view-next-page-command
   "p" 'pdf-view-previous-page-command
   "d" 'pdf-view-midnight-minor-mode
   "/" 'isearch-forward
   "?" 'isearch-backward
   "o" 'pdf-outline
   "m" 'pdf-view-set-slice-using-mouse
   "b" 'pdf-view-set-slice-from-bounding-box
   "r" 'pdf-view-reset-slice
   "q" 'quit-window)

  ;; Move text up and down
  (general-define-key
   :states '(normal visual insert)
   "M-j" 'move-text-down
   "M-k" 'move-text-up)
  
  (ar/local-leader
    :keymaps 'org-mode-map
    "," '(org-ctrl-c-ctrl-c :wk "Ctrl-c Ctrl-c")
    "." '(org-time-stamp :wk "Timestamp")
    "*" '(org-ctrl-c-star :wk "Heading")
    "-" '(org-ctrl-c-minus :wk "List")
    "a" '(org-agenda :wk "Agenda")
    "c" '(org-capture :wk "Capture")
    "e" '(org-export-dispatch :wk "Export")
    "i" '(:ignore t :wk "insert")
    "i l" '(org-insert-link :wk "Link")
    "i s" '(org-download-screenshot :wk "Screenshot")
    "i y" '(org-download-yank :wk "Yank image")
    "i t" '(org-table-create :wk "Table")
    "i h" '(org-insert-heading :wk "Heading")
    "t" '(org-todo :wk "Todo")
    "T" '(org-show-todo-tree :wk "Todo tree")
    "s" '(org-schedule :wk "Schedule")
    "d" '(org-deadline :wk "Deadline")
    "p" '(org-priority :wk "Priority")
    "r" '(org-refile :wk "Refile")
    "A" '(org-archive-subtree :wk "Archive")
    "l" '(:ignore t :wk "link")
    "l l" '(org-insert-link :wk "Insert link")
    "l s" '(org-store-link :wk "Store link")
    "l i" '(org-id-get-create :wk "Insert ID")
    "b" '(:ignore t :wk "babel")
    "b t" '(org-babel-tangle :wk "Tangle")
    "b e" '(org-babel-execute-src-block :wk "Execute")
    "b b" '(org-babel-execute-buffer :wk "Execute buffer")
    "b c" '(org-babel-check-src-block :wk "Check")
    "x" '(:ignore t :wk "text")
    "x b" '(org-toggle-checkbox :wk "Toggle checkbox")
    "x i" '(org-toggle-item :wk "Toggle item")
    "x p" '(org-set-property :wk "Set property")
    "x t" '(org-set-tags-command :wk "Set tags")))
#+end_src

