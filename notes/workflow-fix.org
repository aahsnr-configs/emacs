* Workflow Management
** Perspective Mode (Workspaces)
#+begin_src emacs-lisp
(use-package persp-mode
  :init
  (setq persp-auto-resume-time 0
        persp-auto-save-opt 1
        persp-auto-save-num-of-backups 3
        persp-nil-name "main"
        persp-reset-windows-on-nil-window-conf nil
        persp-set-last-persp-for-new-frames nil
        persp-switch-to-added-buffer nil
        persp-kill-foreign-buffer-behaviour 'kill
        persp-remove-buffers-from-nil-persp-behaviour nil
        persp-add-buffer-on-after-change-major-mode 'free
        persp-add-buffer-on-find-file 'if-not-autopersp
        persp-common-buffer-filter-functions
        (list #'(lambda (b) (or (string-prefix-p " " (buffer-name b))
                                (string-prefix-p "*" (buffer-name b))))))

  :config
  (persp-mode 1)

  ;; Auto-save perspectives
  (add-hook 'kill-emacs-hook #'persp-save-state-to-file)

  ;; Create default perspectives
  (with-eval-after-load 'persp-mode
    (persp-def-auto-persp "org"
      :mode 'org-mode
      :dyn-env '(org-directory my/org-directory)
      :hooks '(after-switch-to-buffer-functions)
      :switch 'window)
    
    (persp-def-auto-persp "code"
      :mode '(prog-mode)
      :hooks '(after-switch-to-buffer-functions)
      :switch 'window)))
#+end_src

** Treemacs: Project Explorer
#+begin_src emacs-lisp
(defvar +treemacs-git-mode 'simple
  "Type of git integration for treemacs.
Options: 'simple (default), 'extended, or 'deferred.
Extended and deferred require python3.")

(use-package treemacs
  :defer t
  :init
  (setq treemacs-follow-after-init t
        treemacs-is-never-other-window t
        treemacs-sorting 'alphabetic-case-insensitive-asc
        treemacs-persist-file (expand-file-name "treemacs-persist" no-littering-var-directory)
        treemacs-last-error-persist-file (expand-file-name "treemacs-last-error-persist" no-littering-var-directory)
        treemacs-width 35
        treemacs-width-is-initially-locked t
        treemacs-display-in-side-window t
        treemacs-position 'left
        treemacs-no-delete-other-windows t
        treemacs-show-hidden-files t
        treemacs-follow-after-init t
        treemacs-expand-after-init t
        treemacs-space-between-root-nodes t
        treemacs-silent-refresh t
        treemacs-silent-filewatch t
        treemacs-file-event-delay 2000
        treemacs-file-follow-delay 0.2)

  :config
  ;; Don't follow cursor by default (toggle with SPC t f)
  (treemacs-follow-mode -1)
  (treemacs-filewatch-mode t)
  (treemacs-fringe-indicator-mode 'always)

  ;; Auto-add current project when opening treemacs
  (defun my/treemacs-project-setup ()
    "Automatically add current project to treemacs."
    (when-let* ((project (project-current))
                (root (project-root project)))
      (unless (treemacs-is-path root :in-workspace)
        (treemacs-do-add-project-to-workspace 
         root 
         (file-name-nondirectory (directory-file-name root))))))

  (add-hook 'treemacs-mode-hook #'my/treemacs-project-setup)

  ;; Git integration with fallback
  (when +treemacs-git-mode
    (when (and (memq +treemacs-git-mode '(deferred extended))
               (not treemacs-python-executable)
               (not (executable-find "python3")))
      (setq +treemacs-git-mode 'simple))
    (treemacs-git-mode +treemacs-git-mode)
    (setq treemacs-collapse-dirs
          (if (memq +treemacs-git-mode '(extended deferred)) 3 0))))

(use-package treemacs-nerd-icons
  :after treemacs
  :config
  (treemacs-load-theme "nerd-icons"))

(use-package treemacs-evil
  :after (treemacs evil)
  :config
  (evil-define-key 'normal treemacs-mode-map (kbd "TAB") 'treemacs-TAB-action))

(use-package treemacs-magit
  :after (treemacs magit))

(use-package treemacs-persp
  :after (treemacs persp-mode)
  :config
  (treemacs-set-scope-type 'Perspectives))
#+end_src

** Dired: File Manager
#+begin_src emacs-lisp
(use-package fd-dired
  :defer t
  :config
  (setq fd-dired-use-gnu-find-syntax t))

(use-package dired-open
  :defer t
  :config
  (setq dired-open-extensions '(("png" . "imv") 
                                ("jpg" . "imv") 
                                ("mp4" . "mpv")
                                ("mkv" . "mpv")
                                ("pdf" . "zathura"))))

(use-package dired
  :straight (:type built-in)
  :commands (dired dired-jump)
  :hook (dired-mode . dired-hide-dotfiles-mode)
  :custom
  (dired-listing-switches "-agho --group-directories-first")
  (dired-auto-revert-buffer t)
  (dired-dwim-target t)
  (dired-recursive-deletes 'always)
  (dired-recursive-copies 'always)
  (dired-kill-when-opening-new-dired-buffer t)
  
  :config
  ;; Evil keybindings for dired
  (evil-define-key 'normal dired-mode-map
    (kbd "h") 'dired-up-directory
    (kbd "l") 'dired-find-file
    (kbd "j") 'dired-next-line
    (kbd "k") 'dired-previous-line
    (kbd "G") 'dired-goto-file
    (kbd "gg") 'beginning-of-buffer
    (kbd "^") 'dired-up-directory
    (kbd "~") (lambda () (interactive) (dired "~"))
    (kbd "RET") 'dired-find-file
    (kbd "i") 'dired-maybe-insert-subdir
    (kbd "m") 'dired-mark
    (kbd "u") 'dired-unmark
    (kbd "U") 'dired-unmark-all-marks
    (kbd "t") 'dired-toggle-marks
    (kbd "C-n") 'dired-create-empty-file
    (kbd "C-d") 'dired-create-directory
    (kbd "R") 'dired-do-rename
    (kbd "D") 'dired-do-delete
    (kbd "C") 'dired-do-copy
    (kbd "X") 'dired-open-file
    (kbd "M") 'dired-do-chmod
    (kbd "O") 'dired-do-chown
    (kbd "s") 'dired-sort-toggle-or-edit
    (kbd "gh") 'dired-hide-dotfiles-mode
    (kbd "gr") 'revert-buffer))

(use-package dired-x
  :straight (:type built-in)
  :after dired
  :custom 
  (dired-x-hands-off-my-keys nil)
  :config
  (setq dired-omit-files "^\\.[^.]\\|^#\\|^\\.$\\|^\\.\\.$\\|\\.pyc$\\|\\.o$")
  (setq dired-omit-verbose nil))

(use-package dired-git-info
  :after dired
  :hook (dired-mode . dired-git-info-mode)
  :config
  (setq dgi-auto-hide-details-p nil))

(use-package nerd-icons-dired
  :straight t
  :hook (dired-mode . nerd-icons-dired-mode))

(use-package dired-ranger
  :straight t
  :after dired
  :config
  (define-key dired-mode-map (kbd "y") 'dired-ranger-copy)
  (define-key dired-mode-map (kbd "p") 'dired-ranger-paste)
  (define-key dired-mode-map (kbd "x") 'dired-ranger-move))

(use-package dired-hide-dotfiles
  :hook (dired-mode . dired-hide-dotfiles-mode))
#+end_src

** Project.el: Enhanced Project Management
#+begin_src emacs-lisp
(use-package project
  :straight (:type built-in)
  :custom
  (project-vc-extra-root-markers '(".envrc" ".project" "pyproject.toml" "Cargo.toml" ".git" "flake.nix"))
  (project-list-file (expand-file-name "projects" no-littering-var-directory))

  :config
  ;; Projects directory
  (defcustom my/projects-directory (expand-file-name "~/Projects/")
    "Base directory for all projects."
    :type 'directory
    :group 'project)

  (unless (file-directory-p my/projects-directory)
    (make-directory my/projects-directory t))

  ;; Auto-discover projects
  (defun my/project-discover-projects ()
    "Discover all projects in ~/Projects directory."
    (interactive)
    (let ((subdirs (directory-files my/projects-directory t "^[^.]")))
      (dolist (dir subdirs)
        (when (and (file-directory-p dir)
                   (or (file-exists-p (expand-file-name ".git" dir))
                       (file-exists-p (expand-file-name ".project" dir))
                       (file-exists-p (expand-file-name "flake.nix" dir))))
          (project-remember-project (project-current nil dir))))))

  (add-hook 'after-init-hook #'my/project-discover-projects)

  ;; Helper functions
  (defun my/project-bibliography-files ()
    "Get bibliography files for current project."
    (when-let* ((project (project-current))
                (root (project-root project)))
      (let ((bib-files '()))
        (dolist (file (directory-files root t "\\.bib\\'"))
          (push file bib-files))
        (let ((ref-dir (expand-file-name "references/" root)))
          (when (file-directory-p ref-dir)
            (dolist (file (directory-files ref-dir t "\\.bib\\'"))
              (push file bib-files))))
        (let ((bib-dir (expand-file-name "bib/" root)))
          (when (file-directory-p bib-dir)
            (dolist (file (directory-files bib-dir t "\\.bib\\'"))
              (push file bib-files))))
        (nreverse bib-files))))

  (defun my/project-find-file-by-extension (ext)
    "Find files with extension EXT in current project."
    (when-let* ((project (project-current))
                (files (project-files project))
                (filtered (seq-filter (lambda (f) (string-suffix-p ext f)) files)))
      (find-file (completing-read (format "Select %s file: " ext) filtered))))

  (defun my/project-find-bib-file ()
    "Open a .bib file in current project."
    (interactive)
    (if-let ((bib-files (my/project-bibliography-files)))
        (find-file (completing-read "Select bibliography: " bib-files))
      (message "No .bib files found in project")))

  (defun my/project-find-tex-file ()
    "Open a .tex file in current project."
    (interactive)
    (my/project-find-file-by-extension ".tex"))

  (defun my/project-find-py-file ()
    "Open a .py file in current project."
    (interactive)
    (my/project-find-file-by-extension ".py"))

  ;; Enhanced project switching to prefer ~/Projects
  (defun my/project-switch-project ()
    "Switch to a project, preferring those in ~/Projects."
    (interactive)
    (let* ((all-projects (project-known-project-roots))
           (projects-dir-projects
            (seq-filter (lambda (p) (string-prefix-p my/projects-directory p))
                       all-projects))
           (other-projects
            (seq-filter (lambda (p) (not (string-prefix-p my/projects-directory p)))
                       all-projects))
           (sorted-projects (append projects-dir-projects other-projects)))
      (project-switch-project (completing-read "Switch to project: " sorted-projects))))

  ;; Open treemacs when switching projects
  (defun my/project-switch-with-treemacs (orig-fun &rest args)
    "Open treemacs after switching projects."
    (let ((result (apply orig-fun args)))
      (when (and (fboundp 'treemacs)
                 (project-current))
        (treemacs-display-current-project-exclusively))
      result))

  (advice-add 'project-switch-project :around #'my/project-switch-with-treemacs))
#+end_src

** Bookmarks Enhancement
#+begin_src emacs-lisp
(use-package bookmark
  :straight (:type built-in)
  :custom
  (bookmark-default-file (expand-file-name "bookmarks" no-littering-var-directory))
  (bookmark-save-flag 1))
#+end_src

** Workspace & Project Utilities
#+begin_src emacs-lisp
(defun ar/kill-other-buffers ()
  "Kill all buffers except the current one."
  (interactive)
  (let ((current (current-buffer)))
    (dolist (buf (buffer-list))
      (unless (or (eq buf current)
                  (string-prefix-p " " (buffer-name buf))
                  (string-prefix-p "*" (buffer-name buf)))
        (kill-buffer buf)))))

(defun ar/save-all-buffers ()
  "Save all file-visiting buffers without prompting."
  (interactive)
  (save-some-buffers t))

(defun ar/switch-to-scratch ()
  "Switch to *scratch* buffer, creating if needed."
  (interactive)
  (switch-to-buffer "*scratch*"))

(defun ar/treemacs-toggle-and-follow ()
  "Toggle treemacs and follow current file."
  (interactive)
  (if (treemacs-is-treemacs-window-selected?)
      (delete-window)
    (progn
      (treemacs)
      (treemacs-find-file))))
#+end_src

** Keybindings
#+begin_src emacs-lisp
(ar/global-leader
 ;; File operations
 "f" '(:ignore t :wk "file")
 "f f" '(find-file :wk "find file")
 "f F" '(find-file-other-window :wk "find file (other window)")
 "f e" '(dired :wk "dired")
 "f E" '(dired-jump :wk "dired (jump to file)")
 "f r" '(consult-recent-file :wk "recent files")
 "f R" '(rename-file :wk "rename file")
 "f s" '(save-buffer :wk "save file")
 "f S" '(ar/save-all-buffers :wk "save all files")
 "f y" '((lambda () (interactive) (kill-new (buffer-file-name))) :wk "yank file path")
 "f t" '(treemacs :wk "toggle treemacs")
 "f T" '(ar/treemacs-toggle-and-follow :wk "treemacs find file")
 
 ;; Buffer operations
 "b" '(:ignore t :wk "buffers")
 "b b" '(consult-buffer :wk "switch buffer")
 "b B" '(switch-to-buffer-other-window :wk "switch (other window)")
 "b i" '(ibuffer :wk "ibuffer")
 "b k" '(kill-current-buffer :wk "kill buffer")
 "b K" '(ar/kill-other-buffers :wk "kill other buffers")
 "b n" '(next-buffer :wk "next buffer")
 "b p" '(previous-buffer :wk "previous buffer")
 "b r" '(revert-buffer :wk "revert buffer")
 "b R" '(rename-buffer :wk "rename buffer")
 "b s" '(ar/switch-to-scratch :wk "scratch buffer")
 "b S" '(save-buffer :wk "save buffer")
 "b m" '(bookmark-set :wk "set bookmark")
 "b M" '(consult-bookmark :wk "jump to bookmark")
 
 ;; Project operations
 "p" '(:ignore t :wk "project")
 "p p" '(my/project-switch-project :wk "switch project")
 "p f" '(project-find-file :wk "find file")
 "p F" '(project-find-regexp :wk "find regexp")
 "p d" '(project-find-dir :wk "find directory")
 "p D" '(project-dired :wk "dired root")
 "p b" '(project-switch-to-buffer :wk "switch buffer")
 "p k" '(project-kill-buffers :wk "kill buffers")
 "p c" '(project-compile :wk "compile")
 "p e" '(project-eshell :wk "eshell")
 "p s" '(project-find-regexp :wk "search (grep)")
 "p r" '(consult-ripgrep :wk "ripgrep")
 "p +" '(my/project-discover-projects :wk "discover projects")
 "p t" '(treemacs-display-current-project-exclusively :wk "treemacs project")
 "p B" '(my/project-find-bib-file :wk "find .bib")
 "p T" '(my/project-find-tex-file :wk "find .tex")
 "p y" '(my/project-find-py-file :wk "find .py")
 
 ;; Workspace (Perspective) operations
 "TAB" '(:ignore t :wk "workspace")
 "TAB TAB" '(persp-switch :wk "switch workspace")
 "TAB n" '(persp-next :wk "next workspace")
 "TAB p" '(persp-prev :wk "previous workspace")
 "TAB d" '(persp-kill :wk "delete workspace")
 "TAB r" '(persp-rename :wk "rename workspace")
 "TAB s" '(persp-save-state-to-file :wk "save workspaces")
 "TAB l" '(persp-load-state-from-file :wk "load workspaces")
 "TAB b" '(persp-switch-to-buffer :wk "switch to workspace buffer")
 "TAB k" '(persp-kill-buffer :wk "kill buffer from workspace")
 "TAB a" '(persp-add-buffer :wk "add buffer to workspace")
 "TAB A" '(persp-set-buffer :wk "move buffer to workspace")
 "TAB i" '(persp-import-buffers :wk "import buffers")
 "TAB 0" '((lambda () (interactive) (persp-switch "main")) :wk "switch to main"))
#+end_src
